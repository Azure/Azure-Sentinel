Parser:
  Title: Network Session ASIM parser for Azure Firewall logs
  Version: '0.2.0'
  LastUpdated: Dec 3, 2025
Product:
  Name: Azure Firewall
Normalization:
  Schema: NetworkSession
  Version: '0.2.6'
References:
  - Title: ASIM Network Session Schema
    Link: https://aka.ms/ASimNetworkSessionDoc
  - Title: ASIM
    Link: https://aka.ms/AboutASIM
Description: |
  This ASIM parser supports normalizing Azure Firewall logs to the ASIM Network Session normalized schema. This parser is partially based on a work by [Koos Goossens](https://github.com/TheCloudScout).
ParserName: ASimNetworkSessionAzureFirewall
EquivalentBuiltInParser: _ASim_NetworkSession_AzureFirewall
ParserParams:
  - Name: disabled
    Type: bool
    Default: false
ParserQuery: |
  let parser = (disabled:bool=false) {
      // AZFWIdpsSignature provides a numerical severity value
      let NumericalEventSeverityLookup = datatable (Severity:int, NumericalEventSeverity:string)
      [
        1, "High",
        2, "Medium",
        3, "Low",
        4, "Informational"
      ];
      let DvcActionLookup = datatable (UpperAction:string, DvcAction:string, EventResult: string, ActionEventSeverity: string)
      [
        "ALLOW", "Allow", "Success", "Informational",
        "",      "Allow", "Success", "Informational", // AZFWNatRule does not have Action, always indicates success
        "DENY",  "Deny",  "Failure", "Low",
        "DROP",  "Drop",  "Failure", "Low"
      ];
      let legacy_AzureFirewallNetworkRuleLogs = 
          AzureDiagnostics
          | where not(disabled)
          | where Category == "AzureFirewallNetworkRule"
          | where isnotempty(msg_s)
          | project msg_s, OperationName, SubscriptionId, ResourceId, TimeGenerated, Type, _ResourceId;
      let legacy_AzureFirewallSessionLogs = 
          legacy_AzureFirewallNetworkRuleLogs
          | where OperationName in ("AzureFirewallNetworkRuleLog","AzureFirewallThreatIntelLog")
          | parse-where
              msg_s with           Protocol:string 
              " request from "     SrcIpAddr:string
              ":"                  SrcPortNumber:int
              " to "               DstIpAddr:string
              ":"                  DstPortNumber:int
              ". Action: "         DvcAction:string
              "."                  *
          | project-away msg_s
          | extend EventSeverity = case (
              OperationName  == "AzureFirewallThreatIntelLog", "Medium",
              DvcAction == "Deny", "Low",
              "Informational")
          | extend EventResult = iff(DvcAction == "Allow", "Success", "Failure")
          ;
      let legacy_AzureFirewallNATLogs = 
          legacy_AzureFirewallNetworkRuleLogs
          | where OperationName == "AzureFirewallNatRuleLog"
          | parse-where
              msg_s with           Protocol:string 
              " request from "     SrcIpAddr:string
              ":"                  SrcPortNumber:int
              " to "               DstIpAddr:string
              ":"                  DstPortNumber:int
              " was DNAT'ed to "   DstNatIpAddr:string
              ":"                  DstNatPortNumber:int
          | project-away msg_s
          | extend EventSeverity = "Informational"
          | extend EventResult = "Success"
          | extend DvcAction = "Allow"
          ;
      let AZFWNetworkSessionLogs = 
            AZFWNetworkRule
            | union AZFWNatRule
            | union AZFWIdpsSignature
            | union AZFWThreatIntel
            | where not(disabled)
            | extend UpperAction = toupper(Action)
            // IDPS Signature and ThreatIntel tables contain alerts, exclude these to avoid duplicate network session logs
            | where UpperAction != "ALERT"
            | lookup DvcActionLookup on UpperAction
            // Determine Event Severity
            | lookup NumericalEventSeverityLookup on Severity
            | extend EventSeverity = iff(Type=="AZFWThreatIntel", "Medium", coalesce(NumericalEventSeverity, ActionEventSeverity))
            | project-rename
                EventOriginalUid = _ItemId,
                EventOriginalResultDetails = ActionReason,
                SrcIpAddr = SourceIp,
                SrcPortNumber = SourcePort,
                DstIpAddr = DestinationIp,
                DstPortNumber = DestinationPort,
                DstFQDN = Fqdn,
                DvcOriginalAction = Action,
                ThreatId = SignatureId,
                ThreatCategory = Category,
                EventReportUrl = TargetUrl,
                NetworkRuleName = Rule,
                DstNatIpAddr = TranslatedIp,
                DstNatPortNumber = TranslatedPort
            | extend
                ThreatName = coalesce(Description, ThreatDescription),
                AdditionalFields = bag_pack(
                    "Policy", Policy,
                    "RuleCollection", RuleCollection, 
                    "RuleCollectionGroup", RuleCollectionGroup,
                    "SourceSystem", SourceSystem,
                    "IsTlsInspected", IsTlsInspected
                )
            | project-away
                ThreatDescription;
      union legacy_AzureFirewallSessionLogs, legacy_AzureFirewallNATLogs, AZFWNetworkSessionLogs
      // Extract Network Information
      | extend temp_Protocol = toupper(Protocol)
      | extend NetworkProtocol = iff(temp_Protocol startswith "ICMP", "ICMP", temp_Protocol)
      | extend NetworkIcmpCode = iff(NetworkProtocol == "ICMP", toint(extract("(?i)TYPE=(\\d+)", 1, temp_Protocol)), int(null))
      | extend NetworkIcmpType = iff(isnotnull(NetworkIcmpCode), _ASIM_LookupICMPType(NetworkIcmpCode), "")
      | extend
          EventVendor="Microsoft",
          EventProduct="Azure Firewall",
          EventType="NetworkSession",
          EventCount=toint(1),
          EventSchemaVersion="0.2.6",
          EventSchema="NetworkSession",
          DvcIdType = "AzureResourceId"
      | extend 
          DvcId = coalesce(ResourceId, _ResourceId),
          DvcSubscriptionId = coalesce(SubscriptionId, _SubscriptionId)
      // -- Aliases
      | extend
          IpAddr = SrcIpAddr,
          Src = SrcIpAddr,
          SrcHostname = SrcIpAddr,
          Dst = DstIpAddr,
          DstHostname = DstIpAddr,
          Dvc = DvcId,
          Hostname = DstIpAddr,
          EventStartTime = TimeGenerated,
          EventEndTime = TimeGenerated
      | project-keep
          Src*,
          Dst*,
          Event*,
          Dvc*,
          IpAddr,
          Hostname,
          Network*,
          Threat*,
          Type,
          AdditionalFields,
          _BilledSize,
          _IsBillable,
          _ResourceId,
          _SubscriptionId,
          TenantId,
          TimeGenerated
  };
  parser (disabled)