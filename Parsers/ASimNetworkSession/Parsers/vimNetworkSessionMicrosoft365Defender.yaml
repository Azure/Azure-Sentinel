Parser:
  Title:  Network Session ASIM filtering parser for M365 Defender for Endpoint
  Version: '0.4'
  LastUpdated: May 1st 2023
Product:
  Name: M365 Defender for Endpoint
Normalization:
  Schema: NetworkSession
  Version: '0.2.3'
References:
- Title: ASIM Network Session Schema
  Link: https://aka.ms/ASimNetworkSessionDoc
- Title: ASIM
  Link: https:/aka.ms/MsASIM
Description: |
  This ASIM parser supports filtering and normalizing M365 Defender for Endpoint to the ASIM Network Session schema.
ParserName: vimNetworkSessionMicrosoft365Defender
EquivalentBuiltInParser: _Im_NetworkSession_Microsoft365Defender
ParserParams:
  - Name: starttime
    Type: datetime
    Default: datetime(null)
  - Name: endtime
    Type: datetime
    Default: datetime(null)
  - Name: srcipaddr_has_any_prefix
    Type: dynamic
    Default: dynamic([])
  - Name: dstipaddr_has_any_prefix
    Type: dynamic
    Default: dynamic([])
  - Name: ipaddr_has_any_prefix
    Type: dynamic
    Default: dynamic([])
  - Name: dstportnumber
    Type: int
    Default: int(null)
  - Name: hostname_has_any
    Type: dynamic
    Default: dynamic([])
  - Name: dvcaction
    Type: dynamic
    Default: dynamic([])
  - Name: eventresult
    Type: string
    Default: '*'
  - Name: disabled
    Type: bool
    Default: false
ParserQuery: |
  let M365Defender=
    (starttime:datetime=datetime(null)
    , endtime:datetime=datetime(null)
    , srcipaddr_has_any_prefix:dynamic=dynamic([])
    , dstipaddr_has_any_prefix:dynamic=dynamic([])
    , ipaddr_has_any_prefix:dynamic=dynamic([])
    , dstportnumber:int=int(null)
    , hostname_has_any:dynamic=dynamic([])
    , dvcaction:dynamic=dynamic([])
    , eventresult:string='*'
    , disabled:bool=false
   ){
  let DirectionLookup=datatable(ActionType:string,NetworkDirection:string,Outbound:boolean)[
    'ConnectionSuccess','Outbound', true
    ,'ConnectionFailed', 'Outbound', true
    ,'ConnectionRequest','Outbound', true
    ,'InboundConnectionAccepted', 'Inbound', false
    ,'ConnectionFound', 'Unknown', false
    ,'ListeningConnectionCreated', 'Listen', false 
  ];
  let src_or_any=set_union(srcipaddr_has_any_prefix, ipaddr_has_any_prefix); 
  let dst_or_any=set_union(dstipaddr_has_any_prefix, ipaddr_has_any_prefix);  
  // -- Common preprocessing to both input and outbound events
  let RawNetworkEvents = (select_outbound:boolean) {
    DeviceNetworkEvents 
    | where    (isnull(starttime) or TimeGenerated>=starttime) 
           and (isnull(endtime) or TimeGenerated<=endtime) 
    | where not(disabled)
    | lookup DirectionLookup on ActionType
    | where Outbound == select_outbound
    | project-away AppGuardContainerId, LocalIPType, MachineGroup, RemoteIPType, Timestamp // , SourceSystem, TenantId, 
    // --  Pre-filtering
    |where  (array_length(dvcaction)==0 )     /// if filtered by action return nothing
           and (isnull(dstportnumber) or dstportnumber == LocalPort or dstportnumber == RemotePort)
           and (array_length(hostname_has_any)==0 
              or RemoteUrl has_any(hostname_has_any) or DeviceName has_any(hostname_has_any)
              )
    | extend temp_isSrcMatch=( // only one of each pair has_any_ipv4_prefix is calculated
                    (Outbound and has_any_ipv4_prefix(LocalIP,src_or_any))
                    or
                    (not(Outbound) and has_any_ipv4_prefix(RemoteIP,src_or_any))
                    ) 
           , temp_isDstMatch=(
                    (not(Outbound) and has_any_ipv4_prefix(LocalIP,dst_or_any))
                    or
                    (Outbound and has_any_ipv4_prefix(RemoteIP,dst_or_any))
                    ) 
    | extend ASimMatchingIpAddr = case(
            array_length(src_or_any) == 0 and array_length(dst_or_any) == 0, "-" // match not requested: probably most common case
            , (temp_isSrcMatch and temp_isDstMatch), "Both" // has to be checked before the individual 
            , temp_isSrcMatch, "SrcIpAddr"
            , temp_isDstMatch, "DstIpAddr"
            , "No match"
    ) 
    | where ASimMatchingIpAddr != "No match"
    | project-away temp_*
    | extend EventResult = iff(ActionType=='ConnectionFailed','Failure','Success')
    | where (eventresult=='*' or EventResult==eventresult)
    // -- End of pre-filtering
    | extend
      // Event
      EventOriginalUid = tostring(ReportId),
      EventCount = int(1),
      EventProduct = 'M365 Defender for Endpoint',
      EventVendor = 'Microsoft',
      EventSchema = 'NetworkSession',
      EventSchemaVersion = '0.2.3',
      EventStartTime = TimeGenerated,
      EventEndTime = TimeGenerated,
      EventType = 'NetworkSession',
      EventSeverity = "Informational",
      DvcIdType = 'MDEid'
    | project-away 
        ReportId, Outbound
    | project-rename 
        EventOriginalResultDetails = ActionType
    | extend
      RemoteUrl = extract (@"(?:https?://)?(.*)", 1, RemoteUrl)
    | extend
      User = iff (InitiatingProcessAccountDomain == '', InitiatingProcessAccountName, strcat(InitiatingProcessAccountDomain, '\\', InitiatingProcessAccountName)),
      UsernameType = iff(InitiatingProcessAccountDomain == '','Simple', 'Windows'),
      SplitHostname = split(DeviceName,"."),
      SplitUrl = split(RemoteUrl,"."),
      NetworkProtocol = case (
        Protocol startswith "Tcp", "TCP",
        Protocol == "Unknown", "",
        toupper(Protocol)
      )
    | project-away Protocol
    | extend  
      DvcHostname = tostring(SplitHostname[0]),
      DvcDomain = tostring(strcat_array(array_slice(SplitHostname, 1, -1), '.')),
      DvcFQDN = iif (DeviceName contains ".", DeviceName, ""),
      UrlHostname = tostring(SplitUrl[0]),
      UrlDomain = tostring(strcat_array(array_slice(SplitUrl, 1, -1), '.')),
      UrlFQDN = iif(RemoteUrl contains ".", RemoteUrl, "")
    | project-away RemoteUrl, DeviceName
    | extend
      DvcDomainType = iif(DvcFQDN != "", "FQDN", ""),
      UrlDomainType = iff(UrlFQDN != "", "FQDN", ""),
      DvcIpAddr = LocalIP
    | extend
      Dvc = DvcHostname  
    | project-rename
      DvcId = DeviceId
    | project-away SplitUrl, SplitHostname
  };
  let OutboundNetworkEvents = 
    RawNetworkEvents (true)
    // *************** Postfilterring *****************************************************************
    | where (isnull(dstportnumber) or dstportnumber==RemotePort)
    // *************** /Postfilterring *****************************************************************
    | extend temp_isMatchSrcHostname=DvcHostname has_any(hostname_has_any)
           , temp_isMatchDstHostname=UrlHostname has_any(hostname_has_any)
    |extend ASimMatchingHostname = case(array_length(hostname_has_any) == 0 ,"-",
                                temp_isMatchDstHostname and temp_isMatchSrcHostname, "Both",
                                temp_isMatchDstHostname, "DstHostname",
                                temp_isMatchSrcHostname, "SrcHostname",
                                  "No match"
                              )
    | project-away temp*
    | where ASimMatchingHostname != "No match"
    | project-rename
       DstIpAddr = RemoteIP,
       SrcIpAddr = LocalIP,
       DstPortNumber = RemotePort,
       SrcPortNumber = LocalPort,
       SrcUsernameType = UsernameType,
       SrcUserAadId = InitiatingProcessAccountObjectId,
       SrcUserId = InitiatingProcessAccountSid,
       SrcUserUpn = InitiatingProcessAccountUpn
    | extend
       SrcUsername = User,
       SrcDvcId = DvcId,
       SrcDvcIdType = 'MDEid',
       SrcUserIdType = iff (SrcUserId <> "S-1-0-0", "SID", ""),
       SrcUserId = iff (SrcUserId <> "S-1-0-0", SrcUserId, ""),
       DstHostname = UrlHostname
    | project-rename
       DstDomain = UrlDomain,
       DstFQDN = UrlFQDN,
       DstDomainType = UrlDomainType
    | extend  
       SrcHostname = DvcHostname,
       SrcDomain = DvcDomain,
       SrcFQDN = DvcFQDN,
       SrcDomainType = DvcDomainType
    // Processes
    | extend
       SrcProcessId = tostring(InitiatingProcessId),
       ParentProcessId = tostring(InitiatingProcessParentId)
    | project-rename
       SrcProcessName = InitiatingProcessFileName,
       SrcProcessCommandLine = InitiatingProcessCommandLine,
       SrcProcessCreationTime = InitiatingProcessCreationTime,
       SrcProcessIntegrityLevel = InitiatingProcessIntegrityLevel,
       SrcProcessTokenElevation = InitiatingProcessTokenElevation,
       ParentProcessName = InitiatingProcessParentFileName,
       ParentProcessCreationTime = InitiatingProcessParentCreationTime
    | extend
       Process = SrcProcessName,
       ProcessId = SrcProcessId,
       SrcAppName = SrcProcessName,
       SrcAppType = "Process"
  ;
  let InboundNetworkEvents = 
    RawNetworkEvents (false)
    // *************** Postfilterring *****************************************************************
    | where (isnull(dstportnumber) or dstportnumber==LocalPort)
    // *************** /Postfilterring *****************************************************************
      |extend ASimMatchingHostname = case(array_length(hostname_has_any) == 0 ,"",
                                  UrlHostname has_any(hostname_has_any), "SrcHostname",
                                  DvcHostname has_any(hostname_has_any), "DstHostname",
                                  "No match"
                              )
    | where ASimMatchingHostname != "No match"
    | project-rename
       SrcIpAddr = RemoteIP,
       DstIpAddr = LocalIP,
       SrcPortNumber = RemotePort,
       DstPortNumber = LocalPort,
       DstUsernameType = UsernameType,
       DstUserAadId = InitiatingProcessAccountObjectId,
       DstUserId = InitiatingProcessAccountSid,
       DstUserUpn = InitiatingProcessAccountUpn,
       SrcDomain = UrlDomain,
       SrcFQDN = UrlFQDN,
       SrcDomainType = UrlDomainType
    | extend
       DstUsername = User,
       DstDvcId = DvcId,
       DstDvcIdType = 'MDEid',
       DstUserIdType = 'SID',
       SrcHostname = UrlHostname
    | extend  
       DstHostname = DvcHostname,
       DstDomain = DvcDomain,
       DstFQDN = DvcFQDN
    // Processes
    | extend
       DstProcessId = tostring(InitiatingProcessId),
       ParentProcessId = tostring(InitiatingProcessParentId)
    | project-rename
       DstProcessName = InitiatingProcessFileName,
       DstProcessCommandLine = InitiatingProcessCommandLine,
       DstProcessCreationTime = InitiatingProcessCreationTime,
       DstProcessIntegrityLevel = InitiatingProcessIntegrityLevel,
       DstProcessTokenElevation = InitiatingProcessTokenElevation,
       ParentProcessName = InitiatingProcessParentFileName,
       ParentProcessCreationTime = InitiatingProcessParentCreationTime
    | extend
       Process = DstProcessName,
       DstAppName = DstProcessName,
       DstAppType = "Process"
  ;
  union InboundNetworkEvents, OutboundNetworkEvents
  | project-rename 
     Hostname = UrlHostname
  | extend // aliases
     IpAddr = SrcIpAddr,
     Src = SrcIpAddr,
     Dst = DstIpAddr 
  };
  M365Defender (starttime, endtime, srcipaddr_has_any_prefix, dstipaddr_has_any_prefix, ipaddr_has_any_prefix, dstportnumber, hostname_has_any, dvcaction, eventresult, disabled)
