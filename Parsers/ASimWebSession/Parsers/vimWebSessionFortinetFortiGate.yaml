Parser:
  Title: Web Session ASIM filtering parser for Fortinet FortiGate
  Version: '0.3.0'
  LastUpdated: Sep 12, 2025
Product:
  Name: Fortinet FortiGate
Normalization:
  Schema: WebSession
  Version: '0.2.6'
References:
  - Title: ASIM Web Session Schema
    Link: https://aka.ms/ASimWebSessionDoc
  - Title: ASIM
    Link: https://aka.ms/AboutASIM
  - Title: web log fields
    Link: https://docs.fortinet.com/document/fortigate/7.4.0/fortios-log-message-reference/400992
  - Title: Fortinet FortiGate CEF setup
    Link: https://community.fortinet.com/t5/FortiGate/Technical-Tip-Integrate-FortiGate-with-Microsoft-Sentinel/ta-p/199709
  - Title: Fortinet FortiGate syslogd documentation
    Link: https://docs.fortinet.com/document/fortigate/7.2.0/cli-reference/450620/config-log-syslogd-setting
  - Title: Fortinet FortiGate extended logging for user agent, http method and x-forwarded-for
    Link: https://docs.fortinet.com/document/fortigate/7.4.0/fortios-log-message-reference/496081/enabling-extended-logging
Description: |
  This ASIM parser supports filtering and normalizing Fortinet FortiGate logs produced by the Microsoft Sentinel Fortinet connector to the ASIM Web Session normalized schema.
ParserName: vimWebSessionFortinetFortiGate                                  
EquivalentBuiltInParser: _Im_WebSession_FortinetFortiGate                                  
ParserParams:
  - Name: starttime
    Type: datetime
    Default: datetime(null)
  - Name: endtime
    Type: datetime
    Default: datetime(null)
  - Name: srcipaddr_has_any_prefix
    Type: dynamic
    Default: dynamic([])
  - Name: ipaddr_has_any_prefix
    Type: dynamic
    Default: dynamic([])
  - Name: url_has_any
    Type: dynamic
    Default: dynamic([])
  - Name: httpuseragent_has_any
    Type: dynamic 
    Default: dynamic([])
  - Name: eventresultdetails_in
    Type: dynamic 
    Default: dynamic([])
  - Name: eventresult
    Type: string
    Default: '*'
  - Name: disabled
    Type: bool
    Default: false
ParserQuery: |
  let parser=(
    starttime:datetime               = datetime(null), 
    endtime:datetime                 = datetime(null),
    srcipaddr_has_any_prefix:dynamic = dynamic([]),
    ipaddr_has_any_prefix:dynamic    = dynamic([]), 
    url_has_any:dynamic              = dynamic([]),
    httpuseragent_has_any:dynamic    = dynamic([]),
    eventresultdetails_in:dynamic    = dynamic([]),
    eventresult:string               = '*',
    disabled:bool                    = false
  ){
    let src_or_any                = set_union(
      srcipaddr_has_any_prefix,
      ipaddr_has_any_prefix
    ); 
    let remove_protocol_from_list = (list:dynamic)
    {
        print list
        | mv-apply l = print_0 to typeof(string) on
          (extend l = iff( l has @"//", substring(l, indexof(l, @'//') + 2), l ))
        | project l
    };
    let EventLookup=datatable(DeviceAction:string,DvcAction:string,EventResult:string)
    [
        "passthrough","Allow","Success"
        , "blocked","Deny","Failure"
    ];
    // -- See https://docs.fortinet.com/document/fortigate/7.2.4/fortios-log-message-reference/671442/cef-priority-levels
    let SeverityLookup = datatable (EventOriginalSeverity:string, EventSeverity:string)
    [
      "1", "Informational", // Debug
      "2", "Informational", // Information
      "3", "Informational", // Notification
      "4", "Low", // Warning
      "5", "Low", // Error
      "6", "High", // Critical
      "7", "Medium", // Alert
      "8", "High" // Emergency
    ];     
    CommonSecurityLog
    | where not(disabled)
    | where (isnull(starttime) or TimeGenerated>=starttime) and (isnull(endtime) or TimeGenerated<=endtime)
    | where DeviceVendor == "Fortinet" 
      and DeviceProduct  startswith "Fortigate"
      and (Activity has_all ('webfilter', 'utm') or Activity has_all ('ips', 'utm'))
    | where (array_length(url_has_any) == 0 or RequestURL has_any (remove_protocol_from_list(url_has_any)))
    | where (array_length(httpuseragent_has_any) == 0 or AdditionalExtensions has_any(httpuseragent_has_any))
    | extend temp_SrcMatch = has_any_ipv4_prefix(SourceIP,src_or_any)
    | extend temp_DstMatch = has_any_ipv4_prefix(DestinationIP,ipaddr_has_any_prefix)
    | extend ASimMatchingIpAddr = case(
        array_length(src_or_any) == 0, "-",
        temp_DstMatch and temp_SrcMatch, "Both",
        temp_SrcMatch , "SrcIpAddr",
        temp_DstMatch, "DstIpAddr",
        "No match") 
    | where ASimMatchingIpAddr != "No match" 
    | project-away temp_*
    | where (array_length(eventresultdetails_in) == 0) 
    | lookup EventLookup on DeviceAction 
    | where (eventresult == '*' or EventResult =~ eventresult)
    | project Activity,AdditionalExtensions,DestinationIP,DestinationPort,DeviceAction,DeviceInboundInterface,DeviceOutboundInterface,DeviceProduct,DeviceVersion,LogSeverity,Protocol,ReceivedBytes,SentBytes,SourceIP,SourcePort,TimeGenerated, DeviceExternalID, Type, _ItemId, Computer, EventResult, DvcAction, RequestURL, RequestContext, DestinationHostName, SourceHostName, SourceUserName, DestinationUserName, ASimMatchingIpAddr , ApplicationProtocol
    | project-rename 
      Url                     = RequestURL
      , UrlCategory           = RequestContext
      , DstBytes              = ReceivedBytes
      , DstInterfaceName      = DeviceOutboundInterface
      , DstIpAddr             = DestinationIP
      , DstPortNumber         = DestinationPort
      , DvcHostname           = Computer
      , EventMessage          = Activity
      , EventOriginalSeverity = LogSeverity
      , EventProductVersion   = DeviceVersion
      , SrcBytes              = SentBytes
      , SrcInterfaceName      = DeviceInboundInterface
      , SrcIpAddr             = SourceIP
      , SrcPortNumber         = SourcePort
      , DvcId                 = DeviceExternalID
      , EventUid              = _ItemId
      , DstHostname           = DestinationHostName
      , SrcHostname           = SourceHostName
      , SrcUsername           = SourceUserName
      , DstUsername           = DestinationUserName
      , NetworkApplicationProtocol = ApplicationProtocol
    | invoke _ASIM_ResolveNetworkProtocol ('Protocol')
    | extend 
        DstUsernameType = _ASIM_GetUsernameType(DstUsername),
        SrcUsernameType = _ASIM_GetUsernameType(SrcUsername)
    | project-rename DvcOriginalAction = DeviceAction
    | parse-kv AdditionalExtensions as (
          start:datetime,
          srcintfrole:string,
          dstintfrole:string,
          externalID:string,
          policyid:int,
          dstcountry:string,
          srccountry:string,
          crscore:string,
          duration:int,
          sentpkt:long,
          rcvdpkt:long,
          ['ad.referralurl']:string,
          ['ad.httpmethod']:string,
          ['ad.agent']:string,
          deviceSeverity:string,
          ref:string,
          msg:string,
          profile:string,
          incidentserialno:string,
          policytype:string,
          attack:string,
          attackid:string,
          logid:string,
          direction:string,
          subtype:string,
          severity: string,
          vd:string,
          FTNTFGTattack:string,
          FTNTFGTattackid:string,
          FTNTFGTlogid:string,
          FTNTFGTsubtype:string,
          FTNTFGTvd:string,
          FTNTFGTstart:datetime,
          FTNTFGTsrcintfrole:string,
          FTNTFGTdstintfrole:string,
          FTNTFGTexternalID:string,
          FTNTFGTpolicyid:int,
          FTNTFGTdstcountry:string,
          FTNTFGTsrccountry:string,
          FTNTFGTcrscore:string,
          FTNTFGTduration:int,
          FTNTFGTsentpkt:long,
          FTNTFGTrcvdpkt:long
      ) with (pair_delimiter=';', kv_delimiter='=')
    | parse AdditionalExtensions with * "x-forwarded-for=" HttpRequestXff:string ";" *
    | project-rename 
      HttpReferrer              =  ['ad.referralurl'],
      HttpRequestMethod         = ['ad.httpmethod'],
      HttpUserAgent             = ['ad.agent']
    | extend 
      ThreatName              = coalesce(attack,FTNTFGTattack),
      ThreatId                = coalesce(attackid,FTNTFGTattackid),
      EventOriginalUid        = coalesce(logid,FTNTFGTlogid),
      EventOriginalSubType    = coalesce(subtype,FTNTFGTsubtype),
      EventStartTime          = coalesce(start,FTNTFGTstart),
      SrcZone                 = coalesce(srcintfrole,FTNTFGTsrcintfrole),
      DstZone                 = coalesce(dstintfrole,FTNTFGTdstintfrole),
      NetworkSessionId        = coalesce(externalID,FTNTFGTexternalID),
      RuleNumber              = coalesce(policyid,FTNTFGTpolicyid),
      NetworkDuration         = coalesce(duration, FTNTFGTduration),
      DstGeoCountry           = coalesce(dstcountry,FTNTFGTdstcountry),
      SrcGeoCountry           = coalesce(srccountry,FTNTFGTsrccountry),
      ThreatOriginalRiskLevel = coalesce(crscore,FTNTFGTcrscore),
      SrcPackets              = coalesce(sentpkt,FTNTFGTsentpkt),
      DstPackets              = coalesce(rcvdpkt,FTNTFGTrcvdpkt)
    | project-away FTNTFGT*, attack, attackid, logid, subtype, start, srcintfrole, dstintfrole, externalID, policyid, dstcountry, srccountry, crscore, duration, sentpkt, rcvdpkt
    | parse AdditionalExtensions with * "Method=" temp_HttpRequestMethod "|User-Agent=" temp_HttpUserAgent ";" *
    | extend 
        HttpRequestMethod = coalesce(temp_HttpRequestMethod,HttpRequestMethod),
        HttpUserAgent = coalesce(temp_HttpUserAgent,HttpUserAgent)
    | project-away temp_*
    | extend AdditionalFields =  bag_pack(
                                "incidentserialno",incidentserialno, 
                                "policytype",policytype,
                                "profile",profile,
                                "ref",ref,
                                "vd",vd,
                                "Message",msg,
                                "deviceSeverity", deviceSeverity,
                                "direction", direction)
    | extend 
      EventCount               = int(1)
      , EventSchema            = "WebSession"
      , EventSchemaVersion     = "0.2.6"
      , EventType              = "HTTPsession"
      , EventVendor            = "Fortinet"
      , EventProduct = "Fortigate"
      , DvcIdType              = "Other"
      , NetworkBytes           = DstBytes + SrcBytes
      , EventEndTime           = TimeGenerated
      , EventStartTime         = coalesce(EventStartTime, TimeGenerated)
      , NetworkProtocolVersion = case(DstIpAddr contains ".", "IPv4"
          , DstIpAddr contains ":", "IPv6"
          , "")
      , NetworkPackets         = DstPackets + SrcPackets
      , UserAgent              = HttpUserAgent
      , Dvc                    = DvcHostname
      , User                   = SrcUsername
      , Hostname               = DstHostname
    | lookup SeverityLookup on EventOriginalSeverity
    | extend ipsseverity = case(
        severity == "low", "Low",
        severity == "high", "High",
        severity == "medium", "Medium",
        severity == "info", "Informational",
        ""
        )
    | extend EventSeverity = iff(EventMessage has_all ('ips', 'utm'), ipsseverity, EventSeverity)
    | extend 
        Src       = SrcIpAddr,
        Dst       = DstIpAddr,
        SessionId = NetworkSessionId,
        IpAddr    = SrcIpAddr,
        Duration  = NetworkDuration,
        Rule      = tostring(RuleNumber)
    | extend NetworkDirection = case(
        direction == "incoming", "Inbound",
        direction == "outgoing", "Outbound",
        "")
    | project-away Protocol, AdditionalExtensions, NetworkProtocolNumber, severity, incidentserialno, policytype, profile, ref, msg, deviceSeverity, direction, vd, ipsseverity
  };
  parser (starttime=starttime, endtime=endtime, srcipaddr_has_any_prefix=srcipaddr_has_any_prefix, ipaddr_has_any_prefix=ipaddr_has_any_prefix, url_has_any=url_has_any, httpuseragent_has_any=httpuseragent_has_any, eventresultdetails_in=eventresultdetails_in, eventresult=eventresult, disabled=disabled)
