// GitHub Enterprise Audit Entry Data Parser
// Last Updated Date: Jun 7, 2020
//
//This parser parses GitHub Enterprise Audit Entry extract the infromation from their various components. It is assumed that the playbook to ingest audit entry data into Sentinel is enabled
//
// Parser Notes:
// 1. This parser assumes logs are collected into a custom log table entitled GitHub_CL
//
// Usage Instruction : 
// Paste below query in log analytics, click on Save button and select as Function from drop down by specifying function name and alias. 
// To work with pre-built GitHub queries this Function should be given the alias of GitHubAudit.
// Functions usually takes 10-15 minutes to activate. You can then use function alias from any other queries (e.g. GitHubAudit | take 10).
//
// References : 
// Using functions in Azure monitor log queries : https://docs.microsoft.com/azure/azure-monitor/log-query/functions
// Tech Community Blog on KQL Functions : https://techcommunity.microsoft.com/t5/Azure-Sentinel/Using-KQL-functions-to-speed-up-analysis-in-Azure-Sentinel/ba-p/712381
// Tech Community Blog on GitHub data: <>
//
//

GitHub_CL  
| extend AuditEntries = todynamic(parse_json(value_s)) 
| mv-expand AuditEntries 
| evaluate bag_unpack(AuditEntries) 
| project Organization=tostring(parse_json(node).organizationName),
    Action=tostring(parse_json(node).action),
    OperationType=tostring(parse_json(node).operationType),
    Repository=tostring(parse_json(node).repositoryName),
    Actor=tostring(parse_json(node).actorLogin),    
    IPaddress=tostring(parse_json(node).actorIp),
    City=tostring(parse_json(node).actorLocation.city),
    Country=tostring(parse_json(node).actorLocation.country),
    ImpactedUser=tostring(parse_json(node).userLogin),
    ImpactedUserEmail=tostring(parse_json(node).user.email),
    InvitedUserPermission=tostring(parse_json(node).permission),
    Visibility=tostring(parse_json(node).visibility),
    TeamName=tostring(parse_json(node).teamName), TimeGenerated



