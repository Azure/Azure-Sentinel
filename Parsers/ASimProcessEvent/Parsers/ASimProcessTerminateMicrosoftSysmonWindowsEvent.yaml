Parser:
  Title: Process Terminate Event ASIM parser for Microsoft Windows Security Events
  Version: '0.4.1'
  LastUpdated: Jun 18, 2024
Product:
  Name: Microsoft Windows Events Sysmon
Normalization:
  Schema: ProcessEvent
  Version: '0.1.4'
References:
- Title: ASIM Process Schema
  Link: https://aka.ms/ASimProcessEventDoc
- Title: ASIM
  Link: https://aka.ms/AboutASIM
Description: |
  This ASIM parser supports normalizing Microsoft Windows Sysmon process terminate events (event 5) collected using the Event or WEF (WindowsEvent table) connectors logs ingested in 'WindowsEvent' table to the ASIM Process Event normalized schema. 
ParserName: ASimProcessEventTerminateMicrosoftSysmonWindowsEvent
EquivalentBuiltInParser: _ASim_ProcessEvent_TerminateMicrosoftSysmonWindowsEvent
ParserParams:
  - Name: disabled
    Type: bool
    Default: false
ParserQuery: | 
    let parser = (disabled:bool = false) {
        let parser_WindowsEvent=
            WindowsEvent
            | where not(disabled)
            | where Provider == "Microsoft-Windows-Sysmon" and EventID == 5
            | extend
                EventProduct = "Security Events",
                ActorUsername = tostring(EventData.User),
                TargetProcessName = tostring(EventData.Image),
                TargetProcessId = tostring(EventData.ProcessId),
                TargetProcessGuid = tostring(EventData.ProcessGuid)
            | project-rename
                DvcHostname = Computer,
                EventOriginalUid = EventOriginId
            | project-away Channel, Data, EventData, EventLevelName, EventLevel, ManagementGroupName, Provider, RawEventData, SourceSystem, Task, TenantId,Correlation,EventRecordId,Keywords,Opcode,SystemProcessId,SystemThreadId,SystemUserId,TimeCreated,Version,_ResourceId
        | extend  
            EventType = "ProcessTerminated",
            EventStartTime = todatetime(TimeGenerated),
            EventEndTime = todatetime(TimeGenerated),
            EventCount = int(1),
            EventVendor = "Microsoft",
            EventSchemaVersion = "0.1.0",
            EventSchema = 'ProcessEvent',
            EventOriginalType=tostring(EventID),
            EventResult = 'Success',
            DvcOs = "Windows",
            ActorUsernameType = iff(isnotempty(ActorUsername),'Windows', ''),
            // -- Aliases 
            User = ActorUsername,
            Process = TargetProcessName,
            Dvc = DvcHostname
        | project-away EventID
        ;
        parser_WindowsEvent
    };
    parser (disabled = disabled) 