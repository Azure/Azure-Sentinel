Parser:
  Title: Authentication ASIM filtering parser for M365 Defender Device Logon Events
  Version: '0.2.0'
  LastUpdated: Jan 14, 2026
Product:
  Name: M365 Defender for EndPoint
Normalization:
  Schema: Authentication
  Version: '0.1.3'
References:
- Title: ASIM Authentication Schema
  Link: https://aka.ms/ASimAuthenticationDoc 
Description: |
  This ASIM parser supports filtering and normalizing endpoint authentication events, collected  by Microsoft 365 Defender for Endpoint, stored in the  DeviceLogonEvents table, to the ASIM Authentication schema.
ParserName: vimAuthenticationM365Defender
EquivalentBuiltInParser: _Im_Authentication_M365Defender
ParserParams:
  - Name: starttime
    Type: datetime
    Default: datetime(null)
  - Name: endtime
    Type: datetime
    Default: datetime(null)
  - Name: username_has_any
    Type: dynamic
    Default: dynamic([])
  - Name: targetappname_has_any
    Type: dynamic
    Default: dynamic([])
  - Name: srcipaddr_has_any_prefix
    Type: dynamic
    Default: dynamic([])
  - Name: srchostname_has_any
    Type: dynamic
    Default: dynamic([])
  - Name: eventtype_in
    Type: dynamic
    Default: dynamic([])
  - Name: eventresultdetails_in
    Type: dynamic
    Default: dynamic([])
  - Name: eventresult
    Type: string
    Default: '*'
  - Name: disabled
    Type: bool
    Default: false
ParserQuery: |
  let EventResultDetailsLookup=datatable(EventOriginalResultDetails:string, EventResultDetails:string)
  [
    'InvalidUserNameOrPassword','No such user or password'
  ];
  let EventSubTypeLookup = datatable (EventOriginalType:string, EventSubType:string)
  [ 
    'Batch',                            'Service',
    'CachedInteractive',                'Interactive',
    'Interactive',                      'Interactive',
    'Network',                          'Remote',
    'Remote interactive (RDP) logons',  'RemoteInteractive',
    'RemoteInteractive',                'RemoteInteractive',
    'Service',                          'Service',
    'Unknown',                          ''
  ];
  let EventResultLookup = datatable (ActionType:string, EventResult:string)
  [ 
    'LogonAttempted', 'NA',
    'LogonFailed',    'Failure',
    'LogonSuccess',   'Success'
  ];
  let parser = (
      starttime: datetime=datetime(null), 
      endtime: datetime=datetime(null), 
      username_has_any: dynamic = dynamic([]),
      targetappname_has_any: dynamic = dynamic([]),
      srcipaddr_has_any_prefix: dynamic = dynamic([]),
      srchostname_has_any: dynamic = dynamic([]),
      eventtype_in: dynamic = dynamic([]),
      eventresultdetails_in: dynamic = dynamic([]),
      eventresult: string = '*',
      disabled: bool=false)
  {
      let UnixDeviceLogonEvents = (disabled: bool=false)
      {
          DeviceLogonEvents  
          | where not(disabled)
          // -- prefilter
          | where
              (isnull(starttime) or Timestamp >= starttime) 
              and (isnull(endtime) or Timestamp <= endtime) 
              and ((array_length(username_has_any) == 0) or (InitiatingProcessAccountName has_any (username_has_any)) or AccountName has_any (username_has_any))
              and (array_length(targetappname_has_any) == 0) // TargetAppName not available in source
              and ((array_length(srcipaddr_has_any_prefix) == 0) or has_any_ipv4_prefix(RemoteIP, srcipaddr_has_any_prefix))
              and ((array_length(srchostname_has_any) == 0) or (RemoteDeviceName has_any (srchostname_has_any)))
              and ((array_length(eventtype_in) == 0) or "Logon" in~ (eventtype_in))
          // eventresultdetails_in filtering done later in the parser
          // eventresult filtering done later in the parser
          // -- end prefilter
          | where InitiatingProcessFolderPath startswith "/"
          | where InitiatingProcessFolderPath startswith "/"
      | extend 
          ActorUsernameType  = "Simple",
          TargetDvcOs        = "Linux",
          TargetUsernameType = "Simple"
      | project-rename 
          ActingProcessName  = InitiatingProcessFolderPath,
          ActorUsername      = InitiatingProcessAccountName,
          TargetUsername     = AccountName
      | project-away 
          InitiatingProcessAccountSid, AccountDomain, InitiatingProcessAccountDomain, InitiatingProcessFileName, AccountSid
      };
      let WindowsDeviceLogonEvents = (disabled: bool=false)
      {
          DeviceLogonEvents
          | where not(disabled)
          // -- prefilter
          | where
              (isnull(starttime) or Timestamp >= starttime) 
              and (isnull(endtime) or Timestamp <= endtime) 
              and ((array_length(username_has_any) == 0) or (AccountName has_any (username_has_any)) or (AccountDomain has_any (username_has_any)) or (strcat(AccountDomain, '\\', AccountName) has_any (username_has_any)) or (InitiatingProcessAccountName has_any (username_has_any)) or (InitiatingProcessAccountDomain has_any (username_has_any)) or (strcat(InitiatingProcessAccountDomain, '\\', InitiatingProcessAccountName) has_any (username_has_any)))
              and (array_length(targetappname_has_any) == 0) // TargetAppName not available in source
              and ((array_length(srcipaddr_has_any_prefix) == 0) or has_any_ipv4_prefix(RemoteIP, srcipaddr_has_any_prefix))
              and ((array_length(srchostname_has_any) == 0) or (RemoteDeviceName has_any (srchostname_has_any)))
              and ((array_length(eventtype_in) == 0) or "Logon" in~ (eventtype_in))
          // eventresultdetails_in filtering done later in the parser
          // eventresult filtering done later in the parser
          // -- end prefilter
          | where InitiatingProcessFolderPath !startswith "/"
          | extend 
          ActingProcessName           = strcat (InitiatingProcessFolderPath,'\\',InitiatingProcessFileName),
          ActorUserIdType             = 'SID',
          ActorUsername               = case (
              isempty(InitiatingProcessAccountName), "",
              isempty(InitiatingProcessAccountDomain), InitiatingProcessAccountName,
              strcat(InitiatingProcessAccountDomain, '\\', InitiatingProcessAccountName)
          ),
          ActorUsernameType           = iff (
              InitiatingProcessAccountDomain == '','Simple',
              'Windows'
          ),
          TargetDvcOs = "Windows",
          TargetUserIdType            = 'SID',
          TargetUsername              = iff (
              isempty(AccountDomain), AccountName,
              strcat(AccountDomain, '\\', AccountName)
          ),
          TargetUsernameType          = iff (AccountDomain == '','Simple', 'Windows')
      | project-rename 
          ActorUserId        = InitiatingProcessAccountSid,
          TargetUserId       = AccountSid
      // -- Specific identifiers aliases
      | extend 
          TargetUserSid         = TargetUserId,
          ActorUserSid          = ActorUserId,
          TargetWindowsUsername = TargetUsername,
          ActorWindowsUsername  = ActorUsername,
          ActorUserType         = _ASIM_GetWindowsUserType (ActorUsername, ActorUserId)
      | extend 
          TargetUserType = iff(IsLocalAdmin, 
              'Admin',
              _ASIM_GetWindowsUserType (TargetWindowsUsername, TargetUserSid)
          )
      | project-away InitiatingProcessAccountName, InitiatingProcessAccountDomain, AccountDomain, AccountName, InitiatingProcessFolderPath, InitiatingProcessFileName
      };
      union 
          WindowsDeviceLogonEvents (disabled=disabled),
          UnixDeviceLogonEvents (disabled=disabled)
      // mapping ASimMatchingUsername
      | extend
          temp_isMatchTargetUsername=TargetUsername has_any(username_has_any)
          ,
          temp_isMatchActorUsername=ActorUsername has_any(username_has_any)
      | extend ASimMatchingUsername = case
                                  (
                                      array_length(username_has_any) == 0,
                                      "-",
                                      temp_isMatchTargetUsername and temp_isMatchActorUsername,
                                      "Both",
                                      temp_isMatchTargetUsername,
                                      "TargetUsername",
                                      temp_isMatchActorUsername,
                                      "ActorUsername",
                                      "No match"
                                  )
      | extend EventUid = columnifexists('_ItemId', "")
      | project-rename 
          ActingProcessCommandLine    = InitiatingProcessCommandLine,
          ActingProcessCreationTime   = InitiatingProcessCreationTime,
          ActingProcessIntegrityLevel = InitiatingProcessIntegrityLevel,
          ActingProcessMD5            = InitiatingProcessMD5,
          ActingProcessSHA1           = InitiatingProcessSHA1 ,
          ActingProcessSHA256         = InitiatingProcessSHA256,
          ActingProcessTokenElevation = InitiatingProcessTokenElevation,
          ActorUserAadId              = InitiatingProcessAccountObjectId,
          ActorUserUpn                = InitiatingProcessAccountUpn,
          EventOriginalResultDetails  = FailureReason,
          EventOriginalType           = LogonType,
          LogonProtocol               = Protocol,
          ParentProcessCreationTime   = InitiatingProcessParentCreationTime,
          ParentProcessName           = InitiatingProcessParentFileName,
          SrcHostname                 = RemoteDeviceName,
          SrcPortNumber               = RemotePort,
          TargetDvcId                 = DeviceId
      | extend 
          ActingProcessId             = tostring (InitiatingProcessId),
          EventCount                  = int(1),
          EventEndTime                = Timestamp,
          EventOriginalUid            = tostring (ReportId),
          EventProduct                = 'M365 Defender for EndPoint',
          EventSchema                 = 'Authentication',
          EventSchemaVersion          = '0.1.3',
          EventStartTime              = Timestamp,
          EventType                   = 'Logon',
          EventVendor                 = 'Microsoft',
          ParentProcessId             = tostring (InitiatingProcessParentId),
          SrcIpAddr                   = iff (RemoteIP == '-', '', RemoteIP),
          TargetDvcIdType             = 'MDEid',
          TargetSessionId             = tostring (LogonId),
          AdditionalFields            = todynamic(AdditionalFields),
          Type                        = "DeviceLogonEvents"
      | extend
              Hash = coalesce(
              ActingProcessMD5,
              ActingProcessSHA1,
              ActingProcessSHA256
              )
      | extend
          HashType = tostring(dynamic(["SHA256", "SHA1", "MD5"])[array_index_of(pack_array(ActingProcessSHA256, ActingProcessSHA1, ActingProcessMD5),Hash)])     
      | invoke _ASIM_ResolveFQDN('DeviceName')
      | project-rename 
          TargetDomain       = Domain, 
          TargetDomainType   = DomainType,
          TargetFQDN         = FQDN,
          TargetHostname     = ExtractedHostname
      | project-away DeviceName
      | lookup EventResultDetailsLookup on EventOriginalResultDetails 
      // filtering on 'eventresultdetails_in', 'TargetUsername' and 'ActorUsername'
      | where (array_length(eventresultdetails_in) == 0 or EventResultDetails in~ (eventresultdetails_in))
          and ((array_length(username_has_any) == 0) or (TargetUsername has_any (username_has_any)) or (ActorUsername has_any (username_has_any)))
      | lookup EventSubTypeLookup on EventOriginalType
      | lookup EventSubTypeLookup on EventOriginalType
      | lookup EventResultLookup on ActionType
      | where (eventresult == "*" or (EventResult == eventresult))
      | extend
              EventSeverity = iff (EventResult == "Success", "Informational", "Low")
      | extend
      UnnormalizedFields = bag_pack(
          "ActingProcessCommandLine", ActingProcessCommandLine,
          "ActingProcessCreationTime", ActingProcessCreationTime,
          "ActingProcessIntegrityLevel", ActingProcessIntegrityLevel,
          "ActingProcessMD5", ActingProcessMD5,
          "ActingProcessSHA1", ActingProcessSHA1,
          "ActingProcessSHA256", ActingProcessSHA256,
          "ActingProcessTokenElevation", ActingProcessTokenElevation,
          "Hash", Hash,
          "HashType", HashType,
          "ParentProcessId", ParentProcessId,
          "ParentProcessCreationTime", ParentProcessCreationTime,
          "ParentProcessName", ParentProcessName,
          "ActingProcessId", ActingProcessId
      )
      | extend
          AdditionalFields = bag_merge(AdditionalFields, UnnormalizedFields)
      // --  Aliases
      | extend 
          ActingAppName = ActingProcessName,
          ActingAppType = "Process",
          Dvc           = coalesce (TargetFQDN, TargetHostname),
          IpAddr        = SrcIpAddr,
          Src           = coalesce (SrcIpAddr, SrcHostname),
          User          = TargetUsername,
      // -- Alias Dvc to Target,
          DvcDomain     = TargetDomain,
          DvcDomainType = TargetDomainType,
          DvcFQDN       = TargetFQDN,
          DvcHostname   = TargetHostname,
          DvcId         = TargetDvcId,
          DvcIdType     = TargetDvcIdType,
          DvcOs         = TargetDvcOs
      | extend 
          Dst         = Dvc,
          LogonTarget = Dvc
      | project
          TimeGenerated,
          Type,
          AdditionalFields,
          ActorUsernameType,
          TargetDvcOs,
          TargetUsernameType,
          ActingProcessName,
          ActorUsername,
          TargetUsername,
          ActorUserIdType,
          TargetUserIdType,
          ActorUserId,
          TargetUserId,
          TargetUserSid,
          ActorUserType,
          ActorUserAadId,
          ActorUserUpn,
          EventOriginalResultDetails,
          EventOriginalType,
          EventUid,
          LogonProtocol,
          SrcHostname,
          SrcPortNumber,
          TargetDvcId,
          EventCount,
          EventEndTime,
          EventOriginalUid,
          EventProduct,
          EventSchema,
          EventSchemaVersion,
          EventStartTime,
          EventType,
          EventVendor,
          SrcIpAddr,
          TargetDvcIdType,
          TargetSessionId,
          TargetDomain,
          TargetDomainType,
          TargetFQDN,
          TargetHostname,
          EventResultDetails,
          EventSubType,
          EventResult,
          EventSeverity,
          ActingAppName,
          ActingAppType,
          Dvc,
          IpAddr,
          Src,
          User,
          DvcDomain,
          DvcDomainType,
          DvcFQDN,
          DvcHostname,
          DvcId,
          DvcIdType,
          DvcOs,
          Dst,
          LogonTarget,
          ASimMatchingUsername
  };
  parser (
    starttime=starttime,
    endtime=endtime,
    username_has_any=username_has_any,
    targetappname_has_any=targetappname_has_any,
    srcipaddr_has_any_prefix=srcipaddr_has_any_prefix,
    srchostname_has_any=srchostname_has_any,
    eventtype_in=eventtype_in,
    eventresultdetails_in=eventresultdetails_in,
    eventresult=eventresult,
    disabled=disabled
  ) 