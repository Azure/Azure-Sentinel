Parser:
  Title: Authentication ASIM filtering parser for OpenSSH sshd
  Version: '0.3.0'
  LastUpdated: Jan 16, 2026
Product:
  Name: OpenSSH
Normalization:
  Schema: Authentication
  Version: '0.1.3'
References:
- Title: ASIM Authentication Schema
  Link: https://aka.ms/ASimAuthenticationDoc
Description: |
  This ASIM parser supports filtering and normalizing OpenSSH server (sshd) sign in logs, collected using Syslog to the ASIM Authentication schema.
Notes: |
  - The parser does not use sshd PAN and preauth events, which are in most cases duplicates of the core sshd events. 
  - Certain preauth events log unsuccessful authentication negotiation, which have security importance but are not authentication events per se, and therefore are not used by this parser.
  - The parser may support other sshd servers, but this has not been tested.  
ParserName: vimAuthenticationSshd
EquivalentBuiltInParser: _Im_Authentication_Sshd
ParserParams:
  - Name: starttime
    Type: datetime
    Default: datetime(null)
  - Name: endtime
    Type: datetime
    Default: datetime(null)
  - Name: username_has_any
    Type: dynamic
    Default: dynamic([])
  - Name: targetappname_has_any
    Type: dynamic
    Default: dynamic([])
  - Name: srcipaddr_has_any_prefix
    Type: dynamic
    Default: dynamic([])
  - Name: srchostname_has_any
    Type: dynamic
    Default: dynamic([])
  - Name: eventtype_in
    Type: dynamic
    Default: dynamic([])
  - Name: eventresultdetails_in
    Type: dynamic
    Default: dynamic([])
  - Name: eventresult
    Type: string
    Default: '*'
  - Name: disabled
    Type: bool
    Default: false
ParserQuery: |
  let parser = (
    starttime: datetime=datetime(null), 
    endtime: datetime=datetime(null), 
    username_has_any: dynamic = dynamic([]),
    targetappname_has_any: dynamic = dynamic([]),
    srcipaddr_has_any_prefix: dynamic = dynamic([]),
    srchostname_has_any: dynamic = dynamic([]),
    eventtype_in: dynamic = dynamic([]),
    eventresultdetails_in: dynamic = dynamic([]),
    eventresult: string = '*',
    disabled: bool=false
    )
  {
  let LogonMethodLookup = datatable(Method: string, LogonMethod: string)
  [
      'password', 'Username & password',
      'publickey', 'PKI',
      'keyboard-interactive/pam',  'Keyboard Interactive/PAM'
  ];
  let prefilter = (T: (SyslogMessage: string, TimeGenerated: datetime))
  {
      T
          | where 
              (isnull(starttime) or TimeGenerated >= starttime) 
              and (isnull(endtime) or TimeGenerated <= endtime)
              and ((array_length(username_has_any) == 0) or SyslogMessage has_any (username_has_any))
              and ((array_length(targetappname_has_any) == 0) or 'sshd' in~ (targetappname_has_any))
              and ((array_length(srcipaddr_has_any_prefix) == 0) or (has_any_ipv4_prefix(SyslogMessage, srcipaddr_has_any_prefix)))
              and (array_length(srchostname_has_any) == 0) // SrcHostname not available in source
  // eventresultdetails_in filtering done later in the parser
  // eventresult filtering done later in the parser 
  };
      let SyslogProjects = Syslog
          | project
              TimeGenerated,
              Computer,
              SyslogMessage,
              ProcessName,
              ProcessID,
              HostIP,
              Type,
              _ItemId,
              _ResourceId,
              _SubscriptionId;
    //
    // -- Successful login
    let SSHDAccepted=(disabled: bool=false)
    { 
        // -- Parse events with the format "Accepted (password|none|publickey|etc.) for <User> from <IP address> port <port> ssh2"
        SyslogProjects
            | where not(disabled)
            | where ProcessName == "sshd" and SyslogMessage startswith 'Accepted'
            | invoke prefilter()
            | parse SyslogMessage with "Accepted " Method: string " for " TargetUsername:string " from " SrcIpAddr:string " port" SrcPortNumber:int *
            | extend
                EventCount    = int(1),
                EventResult   = 'Success',
                EventSeverity = 'Informational',
                EventType     = 'Logon'
            | lookup LogonMethodLookup on Method
            | extend LogonMethod = case(
                isnotempty(LogonMethod), LogonMethod,
                SyslogMessage has "key RSA", "PKI",
                "Other")
            | project-away SyslogMessage, ProcessName, Method
    };
    //
    // -- Failed login - incorrect password
    let SSHDFailed=(disabled: bool=false)
    {
        // -- Parse events with the format Failed (password|none|publickey|etc.)  for <User> from <IP address> port <port> ssh2[: RSA <cipher>:<key>]"
        // -- Or a number of such events message repeated <n> times: [ <message> ]
        SyslogProjects
            | where not(disabled)
            | where ProcessName == "sshd" and (
                SyslogMessage startswith 'Failed' 
                or (SyslogMessage startswith 'message repeated' and SyslogMessage has 'Failed')
                )
            | invoke prefilter()
            | parse SyslogMessage with * "Failed " Method: string " for " TargetUsername:string " from " SrcIpAddr:string " port" SrcPortNumber:int *
            | parse SyslogMessage with "message repeated" EventCount:int " times:" * 
            | extend
                EventCount         = toint(coalesce(EventCount,1)),
                EventResult        = 'Failure',
                EventResultDetails = iff (SyslogMessage has 'publickey', 'Incorrect key', 'Incorrect password'),
                EventSeverity      = 'Low' ,
                EventType          = 'Logon'
            | lookup LogonMethodLookup on Method
            | extend LogonMethod = coalesce(LogonMethod, "Other")
            | project-away SyslogMessage, ProcessName, Method
    };
    //
    // -- Logoff - Timeout
    let SSHDTimeout=(disabled: bool=false)
    {
        // -- Parse events with the format "Timeout, client not responding from user yanivsh 131.107.174.198 port 7623"
        SyslogProjects
            | where not(disabled)
            | where ProcessName == "sshd" and SyslogMessage startswith 'Timeout'
            | invoke prefilter()
            | parse-where SyslogMessage with * "user " TargetUsername: string " " SrcIpAddr: string " port " SrcPortNumber: int
            | extend
                EventCount     = int(1),
                EventResult    = 'Success',
                EventSeverity  = 'Informational',
                EventType      = 'Logoff'
            | project-away SyslogMessage, ProcessName
    };
    //
    // -- Failed login - invalid user
    let SSHDInvalidUser=(disabled: bool=false)
    {
        // -- Parse events with the format "Invalid user [<User>] from <IP address> port <port>"
        SyslogProjects
            | where not(disabled)
            | where ProcessName == "sshd" and SyslogMessage startswith 'Invalid user'
            | invoke prefilter()
            | parse SyslogMessage with "Invalid user " TargetUsername: string " from " SrcIpAddr: string " port " SrcPortNumber: int
            | parse SyslogMessage with "Invalid user  from " SrcIpAddrNoUser: string " port " SrcPortNumberNoUser: int
            | extend
                EventCount            = int(1),
                EventResult           = 'Failure',
                EventResultDetails    = 'No such user',
                EventSeverity         = 'Low',
                EventType             = 'Logon',
                SrcIpAddr             = coalesce(SrcIpAddr, SrcIpAddrNoUser),
                SrcPortNumber         = coalesce(SrcPortNumber, SrcPortNumberNoUser)
            | project-away SyslogMessage, ProcessName, SrcIpAddrNoUser, SrcPortNumberNoUser
    };
    //
    // -- Blocked intrusion attempts
    let SSHDABreakInAttemptMappingFailed=(disabled: bool=false)
    {
        // -- Parse events with the format "reverse mapping checking getaddrinfo for <host> [<Ip address>] failed - POSSIBLE BREAK-IN ATTEMPT!"
        SyslogProjects
            | where not(disabled)
            | where ProcessName == "sshd" and SyslogMessage startswith "reverse mapping checking getaddrinfo for"
            | invoke prefilter()
            | parse SyslogMessage with * " for " Src " [" SrcIpAddr "]" *
            | invoke _ASIM_ResolveSrcFQDN ('Src')
            | extend
                DvcAction             = 'Block',
                EventCount            = int(1),
                EventResult           = 'Failure',
                EventResultDetails    = 'Logon violates policy',
                EventSeverity         = 'Medium',
                EventType             = 'Logon',
                RuleName              = "Reverse mapping failed",    
                TargetUsername        = ''
            | extend
                Rule = RuleName
            | project-away SyslogMessage, ProcessName, Src
    };
    let SSHDABreakInAttemptMappingMismatch=(disabled: bool=false)
    {
        // -- Parse events with the format "Address 61.70.128.48 maps to host-61-70-128-48.static.kbtelecom.net, but this does not map back to the address - POSSIBLE BREAK-IN ATTEMPT!"
        SyslogProjects
            | where not(disabled)
            | where ProcessName == "sshd" and SyslogMessage has "but this does not map back to the address"
            | invoke prefilter()
            | parse SyslogMessage with "Address " SrcIpAddr: string " maps to " Src: string ", but this" *
            | invoke _ASIM_ResolveSrcFQDN ('Src')
            | extend
                DvcAction          = 'Block',
                EventCount         = int(1),
                EventResult        = 'Failure',
                EventResultDetails = 'Logon violates policy',
                EventSeverity      = 'Medium',
                EventType          = 'Logon',
                RuleName           = "Address to host to address mapping does not map back to address",
                TargetUsername     = ''
            | extend
                Rule = RuleName
            | project-away SyslogMessage, ProcessName, Src
    };
    let SSHDABreakInAttemptNastyPtr=(disabled: bool=false)
    {
        // -- Parse events with the format "Nasty PTR record "<RR>" is set up for <IP Address>, ignoring"
        SyslogProjects
            | where not(disabled)
            | where ProcessName == "sshd" and SyslogMessage startswith "Nasty PTR record"
            | invoke prefilter()
            | parse SyslogMessage with * "set up for " SrcIpAddr: string ", ignoring"
            | extend
                DvcAction          = 'Block',
                EventCount         = int(1),
                EventResult        = 'Failure',
                EventResultDetails = 'Logon violates policy',
                EventSeverity      = 'Medium',
                EventType          = 'Logon',
                RuleName           = "Nasty PTR record set for IP Address",
                TargetUsername     = ''
            | extend
                Rule = RuleName
            | project-away SyslogMessage, ProcessName
    };
    union isfuzzy=false 
        SSHDAccepted (disabled=disabled),
        SSHDFailed (disabled=disabled),
        SSHDInvalidUser (disabled=disabled),
        SSHDTimeout (disabled=disabled),
        SSHDABreakInAttemptMappingFailed (disabled=disabled),
        SSHDABreakInAttemptMappingMismatch (disabled=disabled),
        SSHDABreakInAttemptNastyPtr (disabled=disabled)
    // Post-filtering
    | where ((array_length(username_has_any) == 0) or TargetUsername has_any (username_has_any))
        and ((array_length(srcipaddr_has_any_prefix) == 0) or (has_any_ipv4_prefix(SrcIpAddr, srcipaddr_has_any_prefix)))
        and ((array_length(eventtype_in) == 0) or EventType in~ (eventtype_in))
        and (array_length(eventresultdetails_in) == 0 or EventResultDetails in~ (eventresultdetails_in))
        and (eventresult == "*" or (EventResult == eventresult))
    // mapping ASimMatchingUsername
    | extend temp_isMatchTargetUsername=TargetUsername has_any(username_has_any)
    // ActorUsername not coming from source. Hence, not mapped.
    | extend ASimMatchingUsername = case
        (
            array_length(username_has_any) == 0, "-",
            temp_isMatchTargetUsername, "TargetUsername",
            "No match"
        )
    | invoke _ASIM_ResolveDvcFQDN ('Computer')
    | extend 
        DvcIdType             = iff (isnotempty(_ResourceId), "AzureResourceId", ""),
        DvcOs                 = 'Linux',
        EventEndTime          = TimeGenerated,
        EventProduct          = 'OpenSSH',
        EventSchema           = 'Authentication',
        EventSchemaVersion    = '0.1.2',
        EventStartTime        = TimeGenerated,
        EventSubType          = 'Remote',
        EventVendor           = 'OpenBSD',
        LogonProtocol         = 'ssh',
        TargetAppId           = tostring(ProcessID),
        TargetAppName         = 'sshd',
        TargetAppType         = 'Service',
        TargetDvcOs           = 'Linux',
        TargetUsernameType    = 'Simple',
        Type                  = 'Syslog'
    | project-away Computer, ProcessID, temp*
    | project-rename 
        DvcId                 = _ResourceId,
        DvcIpAddr             = HostIP,
        DvcScopeId            = _SubscriptionId,
        EventUid              = _ItemId
    //
    // -- Aliases
    | extend
        Dst                   = coalesce (DvcFQDN, DvcHostname, DvcIpAddr),
        IpAddr                = SrcIpAddr,
        Src                   = SrcIpAddr,
        TargetDomain          = DvcDomain,
        TargetDomainType      = DvcDomainType,
        TargetDvcId           = DvcId,
        TargetDvcIdType       = DvcIdType,
        TargetDvcScopeId      = DvcScopeId,
        TargetFQDN            = DvcFQDN,
        TargetHostname        = DvcHostname,
        TargetIpAddr          = DvcIpAddr,
        User                  = TargetUsername,
        Application           = TargetAppName
    | extend Dvc = Dst
  };
  parser(
    starttime=starttime,
    endtime=endtime,
    username_has_any=username_has_any,
    targetappname_has_any=targetappname_has_any,
    srcipaddr_has_any_prefix=srcipaddr_has_any_prefix,
    srchostname_has_any=srchostname_has_any,
    eventtype_in=eventtype_in,
    eventresultdetails_in=eventresultdetails_in,
    eventresult=eventresult,
    disabled=disabled
  )