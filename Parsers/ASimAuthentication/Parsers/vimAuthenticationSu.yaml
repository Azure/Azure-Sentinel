Parser:
  Title: Authentication ASIM filtering parser for Linux su 
  Version: '0.3.0'
  LastUpdated: Jan 15, 2026
Product:
  Name: su
Normalization:
  Schema: Authentication
  Version: '0.1.3'
References:
- Title: ASIM Authentication Schema
  Link: https://aka.ms/ASimAuthenticationDoc
- Title: ASIM
  Link: https:/aka.ms/AboutASIM
Description: |
  This ASIM parser supports filtering and normalizing Linux su elevation commands collected using Syslog to the ASIM Authentication schema.
ParserName: vimAuthenticationSu
EquivalentBuiltInParser: _Im_Authentication_Su
ParserParams:
  - Name: starttime
    Type: datetime
    Default: datetime(null)
  - Name: endtime
    Type: datetime
    Default: datetime(null)
  - Name: username_has_any
    Type: dynamic
    Default: dynamic([])
  - Name: targetappname_has_any
    Type: dynamic
    Default: dynamic([])
  - Name: srcipaddr_has_any_prefix
    Type: dynamic
    Default: dynamic([])
  - Name: srchostname_has_any
    Type: dynamic
    Default: dynamic([])
  - Name: eventtype_in
    Type: dynamic
    Default: dynamic([])
  - Name: eventresultdetails_in
    Type: dynamic
    Default: dynamic([])
  - Name: eventresult
    Type: string
    Default: '*'
  - Name: disabled
    Type: bool
    Default: false
ParserQuery: |
  let parser = (
    starttime: datetime=datetime(null), 
    endtime: datetime=datetime(null), 
    username_has_any: dynamic = dynamic([]),
    targetappname_has_any: dynamic = dynamic([]),
    srcipaddr_has_any_prefix: dynamic = dynamic([]),
    srchostname_has_any: dynamic = dynamic([]),
    eventtype_in: dynamic = dynamic([]),
    eventresultdetails_in: dynamic = dynamic([]),
    eventresult: string = '*',
    disabled: bool=false
  )
  {
    let prefilter = (T: (SyslogMessage: string, TimeGenerated: datetime, EventResult: string, EventType: string, HostIP: string))
    {
        T
        | where 
            (isnull(starttime) or TimeGenerated >= starttime) 
            and (isnull(endtime) or TimeGenerated <= endtime)
            and ((array_length(username_has_any) == 0) or SyslogMessage has_any (username_has_any))
            and (array_length(targetappname_has_any) == 0 or 'su' in~ (targetappname_has_any))
            and (array_length(srcipaddr_has_any_prefix) == 0 or (has_any_ipv4_prefix(HostIP, srcipaddr_has_any_prefix))) // Mapping dvc ip to src filtering
            and (array_length(srchostname_has_any) == 0) // SrcHostname not available in source
            and ((array_length(eventtype_in) == 0) or (EventType in~ (eventtype_in)))
            and (array_length(eventresultdetails_in) == 0) // EventResultDetails not available in source
            and (eventresult == "*" or (EventResult == eventresult))
    };
     let SyslogProjects = Syslog
        | project
            TimeGenerated,
            Computer,
            SyslogMessage,
            ProcessName,
            ProcessID,
            HostIP,
            Type,
            _ItemId,
            _ResourceId,
            _SubscriptionId;
    //
    // -- Successful SU
    // Parses the event "Successful su for <user> by <user>"
    let SuSignInAuthorized=(disabled: bool=false)
    {
    SyslogProjects 
        | where not(disabled)
        | where ProcessName == "su" and SyslogMessage startswith "Successful su for"
        | extend
            EventType = 'Logon',
            EventResult = "Success"
        | invoke prefilter()
        | parse SyslogMessage with * "for " TargetUsername: string " by " ActorUsername: string
        | project-away SyslogMessage, ProcessName
    };
    // 
    // -- SU end
    // Parsers the event "pam_unix(su[-l]:session): session closed for user <user>"
    let SuDisconnect=(disabled: bool=false)
    {
    SyslogProjects 
        | where not(disabled)
        | where ProcessName == "su" and SyslogMessage has_all ('pam_unix(su', 'session): session closed for user')
        | extend
            EventType = 'Logoff',
            EventResult = "Success"
        | invoke prefilter()
        | parse SyslogMessage with * "for user " TargetUsername: string
        | project-away SyslogMessage, ProcessName
    };
    // Failed SU
    let SuFailed=(disabled: bool=false)
    {
        SyslogProjects
        | where not(disabled)
        | where ProcessName == "su" and SyslogMessage startswith "FAILED SU"
        | extend 
            EventType = "Logon",
            EventResult = "Failure"
        | invoke prefilter()
        | parse SyslogMessage with * "to " TargetUsername: string ") " ActorUsername: string " on " *
        | project-away SyslogMessage, ProcessName
    };
    union isfuzzy=false 
        SuDisconnect(disabled = disabled),
        SuSignInAuthorized(disabled = disabled),
        SuFailed(disabled = disabled)
    // Post-filtering
    | where ((array_length(username_has_any) == 0) or (TargetUsername has_any (username_has_any)) or (ActorUsername has_any (username_has_any)))
        and (array_length(eventtype_in) == 0 or EventType in~ (eventtype_in))
    // mapping ASimMatchingUsername
    | extend
        temp_isMatchTargetUsername=TargetUsername has_any(username_has_any),
        temp_isMatchActorUsername=ActorUsername has_any(username_has_any)
    | extend ASimMatchingUsername = case
        (
            array_length(username_has_any) == 0, "-",
            temp_isMatchTargetUsername and temp_isMatchActorUsername, "Both",
            temp_isMatchTargetUsername, "TargetUsername",
            temp_isMatchActorUsername, "ActorUsername",
            "No match"
        )
    | invoke _ASIM_ResolveDvcFQDN('Computer')
    | extend
        ActingAppId           = tostring(ProcessID),
        ActingAppType         = 'Process',
        ActorUsernameType     = 'Simple',
        DvcIdType             = iff (isnotempty(_ResourceId), "AzureResourceId", ""),
        DvcOs                 = 'Linux',
        EventCount            = int(1),
        EventEndTime          = TimeGenerated,
        EventProduct          = 'su',
        EventSchema           = 'Authentication',
        EventSchemaVersion    = '0.1.3',
        EventSeverity         = 'Informational',
        EventStartTime        = TimeGenerated,
        EventVendor           = 'Linux',
        TargetDvcOs           = 'Linux',
        TargetUsernameType    = 'Simple',
        Type                  = "Syslog"
    | project-away Computer, ProcessID
    | project-rename 
        DvcId                 = _ResourceId,
        DvcIpAddr             = HostIP,
        DvcScopeId            = _SubscriptionId,
        EventUid              = _ItemId
    //
    // -- Aliases
    | extend
        Dst                   = coalesce (DvcFQDN, DvcHostname, DvcIpAddr),
        IpAddr                = DvcIpAddr,
        TargetDomain          = DvcDomain,
        TargetDomainType      = DvcDomainType,
        TargetDvcId           = DvcId,
        TargetDvcIdType       = DvcDomainType,
        TargetDvcScopeId      = DvcScopeId,
        TargetFQDN            = DvcFQDN,
        TargetHostname        = DvcHostname,
        TargetIpAddr          = DvcIpAddr,
        User                  = TargetUsername
    | extend Dvc = Dst
    | project
        TimeGenerated,
        EventType,
        EventResult,
        DvcHostname,
        DvcDomain,
        DvcFQDN,
        DvcDomainType,
        ActingAppId,
        ActingAppType,
        ActorUsernameType,
        DvcIdType,
        DvcOs,
        EventCount,
        EventEndTime,
        EventProduct,
        EventSchema,
        EventSchemaVersion,
        EventSeverity,
        EventStartTime,
        EventVendor,
        TargetDvcOs,
        TargetUsernameType,
        Type,
        DvcId,
        DvcIpAddr,
        DvcScopeId,
        EventUid,
        Dst,
        Dvc,
        IpAddr,
        TargetDomain,
        TargetDomainType,
        TargetDvcId,
        TargetDvcIdType,
        TargetDvcScopeId,
        TargetFQDN,
        TargetHostname,
        TargetIpAddr,
        User,
        TargetUsername,
        ActorUsername,
        ASimMatchingUsername
  };
  parser (
    starttime=starttime,
    endtime=endtime,
    username_has_any=username_has_any,
    targetappname_has_any=targetappname_has_any,
    srcipaddr_has_any_prefix=srcipaddr_has_any_prefix,
    srchostname_has_any=srchostname_has_any,
    eventtype_in=eventtype_in,
    eventresultdetails_in=eventresultdetails_in,
    eventresult=eventresult,
    disabled=disabled
  )