Parser:
  Title: Authentication ASIM parser for Syslog sudo
  Version: '0.2.0'
  LastUpdated: Jan 29, 2026
Product:
  Name: sudo
Normalization:
  Schema: Authentication
  Version: '0.1.4'
References:
- Title: ASIM Authentication Schema
  Link: https://aka.ms/ASimAuthenticationDoc
- Title: ASIM
  Link: https://aka.ms/AboutASIM
Description: |
  This ASIM parser supports normalizing Syslog sudo sign in logs to the ASIM Authentication schema.
ParserName: ASimAuthenticationSudo
EquivalentBuiltInParser: _ASim_Authentication_Sudo
ParserParams:
  - Name: disabled
    Type: bool
    Default: false
ParserQuery: |
  let parser = (disabled: bool = false) {
    let SeverityLevelLookup = datatable (SeverityLevel: string, EventSeverity: string)
    [
        "info", "Informational",
        "notice", "Informational",
        "alert", "Low",
        "error", "Medium",
        "err", "Medium",
        "critical", "High",
        "warning", "Low",
        "warn", "Low",
        "debug", "Informational",
        "crit", "High"
    ];
        Syslog
    | where not(disabled)
    | where ProcessName == "sudo"
        | where SyslogMessage has 'TTY=' and 
            SyslogMessage has 'USER=' and
            SyslogMessage has 'COMMAND='
        | parse-kv SyslogMessage as (TTY: string, PWD: string, USER: string, COMMAND: string) with (pair_delimiter=' ', kv_delimiter='=')
        | project-rename TargetUsername = USER
        | extend
            EventResult                 = 'Success',
            EventType                   = 'Logon',
            ActorUsernameType           = 'Simple',
            ActorUsername               = "DOES THIS WORK?",
            EventResultDetails          = 'Other',
            EventOriginalResultDetails = 'Connection authorized'
        | extend
            EventCount = int(1),
            EventStartTime = TimeGenerated,
            EventEndTime = TimeGenerated,
            EventSchema = "Authentication",
            EventSchemaVersion = "0.1.4",
            EventVendor = "Linux",
            EventProduct = "sudo",
            TargetUsernameType = "Simple",
            Type = "Syslog",
            ActingAppId = tostring(ProcessID)
        | extend
            SrcIpAddr = iff(ipv4_is_in_range(HostIP, "0.0.0.0/0"), HostIP, ""),
            TargetAppType = "Process"
        | lookup SeverityLevelLookup on SeverityLevel
        | project-rename
            SrcHostname = HostName,
            DvcHostname = Computer,
            EventOriginalSeverity = SeverityLevel,
            ActingAppName = ProcessName,
            EventUid = _ItemId
        | extend
            TargetAppName = ActingAppName,
            Application = ActingAppName,
            User = TargetUsername,
            DvcIpAddr = SrcIpAddr,
            IpAddr = SrcIpAddr,
            Src = coalesce(SrcIpAddr, SrcHostname),
            Dvc = coalesce(SrcIpAddr, DvcHostname)
        | project
            TimeGenerated,
            EventCount,
            EventStartTime,
            EventEndTime,
            EventSchema,
            EventSchemaVersion,
            EventVendor,
            EventProduct,
            EventResult,
            EventType,
            ActorUsername,
            ActorUsernameType,
            EventResultDetails,
            EventOriginalResultDetails,
            TargetUsername,
            TargetUsernameType,
            Type,
            SrcIpAddr,
            SrcHostname,
            EventSeverity,
            DvcHostname,
            EventOriginalSeverity,
            ActingAppName,
            ActingAppId,
            EventUid,
            TargetAppName,
            TargetAppType,
            Application,
            Src,
            Dvc,
            User,
            DvcIpAddr,
            IpAddr
  };
  parser(disabled=disabled);