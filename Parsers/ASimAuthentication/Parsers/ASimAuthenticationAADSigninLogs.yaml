Parser:
  Title: Authentication ASIM parser for Microsoft Entra ID interactive sign-in logs
  Version: '0.4.0'
  LastUpdated: Jan 08, 2026
Product:
  Name: Microsoft Entra ID
Normalization:
  Schema: Authentication
  Version: '0.1.3'
References:
- Title: Using functions
  Link: https://docs.microsoft.com/azure/azure-monitor/log-query/function
- Title: Authentication schema documentation
  Link: https://aka.ms/ASimAuthenticationDoc
Description: |
  This ASIM parser supports normalizing Microsoft Entra ID Interactive sign in logs, stored in the  SigninLogs table, to the ASIM Authentication schema.
ParserName: ASimAuthenticationSigninLogs
EquivalentBuiltInParser: _ASim_Authentication_SigninLogs
ParserParams:
  - Name: disabled
    Type: bool
    Default: false
ParserQuery: |
  let FailedReason=datatable(ResultType:string, EventResultDetails:string)[
    '0', 'Success',
    '50005', 'Logon violates policy',
    '50011', 'Logon violates policy', 
    '50020', 'Logon violates policy',
    '50034', 'No such user or password',
    '50053', 'User locked',
    '50055', 'Password expired',
    '50056', 'Incorrect password',
    '50057', 'User disabled',
    '50058', 'Logon violates policy',
    '50059', 'No such user or password',
    '50064', 'No such user or password',
    '50072', 'Logon violates policy',
    '50074', 'Logon violates policy', 
    '50076', 'Logon violates policy',
    '50079', 'Logon violates policy',
    '50105', 'Logon violates policy',
    '50126', 'No such user or password',
    '50132', 'Password expired',
    '50133', 'Password expired',
    '50144', 'Password expired',
    '50173', 'Password expired',
    '51004', 'No such user or password',
    '53003', 'Logon violates policy',
    '70008', 'Password expired',
    '80012', 'Logon violates policy',
    '500011', 'No such user or password',
    '700016',  'No such user or password', 
    ];
  let UserTypeLookup = datatable (UserType:string, TargetUserType:string) [
    'Guest','Guest', 
    'Member', 'Regular',
    '',''
  ];
  let TargetAppType = datatable (TargetOriginalAppType: string, TargetAppType: string) [
    'Mobile Apps and Desktop clients', 'Process',
    'Browser', 'Service',
    'Authenticated STMP', 'CSP',
    'Exchange Active Sync', 'CSP',
    'Other', 'Other',
    'Unknown', 'Other'
  ];
  let LogonMethodLookup = datatable(OriginalLogonMethod: string, LogonMethod: string)
  [
    "singleFactorAuthentication", "Username & Password",
    "multiFactorAuthentication", "Multi factor authentication"
  ];
  let parser=(disabled:bool=false) {
    SigninLogs
    | where not(disabled)
    | extend
        EventCount = int(1),
        EventEndTime = TimeGenerated,
        EventOriginalResultDetails = coalesce(ResultDescription, ResultType),
        EventProduct = 'Entra ID',
        EventResult = iff (ResultType == 0, 'Success', 'Failure'),
        EventSchema = 'Authentication',
        EventSchemaVersion = '0.1.3',
        EventStartTime = TimeGenerated,
        EventSubType = 'Interactive',
        EventType = 'Logon',
        EventVendor = 'Microsoft',
        Type = 'SigninLogs',
        Location = todynamic(LocationDetails),
        SrcHostname = tostring(DeviceDetail.displayName),
        SrcDvcId = tostring(DeviceDetail.deviceId),
        SrcDvcIdType = "",
        SrcIpAddr = IPAddress,
        SrcDvcOs = tostring(DeviceDetail.operatingSystem),
        TargetUserIdType = 'AADID',
        TargetUsernameType = 'UPN',
        OriginalLogonMethod = coalesce(AuthenticationMethodsUsed, AuthenticationRequirement)
    | extend
        SrcDvcIdType = iif(isempty(SrcDvcId), "", "Other"),
        SrcGeoCity = tostring(Location.city),
        SrcGeoCountry = tostring(Location.countryOrRegion),
        SrcGeoLatitude = toreal(Location.geoCoordinates.latitude),
        SrcGeoLongitude = toreal(Location.geoCoordinates.longitude)
    | lookup FailedReason on ResultType
    | lookup LogonMethodLookup on OriginalLogonMethod
    | project-rename
        EventOriginalUid = Id,
        HttpUserAgent    = UserAgent,
        TargetAppId      = AppId,
        TargetAppName    = AppDisplayName,
        TargetSessionId  = CorrelationId,
        TargetUserId     = UserId,
        TargetUsername   = UserPrincipalName,
        TargetOriginalAppType = ClientAppUsed
    | lookup TargetAppType on TargetOriginalAppType
    | extend EventUid = column_ifexists("_ItemId", "")
    | lookup UserTypeLookup on UserType
    // ** Aliases
    | extend 
        Application     = TargetAppName,
        IpAddr          = SrcIpAddr,
        Dvc             = EventVendor,
        LogonTarget     = TargetAppName,
        User            = TargetUsername
    | project
      TimeGenerated,
      EventSchema,
      Type,
      EventVendor,
      EventProduct,
      EventCount,
      EventSchemaVersion,
      EventResult,
      EventOriginalResultDetails,
      EventStartTime,
      EventEndTime,
      EventType,
      Application,
      IpAddr,
      SrcDvcId,
      SrcDvcIdType,
      SrcHostname,
      SrcDvcOs,
      TargetUsernameType,
      TargetUserIdType,
      SrcIpAddr,
      LogonMethod,
      SrcGeoCity,
      SrcGeoCountry,
      SrcGeoLatitude,
      SrcGeoLongitude,
      EventOriginalUid,
      EventUid,
      HttpUserAgent,
      TargetSessionId,
      TargetUserId,
      TargetUsername,
      TargetAppId,
      TargetAppName,
      TargetAppType,
      TargetOriginalAppType,
      TargetUserType,
      User,
      LogonTarget,
      Dvc
  };
  parser  
  (
      disabled = disabled
  )