Parser:
  Title: Authentication ASIM parser for Cisco Device Logon Events
  Version: '0.1.0'
  LastUpdated: 6 Jul 2023
Product:
  Name: M365 Defender for EndPoint
Normalization:
  Schema: Authentication
  Version: '0.1.0'
References:
- Title: ASIM Authentication Schema
  Link: https://aka.ms/ASimAuthenticationDoc
- Title: ASIM
  Link: https:/aka.ms/AboutASIM
Description: |
  This ASIM parser supports normalizing authentication events, collected from Cisco ASA devices, to the ASIM Authentication schema.
ParserName: ASimAuthenticationCiscoASA
ParserParams:
  - Name: disabled
    Type: bool
    Default: false
ParserQuery: |
  let parser = (
    disabled:bool=false
  ){
    let DeviceEventClassIDLookup = datatable (DeviceEventClassID:string, EventResultDetails:string, EventType:string, EventResult:string, EventSubType:string, DvcAction:string)
    [
        "113004", "", "Logon", "Success", "Remote", "",
        "113005", "Other", "Logon", "Failure", "Remote", "",
        "113006", "Logon violates policy", "Logon", "Failure", "Remote", "Blocked",
        "113008", "", "Logon", "Success", "Remote", "",
        "113010", "", "Logon", "Success", "Remote", "",
        "113012", "", "Logon", "Success", "Remote", "",
        "113019", "", "Logoff", "Success", "", "",
        "113039", "", "Logon", "Success", "Remote", "",
        "315011", "", "Logoff", "Success", "", "",
        "502103", "", "Elevate", "Success", "AssumeRole", "",
        "605004", "Other", "Logon", "Failure", "Remote", "",
        "605005", "", "Logon", "Success", "Remote", "",
        "611101", "", "Logon", "Success", "Remote", "",
        "611102", "Other", "Logon", "Failure", "Remote", "",
        "611103", "", "Logoff", "Success", "", "",
        "713198", "Logon violates policy", "Logon", "Failure", "Remote", "Blocked",
        "716002", "", "Logoff", "Success", "", "",
        "716038", "", "Logon", "Success", "Remote", "",
        "716039", "Other", "Logon", "Failure", "Remote", "",
        "716040", "Other", "Logon", "Failure", "Remote", "",
        "722022", "", "Logon", "Success", "Remote", "",
        "722023", "", "Logoff", "Success", "", "",
        "722028", "", "Logoff", "Success", "", "",
        "722037", "", "Logoff", "Success", "", ""
        ];
    let FilteredDeviceEventClassID = toscalar(
        DeviceEventClassIDLookup 
        | summarize make_set(DeviceEventClassID)
    );
    let LogMessages = 
    CommonSecurityLog 
    | where DeviceVendor =~ "Cisco"
    | where DeviceProduct == "ASA"
    | where DeviceEventClassID in(FilteredDeviceEventClassID)
    | project TimeGenerated, Type, Computer, _ItemId, DeviceEventClassID, Message, LogSeverity, DeviceAddress
    | lookup DeviceEventClassIDLookup on DeviceEventClassID;
    union 
    (
        LogMessages
        | where DeviceEventClassID == 113005
        | parse Message with * 'server = ' TargetIpAddr ' ' * 'user = ' TargetUsername ' ' * 'user IP = ' SrcIpAddr
        | project-away Message
    ),
    (
        LogMessages
        | where DeviceEventClassID == 502103
        | parse Message with * "Uname: " TargetUsername " " *
        | project-away Message
    ),
    (
        LogMessages
        | where DeviceEventClassID in(605004,605005)
        | parse Message with * 'from ' SrcIpAddr '/' SrcPortNumber:int " to " * ":" TargetIpAddr '/' * 'user "' TargetUsername '"'
        | project-away Message
    ),
    (
        LogMessages
        | where DeviceEventClassID in(611101,611102)
        | parse Message with * 'IP address: ' SrcIpAddr ', Uname: ' TargetUsername
        | project-away Message
    ),
    (
        LogMessages
        | where DeviceEventClassID == 611103
        | parse Message with * ' Uname: ' TargetUsername
        | project-away Message
    ),
    (
        LogMessages
        | where DeviceEventClassID == 113004
        | parse Message with * 'server = ' TargetIpAddr ' ' * 'user = ' TargetUsername
        | project-away Message
    ),
    (
        LogMessages
        | where DeviceEventClassID in(113008,113012)
        | parse Message with * 'user = ' TargetUsername
        | project-away Message
    ),
    (
        LogMessages
        | where DeviceEventClassID == 113019
        | parse Message with * 'Username = ' TargetUsername ', IP = ' SrcIpAddr ',' * 
        | project-away Message
    ),
    (
        LogMessages
        | where DeviceEventClassID in(113039,716002,716039,722022,722023,722028,722037)
        | parse Message with * '> User <' TargetUsername "> IP <" SrcIpAddr ">" *
        | project-away Message
    ),
    (
        LogMessages
        | where DeviceEventClassID == 315011
        | parse Message with * 'from ' SrcIpAddr ' ' * 'user "' TargetUsername '" ' *
        | project-away Message
    ),
    (
        LogMessages
        | where DeviceEventClassID == 113010
        | parse Message with * 'user ' TargetUsername ' from  server' SrcIpAddr
        | project-away Message
    ),
    (
        LogMessages
        | where DeviceEventClassID == 113006
        | parse Message with * 'User ' TargetUsername ' locked' *
        | project-away Message
    ),
    (
        LogMessages
        | where DeviceEventClassID == 716040
        | parse Message with * 'Denied ' TargetUsername ' login' *
        | project-away Message
    ),
    (
        LogMessages
        | where DeviceEventClassID == 713198
        | parse Message with * 'Failed: ' TargetUsername ' User' *
        | project-away Message
    ),
    (
        LogMessages
        | where DeviceEventClassID == 716038
        | parse Message with * 'User ' TargetUsername ' IP ' SrcIpAddr ' Authentication'*
        | project-away Message
    )
    | project-rename 
        DvcHostname           = Computer,
        EventUid              = _ItemId,
        EventOriginalType     = DeviceEventClassID,
        EventSeverity         = LogSeverity,
        DvcIpAddr             = DeviceAddress
    | extend 
        EventSchemaVersion = "0.1.3",
        EventSchema        = "Authentication",
        EventVendor        = "Cisco",
        EventProduct       = "ASA",
        EventCount         = int(1),
        EventStartTime     = TimeGenerated,
        EventEndTime       = TimeGenerated,
        Dvc                = DvcHostname,
        User               = TargetUsername,
        Src                = SrcIpAddr,
        IpAddr             = SrcIpAddr,
        Dst                = TargetIpAddr,
        TargetUsernameType = _ASIM_GetUsernameType(TargetUsername),
        EventResultDetails = iif(TargetUsername == "*****", "No such user or password", EventResultDetails)
  };
  parser (
    disabled = disabled
  )