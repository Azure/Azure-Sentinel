Parser:
  Title: DNS activity ASIM parser for Fortinet FortiGate
  Version: '0.1.2'
  LastUpdated: Dec 8, 2025
Product:
  Name: Fortinet FortiGate
Normalization:
  Schema: Dns
  Version: '0.1.7'
References:
- Title: ASIM DNS Schema
  Link: https://aka.ms/ASimDnsDoc
- Title: ASIM
  Link: https://aka.ms/AboutASIM
- Title: Fortinet FortiGate DNS log message reference
  Link: https://docs.fortinet.com/document/fortigate/7.4.1/fortios-log-message-reference/229/dns
Description: |
  This ASIM parser supports normalizing Fortinet FortiGate logs ingested in 'CommonSecurityLogs' table produced by the Microsoft Sentinel Fortinet connector to the ASIM DNS normalized schema.
ParserName: ASimDnsFortinetFortiGate
EquivalentBuiltInParser: _ASim_Dns_FortinetFortiGate
ParserParams:
  - Name: disabled
    Type: bool
    Default: false
ParserQuery: |
  let Parser = (disabled:bool=false) {
      let DeviceEventClassIDLookup = datatable(EventOriginalSubType:string,EventSubType:string, EventSeverity:string, DvcAction:string, ThreatCategory:string, ThreatField:string)[
          "54000", "request", "Informational", "", "", "",
          "54200", "response", "Low", "", "", "",
          "54400", "response", "Low", "Blocked", "", "",
          "54401", "response", "Informational", "", "", "",
          "54600", "response", "Low", "Blocked", "Botnet", "DstIpAddr",
          "54601", "response", "Low", "Blocked", "Botnet", "Domain",
          "54800", "response", "Low", "", "", "",
          "54801", "response", "Low", "", "", "",
          "54802", "response", "Informational", "", "", "",
          "54803", "response", "Low", "Blocked", "", "",
          "54804", "response", "Informational", "", "", "",
          "54805", "response", "Informational", "", "", "",
      ];
      let EventOriginalResultDetailsLookup = datatable(EventOriginalResultDetails:string, EventResultDetails:string, EventResult:string)[
          "", "NOERROR", "Success",
          "0", "NOERROR", "Success",
          "1", "FORMERR", "Failure",
          "2", "SERVFAIL", "Failure",
          "3", "NXDOMAIN", "Failure",
          "4", "NOTIMP", "Failure",
          "5", "REFUSED", "Failure",
          "6", "YXDOMAIN", "Failure",
          "7", "YXRRSET", "Failure",
          "8", "NXRRSET", "Failure",
          "9", "NOTAUTH", "Failure",
          "10", "NOTZONE", "Failure",
          "11", "DSOTYPENI", "Failure",
          "16", "BADVERS", "Failure",
          "16", "BADSIG", "Failure",
          "17", "BADKEY", "Failure",
          "18", "BADTIME", "Failure",
          "19", "BADMODE", "Failure",
          "20", "BADNAME", "Failure",
          "21", "BADALG", "Failure",
          "22", "BADTRUNC", "Failure",
          "23", "BADCOOKIE", "Failure"
      ];
      let DnsQueryTypeLookup = datatable(DnsQueryType:int, DnsQueryTypeName:string)[
          0, "Reserved",
          1, "A",
          2, "NS",
          3, "MD",
          4, "MF",
          5, "CNAME",
          6, "SOA",
          7, "MB",
          8, "MG",
          9, "MR",
          10, "NULL",
          11, "WKS",
          12, "PTR",
          13, "HINFO",
          14, "MINFO",
          15, "MX",
          16, "TXT",
          17, "RP",
          18, "AFSDB",
          19, "X25",
          20, "ISDN",
          21, "RT",
          22, "NSAP",
          23, "NSAP-PTR",
          24, "SIG",
          25, "KEY",
          26, "PX",
          27, "GPOS",
          28, "AAAA",
          29, "LOC",
          30, "NXT",
          31, "EID",
          32, "NIMLOC",
          33, "SRV",
          34, "ATMA",
          35, "NAPTR",
          36, "KX",
          37, "CERT",
          38, "A6",
          39, "DNAME",
          40, "SINK",
          41, "OPT",
          42, "APL",
          43, "DS",
          44, "SSHFP",
          45, "IPSECKEY",
          46, "RRSIG",
          47, "NSEC",
          48, "DNSKEY",
          49, "DHCID",
          50, "NSEC3",
          51, "NSEC3PARAM",
          52, "TLSA",
          53, "SMIMEA",
          55, "HIP",
          56, "NINFO",
          57, "RKEY",
          58, "TALINK",
          59, "CDS",
          60, "CDNSKEY",
          61, "OPENPGPKEY",
          62, "CSYNC",
          63, "ZONEMD",
          64, "SVCB",
          65, "HTTPS",
          99, "SPF",
          100, "UINFO",
          101, "UID",
          102, "GID",
          103, "UNSPEC",
          104, "NID",
          105, "L32",
          106, "L64",
          107, "LP",
          108, "EUI48",
          109, "EUI64",
          249, "TKEY",
          250, "TSIG",
          251, "IXFR",
          252, "AXFR",
          253, "MAILB",
          254, "MAILA",
          255, "*",
          256, "URI",
          257, "CAA",
          258, "AVC",
          259, "DOA",
          32768, "TA",
          32769, "DLV"
      ];
      CommonSecurityLog
      | where not(disabled)
      | where DeviceVendor == "Fortinet" and DeviceProduct startswith "Fortigate"
      | where DeviceEventClassID endswith "54000" or 
              DeviceEventClassID endswith "54200" or 
              DeviceEventClassID endswith "54400" or 
              DeviceEventClassID endswith "54401" or 
              DeviceEventClassID endswith "54600" or 
              DeviceEventClassID endswith "54601" or 
              DeviceEventClassID endswith "54800" or 
              DeviceEventClassID endswith "54801" or 
              DeviceEventClassID endswith "54802" or 
              DeviceEventClassID endswith "54803" or 
              DeviceEventClassID endswith "54804" or 
              DeviceEventClassID endswith "54805"
      | extend EventOriginalSubType = case(
          DeviceEventClassID endswith "54000", "54000",
          DeviceEventClassID endswith "54200", "54200",
          DeviceEventClassID endswith "54400", "54400",
          DeviceEventClassID endswith "54401", "54401",
          DeviceEventClassID endswith "54600", "54600",
          DeviceEventClassID endswith "54601", "54601",
          DeviceEventClassID endswith "54800", "54800",
          DeviceEventClassID endswith "54801", "54801",
          DeviceEventClassID endswith "54802", "54802",
          DeviceEventClassID endswith "54803", "54803",
          DeviceEventClassID endswith "54804", "54804",
          DeviceEventClassID endswith "54805", "54805",
          DeviceEventClassID
      )
      | project TimeGenerated, EventOriginalSubType, AdditionalExtensions, EventUid = _ItemId, EventOriginalSeverity = LogSeverity, EventProductVersion = DeviceVersion ,Computer, Type, SrcIpAddr = SourceIP, SrcPortNumber = SourcePort, DstIpAddr = DestinationIP, DstPortNumber = DestinationPort, EventMessage = Message, NetworkProtocolNumber = Protocol, DvcId = DeviceExternalID, DnsSessionId = ExtID
      | lookup DeviceEventClassIDLookup on EventOriginalSubType
      | parse-kv  AdditionalExtensions as (
            // FTNTFGT format for FortiGate
            FTNTFGTlogid:string, 
            FTNTFGTsubtype:string, 
            FTNTFGTsrccountry:string, 
            FTNTFGTdstcountry:string,
            FTNTFGTsrcintfrole:string, 
            FTNTFGTrcode:string, 
            FTNTFGTqname:string, 
            FTNTFGTqtype:string, 
            FTNTFGTxid:string, 
            FTNTFGTqtypeval:int, 
            FTNTFGTqclass:string, 
            FTNTFGTcatdesc:string, 
            FTNTFGTipaddr:string, 
            FTNTFGTunauthuser:string, 
            FTNTFGTuser:string, 
            FTNTFGTbotnetip:string,
            // Simple format for FortiAnalyzer
            logid:string, 
            subtype:string, 
            srccountry:string, 
            dstcountry:string,
            srcintfrole:string, 
            rcode:string, 
            qname:string, 
            qtype:string, 
            xid:string, 
            qtypeval:int, 
            qclass:string, 
            catdesc:string, 
            ipaddr:string, 
            unauthuser:string, 
            user:string, 
            botnetip:string,
            // Additional fields
            sessionid:int
        ) with (pair_delimiter=";", kv_delimiter="=")
      | extend 
          EventOriginalResultDetails  = coalesce(FTNTFGTrcode, rcode),
          EventOriginalUid            = coalesce(FTNTFGTlogid, logid),
          DvcZone                     = coalesce(FTNTFGTsrcintfrole, srcintfrole),
          EventOriginalType           = coalesce(FTNTFGTsubtype, subtype),
          SrcGeoCountry               = coalesce(FTNTFGTsrccountry, srccountry),
          DstGeoCountry               = coalesce(FTNTFGTdstcountry, dstcountry),
          DnsQuery                    = coalesce(FTNTFGTqname, qname),
          DnsQueryTypeName            = coalesce(FTNTFGTqtype, qtype),
          TransactionIdHex            = coalesce(FTNTFGTxid, xid),
          DnsQueryClass               = coalesce(FTNTFGTqtypeval, qtypeval),
          DnsQueryClassName           = coalesce(FTNTFGTqclass, qclass),
          UrlCategory                 = coalesce(FTNTFGTcatdesc, catdesc),
          DnsResponseName             = coalesce(FTNTFGTipaddr, ipaddr),
          ThreatIpAddr                = coalesce(FTNTFGTbotnetip, botnetip),
          User1                      = coalesce(FTNTFGTuser, user),
          UnauthUser1                 = coalesce(FTNTFGTunauthuser, unauthuser)
      | extend 
          DnsQueryTypeName = case(
              DnsQueryTypeName == "Unknown","",
              DnsQueryTypeName
          )
      | lookup EventOriginalResultDetailsLookup on EventOriginalResultDetails
      | lookup DnsQueryTypeLookup on DnsQueryTypeName
      | invoke _ASIM_ResolveDvcFQDN ("Computer")
      | invoke _ASIM_ResolveNetworkProtocol("NetworkProtocolNumber")
      | extend 
          SrcUsername         = coalesce(User1, UnauthUser1),
          IpAddr              = SrcIpAddr,
          Src                 = SrcIpAddr,
          Dst                 = DstIpAddr,
          Dvc                 = DvcHostname,
          DnsResponseCodeName = EventResultDetails,
          EventType           = "Query",
          EventSchemaVersion  = "0.1.7",
          EventSchema         = "Dns",
          EventCount          = int(1),
          EventEndTime        = TimeGenerated,
          EventStartTime      = TimeGenerated,
          EventVendor         = "Fortinet",
          EventProduct        = "FortiGate",
          Domain              = DnsQuery,
          DomainCategory      = UrlCategory,
          SessionId           = DnsSessionId,
          DvcIdType           = "Other"
      | extend 
          User            = SrcUsername,
          SrcUsernameType = _ASIM_GetUsernameType(SrcUsername),
          SrcUserType     = _ASIM_GetUserType(SrcUsername, "")
      | project-away FTNTFGT*, logid, subtype, srccountry, dstcountry, srcintfrole, rcode,
        qname, qtype, xid, qtypeval, qclass, catdesc, ipaddr, unauthuser, user, botnetip, sessionid,
        User1, UnauthUser1, AdditionalExtensions, Computer, NetworkProtocolNumber
  };
  Parser(
    disabled = disabled
  )
  