Parser:
  Title: DNS activity ASIM filtering parser for Azure Firewall
  Version: '0.1.0'
  LastUpdated: Nov 19, 2025
Product:
  Name: Azure Firewall
Normalization:
  Schema: Dns
  Version: '0.1.7'
References:
- Title: ASIM DNS Schema
  Link: https://aka.ms/ASimDnsDoc
- Title: ASIM
  Link: https://aka.ms/AboutASIM
Description: |
  This ASIM filtering parser supports filtering and normalizing the Azure Firewall logs to the ASIM DNS activity normalized schema.
ParserName: vimDnsAZFW
EquivalentBuiltInParser: _Im_Dns_AZFW
ParserParams:
  - Name: starttime
    Type: datetime
    Default: datetime(null)
  - Name: endtime
    Type: datetime
    Default: datetime(null)
  - Name: srcipaddr
    Type: string
    Default: '*'
  - Name: domain_has_any
    Type: dynamic
    Default: dynamic([])
  - Name: responsecodename
    Type: string
    Default: '*'
  - Name: response_has_ipv4
    Type: string
    Default: '*'
  - Name: response_has_any_prefix
    Type: dynamic
    Default: dynamic([])
  - Name: eventtype
    Type: string
    Default: 'Query'
  - Name: disabled
    Type: bool
    Default: false
ParserQuery: |
 let parser = (
    starttime:datetime                = datetime(null)
    , endtime:datetime                = datetime(null)
    , srcipaddr:string                = '*'
    , domain_has_any:dynamic          = dynamic([])
    , responsecodename:string         = '*'
    , response_has_ipv4:string        = '*'
    , response_has_any_prefix:dynamic = dynamic([])
    , eventtype:string                = 'Query'
    , disabled:bool                   = false
  )
  {
    AZFWDnsQuery
    | where not(disabled)
    // Pre-filtering
    | where (isnull(starttime) or TimeGenerated >= starttime)
        and (isnull(endtime) or TimeGenerated <= endtime)
        and (srcipaddr == "*" or has_ipv4(SourceIp, srcipaddr)) 
        and (array_length(domain_has_any) == 0 or QueryName has_any (domain_has_any))
        and (responsecodename == '*' or ResponseCode == responsecodename)
        and (response_has_ipv4 == '*' or has_ipv4(SourceIp, response_has_ipv4))
        and (array_length(response_has_any_prefix) == 0 or has_any_ipv4_prefix(SourceIp, response_has_any_prefix))
        and (eventtype=='*' or eventtype in ("Query", "lookup")) // -- support both legacy and standard value
    // Extract Network Information
    | extend NetworkProtocol = toupper(Protocol)
    | extend DnsNetworkDuration = toint(RequestDurationSecs * 1000) // Convert to ms
    | extend EventResultDetails = iff(ResponseCode == "0", "NA", ResponseCode) // ResponseCode of 0 indicates a request
    | project-rename
        EventMessage = ErrorMessage,
        EventUid = _ItemId,
        Src = SourceIp,
        SrcPortNumber = SourcePort,
        DnsQuery = QueryName,
        DnsQueryType = QueryId,
        DnsQueryClassName = QueryClass,
        DnsResponseCode = ErrorNumber,
        DnsFlags = ResponseFlags
    | extend
        EventStartTime = TimeGenerated,
        EventEndTime = TimeGenerated,
        EventType = "Query",
        EventSubType =  iff(ResponseCode == "0", "request", "response"),
        EventSchema = "Dns",
        EventSchemaVersion = "0.1.7",
        EventVendor = "Microsoft",
        EventProduct = "Azure Firewall",
        EventCount = toint(1),
        EventOriginalUid = EventUid,
        EventResult = iff(ResponseCode == "NOERROR", "Success", "Failure"),
        DvcIdType = "AzureResourceId",
        SrcIpAddr = Src,
        SrcHostname = Src,
        Dvc = _ResourceId,
        DvcId = _ResourceId,
        DvcScopeId = _SubscriptionId,
        Hostname = Src,
        IpAddr = Src,
        Domain = DnsQuery,
        DnsResponseCodeName = EventResultDetails,
        DnsFlagsAuthenticated = DnsFlags has "aa",
        DnsFlagsAuthoritative = DnsFlags has "ad",
        DnsFlagsCheckingDisabled = DnsFlags has "cd",
        DnsFlagsRecursionAvailable = DnsFlags has "ra",
        DnsFlagsRecursionDesired = DnsFlags has "rd",
        DnsFlagsTruncated = DnsFlags has "tc",
        DnsQueryTypeName = trim(":", QueryType),
        Duration = DnsNetworkDuration,
        AdditionalFields = bag_pack(
            "DnssecOkBit", DnssecOkBit,
            "RequestSize", RequestSize,
            "EDNS0BufferSize", EDNS0BufferSize,
            "ResponseSize", ResponseSize,
            "SourceSystem", SourceSystem
        )
    | project
        EventStartTime,
        EventEndTime,
        EventType,
        EventSubType,
        EventSchema,
        EventSchemaVersion,
        EventVendor,
        EventProduct,
        EventCount,
        EventResultDetails,
        EventOriginalUid,
        EventUid,
        EventResult,
        Type,
        Src,
        SrcIpAddr,
        SrcPortNumber,
        SrcHostname,
        DnsQuery,
        DnsQueryType,
        DnsQueryTypeName,
        DnsQueryClassName,
        DnsResponseCode,
        DnsResponseCodeName,
        DnsFlags,
        DnsFlagsAuthenticated,
        DnsFlagsAuthoritative,
        DnsFlagsCheckingDisabled,
        DnsFlagsRecursionAvailable,
        DnsFlagsRecursionDesired,
        DnsFlagsTruncated,
        DnsNetworkDuration,
        Duration,
        Domain,
        NetworkProtocol,
        Dvc,
        DvcId,
        DvcIdType,
        DvcScopeId,
        Hostname,
        IpAddr,
        TimeGenerated,
        AdditionalFields,
        _BilledSize,
        _IsBillable,
        _ResourceId,
        _SubscriptionId,
        TenantId
  };
  parser (
      starttime                 = starttime
      , endtime                 = endtime
      , srcipaddr               = srcipaddr
      , domain_has_any          = domain_has_any
      , responsecodename        = responsecodename
      , response_has_ipv4       = response_has_ipv4
      , response_has_any_prefix = response_has_any_prefix
      , eventtype               = eventtype
      , disabled                = disabled
    )