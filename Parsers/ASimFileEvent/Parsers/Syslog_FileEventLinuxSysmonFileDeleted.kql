// For reference, see: https://github.com/Azure/Azure-Sentinel/blob/master/Parsers/ASimFileEvent/ProductParsers/FileEventLinuxSysmonFileDeleted.yaml
source
| where is_sentinel_workspace(TenantId)
| where SyslogMessage contains ('<Provider Name="Linux-Sysmon"') 
    and (SyslogMessage contains ('<EventID>23</EventID>') or SyslogMessage contains ('<EventID>26</EventID>'))
| parse SyslogMessage with  
    '<Event><System><Provider Name="Linux-Sysmon" Guid="{ff032593-a8d3-4f13-b0d6-01fc615a0f97}"/><EventID>'msgEventId:string'</EventID>'
    *
    '<EventRecordID>'msgEventRecordID:string'</EventRecordID>'
    *
    '<Computer>'msgComputer:string'</Computer>'
    '<Security UserId="'msgUserId:string'"/>'
    *
    '<Data Name="ProcessGuid">{'msgProcessGuid:string'}</Data>'
    '<Data Name="ProcessId">'msgProcessId:string'</Data>'
    '<Data Name="User">'msgUser:string'</Data>'
    '<Data Name="Image">'msgImage:string'</Data>'
    '<Data Name="TargetFilename">'msgTargetFilename:string'</Data>'
    '<Data Name="Hashes">'msgHashes:string'</Data>' *	
| extend
    EventCount=int(1)
, EventStartTime =TimeGenerated
, EventEndTime=TimeGenerated
, EventType = 'FileDeleted'
, EventResult ='Success' 
, EventProduct='Sysmon for Linux'
, EventProductVersion='v13.22' 
, EventVendor ='Microsoft'
, EventSchemaVersion ='0.1.0'
, DvcOs = 'Linux'
, TargetFilePathType='Unix'
, ActorUsernameType='Simple'
| project-rename
    DvcHostname=Computer
, EventOriginalUid=msgEventRecordID
, EventOriginalType =msgEventId 
, ActorUsername=msgUser
, ActingProcessName =msgImage
, ActingProcessId=msgProcessId
, ActingProcessGuid=msgProcessGuid
, TargetFilePath =msgTargetFilename
// ------  Alias
| extend
    Process=ActingProcessName
    , FilePath=TargetFilePath
    , Dvc =DvcHostname
    , User=ActorUsername
| project-away SyslogMessage