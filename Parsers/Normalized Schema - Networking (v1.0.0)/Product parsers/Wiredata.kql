// Usage Instruction : 
// Paste below query in Sentinel, click on Save button and select as Function from drop down by specifying function name and alias.
// Function usually takes 10-15 minutes to activate. You can then use function alias from any other queries
// Reference: Using functions in Azure monitor log queries: https://docs.microsoft.com/azure/azure-monitor/log-query/functions
// Reference: Sentinel normalization documentation: https://aka.ms/sentinelnormalizationdocs
//
// Note: This parser only populates and shows relevant fields within the networking schema. Please use the meta parser to see all relevant fields, or union this parser with the empty network schema. 
// Parser Version: 1.0.0
// Schema version: 1.0.0
// Alias name: WireData-Network-NormalizedParser (please change hypens(-) to underscores(_) when using the alias)
let ParserWireData=(){ WireData
| extend EventType = "Traffic"
  , EventSchemaVersion="1.0.0"
  , EventCount=tolong(1) 
  , EventVendor = "Microsoft"
  , EventProduct = "WireData"
  , EventResult = "Success"
  , EventTimeIngested = ingestion_time()
  , EventOriginalUid = _ItemId
  , DstIpAddr =  iff(Direction == "Outbound", RemoteIP, LocalIP )
  , DstPortNumber =  iff(Direction == "Outbound", LocalPortNumber, RemotePortNumber)
  , SrcIpAddr = iff(Direction == "Outbound", LocalIP, RemoteIP  )
  , SrcPortNumber = iff(Direction == "Outbound", LocalPortNumber, RemotePortNumber )
  , SrcZone = iff(Direction == "Outbound", LocalSubnet, "")
  , DstZone = iff(Direction == "Inbound", LocalSubnet, "")
  , NetworkSessionId = tostring(SessionID)
  , EventSeverity = ""
| project-rename 
	 DvcHostname = Computer, 
	 EventEndTime = SessionEndTime, 
	 EventStartTime = SessionStartTime, 
	 EventResourceId = _ResourceId,
	 NetworkApplicationProtocol =  ApplicationProtocol,
	 SrcBytes = SentBytes,
	 DstBytes = ReceivedBytes,
	 NetworkBytes= TotalBytes,
	 NetworkDirection =Direction,
	 NetworkProtocol = ProtocolName,
	 SrcPackets = SentPackets,
	 DstPackets = ReceivedPackets
};
ParserWireData