//Name: Large number of attempts by a given client IP on many ports
// Description: This query shows when 30 or more ports are used for a given client IP in 10 minutes occurring on a given server
// This could be indicative of attempted scanning.  This could also simply indicate a misconfigured service or device. 
//
// Status code mappings for your convenience, also inline if the mapping is not available
// IIS status code mapping - https://support.microsoft.com/en-us/help/943891/the-http-status-code-in-iis-7-0-iis-7-5-and-iis-8-0
// Win32 Status code mapping - https://msdn.microsoft.com/en-us/library/cc231199.aspx
//
// Id: 44a555d8-ecee-4a25-95ce-055879b4b14b
//
// Severity: Medium
//
// QueryFrequency: 10m
//
// QueryPeriod: 10m
//
// AlertTriggerOperator: gt
//
// AlertTriggerThreshold: 30
//
// DataSource: W3CIISLog
//
// Tactics: #Discovery, #PrivilegeEscalation
//
let timeFrame = ago(10m);
W3CIISLog
| where TimeGenerated >= timeFrame
| summarize FailStartTimeUtc = min(TimeGenerated), FailEndTimeUtc = max(TimeGenerated), makeset(sSiteName) , makeset(csMethod), ConnectionCount = count() by Computer, sIP, cIP, sPort, scStatus, scSubStatus, scWin32Status
| extend scStatusFull = strcat(scStatus, ".",scSubStatus) 
// Map common IIS codes
| extend scStatusFull_Friendly = case(
scStatusFull == "401.0", "Access denied.",
scStatusFull == "401.1", "Logon failed.",
scStatusFull == "401.2", "Logon failed due to server configuration.",
scStatusFull == "401.3", "Unauthorized due to ACL on resource.",
scStatusFull == "401.4", "Authorization failed by filter.",
scStatusFull == "401.5", "Authorization failed by ISAPI/CGI application.",
scStatusFull == "403.0", "Forbidden.",
scStatusFull == "403.4", "SSL required.",
"See - https://support.microsoft.com/en-us/help/943891/the-http-status-code-in-iis-7-0-iis-7-5-and-iis-8-0")
// Mapping to Hex so can be mapped using website in comments above
| extend scWin32Status_Hex = tohex(tolong(scWin32Status)) 
// Map common win32 codes
| extend scWin32Status_Friendly = case(
scWin32Status_Hex =~ "52e", "Logon failure: Unknown user name or bad password.", 
scWin32Status_Hex =~ "533", "Logon failure: Account currently disabled.", 
scWin32Status_Hex =~ "2ee2", "The request has timed out.", 
scWin32Status_Hex =~ "0", "The operation completed successfully.", 
scWin32Status_Hex =~ "1", "Incorrect function.", 
scWin32Status_Hex =~ "2", "The system cannot find the file specified.", 
scWin32Status_Hex =~ "3", "The system cannot find the path specified.", 
scWin32Status_Hex =~ "4", "The system cannot open the file.", 
scWin32Status_Hex =~ "5", "Access is denied.", 
scWin32Status_Hex =~ "8009030e", "SEC_E_NO_CREDENTIALS", 
scWin32Status_Hex =~ "8009030C", "SEC_E_LOGON_DENIED", 
"See - https://msdn.microsoft.com/en-us/library/cc231199.aspx")
| summarize FailStartTimeUtc = min(FailStartTimeUtc), FailEndTimeUtc = max(FailEndTimeUtc), set_sSiteName = makeset(set_sSiteName) , makeset(sPort) , set_csMethod = makeset(set_csMethod), makeset(scStatus), makeset(scSubStatus), makeset(scWin32Status), makeset(scStatusFull), makeset(scStatusFull_Friendly), makeset(scWin32Status_Hex), makeset(scWin32Status_Friendly), sum(ConnectionCount) by Computer, sIP, cIP
| extend sPortCount = arraylength(set_sPort)
| where sPortCount >= 30
| project FailStartTimeUtc, FailEndTimeUtc, Computer, set_sSiteName, sIP, cIP, set_csMethod, set_scStatus, set_scSubStatus, set_scWin32Status, set_sPort, sum_ConnectionCount, sPortCount, set_scStatusFull, set_scStatusFull_Friendly, set_scWin32Status_Hex, set_scWin32Status_Friendly