// Name: Threat Intel detection on IP in last 6 hours not seen in previous 30 days
// Description: These are new TI detections on a given IP and should be reviewed to determine if they are a threat
//
// Id: b3285f15-03b8-4468-8a92-1377ca377c46
//
// Severity: Low
//
// QueryFrequency: 6h
//
// QueryPeriod: 30d
//
// AlertTriggerOperator: gt
//
// AlertTriggerThreshold: 0
//
// DataConnector: #CommonSecurityLog; DataTypes: #Firewall
//
// Tactics: #CommandAndControl, #InitialAccess
//
let timeframe = 6h;
let last1dfull =
CommonSecurityLog
| where TimeGenerated >= ago(timeframe)
| where tolower(SimplifiedDeviceAction) !in ('deny', 'drop', 'block', 'reset-server', 'reset-both', 'reset-server')
| where isnotempty(MaliciousIP)
| where Message !contains "timed out";
let last1d = 
CommonSecurityLog
| where TimeGenerated >= ago(timeframe)
| where tolower(SimplifiedDeviceAction) !in ('deny', 'drop', 'block', 'reset-server', 'reset-both', 'reset-server')
| where isnotempty(MaliciousIP)
| where Message !contains "timed out"
| summarize todayCount = count() by DeviceVendor, DeviceProduct, DeviceName, ThreatConfidence, MaliciousIP, IndicatorThreatType, Message
;
let prev30d = CommonSecurityLog
| where TimeGenerated between (ago(30d) .. ago(6h))
| where tolower(SimplifiedDeviceAction) !in ('deny', 'drop', 'block', 'reset-server', 'reset-both', 'reset-server')
| where isnotempty(MaliciousIP)
| where Message !contains "timed out"
| summarize by DeviceVendor, DeviceProduct, DeviceName, ThreatConfidence, MaliciousIP, IndicatorThreatType, Message;
let newToday = last1d | join kind= leftanti (
   prev30d 
) on DeviceVendor, DeviceProduct, DeviceName, ThreatConfidence, MaliciousIP, IndicatorThreatType, Message
;
last1dfull | join (
   newToday 
) on DeviceVendor, DeviceProduct, DeviceName, ThreatConfidence, MaliciousIP, IndicatorThreatType, Message 
| summarize makeset(Message), sum(todayCount)  by DeviceVendor, DeviceProduct, DeviceName, MaliciousIP, IndicatorThreatType, ThreatConfidence, MaliciousIPCountry
| project DeviceVendor, DeviceProduct, DeviceName, MaliciousIP, IndicatorThreatType, set_Message, FailureMessageCount = arraylength(set_Message), ThreatConfidence, MaliciousIPCountry, sum_todayCount 
