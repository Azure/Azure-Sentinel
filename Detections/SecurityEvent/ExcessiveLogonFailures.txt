// Name: Excessive logon failures
// Description: User has over 50 logon failures today and at least 25% of the count of logon failures previous 7 days
//
// Id: 2391ce61-8c8d-41ac-9723-d945b2e90720
//
// Severity: Low
//
// QueryFrequency: 1d
//
// QueryPeriod: 7d
//
// DataSource: #SecurityEvent
//
// Techniques: #LateralMovement, #Probing
//
let timeframe = 7d;
let threshold = 0.25;
let countlimit = 50;
SecurityEvent
| where TimeGenerated >= ago(1d)
| where EventID == 4625
| where AccountType == "User"
| summarize CountToday = count() by EventID, Account, LogonTypeName, SubStatus, AccountType, Computer, WorkstationName, IpAddress
| join (
    SecurityEvent 
    | where TimeGenerated >= ago(timeframe)
    | where EventID == 4625
    | where AccountType == "User"
    | summarize CountLast7day = count() by EventID, Account, LogonTypeName, SubStatus, AccountType, Computer, WorkstationName, IpAddress
) on EventID, Account, LogonTypeName, SubStatus, AccountType, Computer, WorkstationName, IpAddress
| where CountToday >= CountLast7day*threshold and CountToday >= countlimit
| project EventID, Account, LogonTypeName, SubStatus, AccountType, Computer, WorkstationName, IpAddress, CountToday, CountLast7day, Avg7Day = CountLast7day/7