// Name: New User created and then added to builtin\administrators group
// Description: User account was created and then added to the builtin Administrators group in the same day
//
// Id: aa1eff90-29d4-49dc-a3ea-b65199f516db
//
// Severity: Low
//
// QueryFrequency: 1d
//
// QueryPeriod: 1d
//
// AlertTriggerOperator: gt
//
// AlertTriggerThreshold: 0
//
// DataSource: #SecurityEvent
//
// Techniques: #Persistence, #Discovery, #LateralMovement, #Collection
//
let timeframe = 1d;
SecurityEvent
| where TimeGenerated > ago(timeframe) 
| where EventID == 4720
| where AccountType == "User"
| project CreatedUserTime = TimeGenerated, CreatedUserEventID = EventID, CreatedUserActivity = Activity, Computer = toupper(Computer), CreatedUser = tolower(TargetUserName), Domain = toupper(TargetDomainName), CreatedUserSid = TargetSid, AccountUsedToCreateUser = SubjectUserName
|join (
SecurityEvent 
| where TimeGenerated > ago(timeframe) 
| where AccountType == "User"
// 4732 - A member was added to a security-enabled local group
| where EventID == 4732
//TargetSid is the builin Admins group: S-1-5-32-544
| where TargetSid == "S-1-5-32-544"
| project GroupAddTime = TimeGenerated, GroupAddEventID = EventID, GroupAddActivity = Activity, Computer = toupper(Computer), GroupName = TargetUserName, Domain = toupper(TargetDomainName), GroupSid = TargetSid, UserAdded = SubjectUserName, UserAddedSid = SubjectUserSid, CreatedUser = tolower(SubjectUserName)
)
on CreatedUser, Domain
//Create User first, then the add to the group.
| where CreatedUserTime < GroupAddTime
| project Computer, CreatedUserTime, CreatedUserEventID, CreatedUserActivity, CreatedUser, CreatedUserSid, Domain, GroupAddTime, GroupAddEventID, GroupAddActivity, AccountUsedToCreateUser, GroupName, GroupSid, UserAdded, UserAddedSid 