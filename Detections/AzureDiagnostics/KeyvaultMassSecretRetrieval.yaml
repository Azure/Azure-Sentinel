id: 24f8c234-d1ff-40ec-8b73-96b17a3a9c1c
name: Mass secret retrieval from Azure Key Vault
description: |
  'Identifies mass secret retrieval from Azure Key Vault observed by a single user. 
  Mass secret retrival crossing a certain threshold is an indication of credential dump operations or mis-configured applications. 
  You can tweak the EventCountThreshold based on average count seen in your environment 
  and also filter any known sources (IP/Account) and useragent combinations based on historical analysis to further reduce noise'
severity: Low
requiredDataConnectors:
  - connectorId: AzureMonitor(Keyvault)
    dataTypes:
      - AzureDiagnostics
queryFrequency: 1d
queryPeriod: 1d                
triggerOperator: gt
triggerThreshold: 0
tactics:
  - CredentialAccess
relevantTechniques:
  - T1003
query: |

  let timeframe =1d;
  let EventCountThreshold = 25;
  let OperationList = dynamic(
  ["SecretGet", "KeyGet", "VaultGet"]);
  AzureDiagnostics
  | where TimeGenerated > ago(timeframe)
  | extend ResultType = columnifexists("ResultType", "None"), identity_claim_http_schemas_microsoft_com_identity_claims_objectidentifier_g = columnifexists("identity_claim_http_schemas_microsoft_com_identity_claims_objectidentifier_g", "None")
  | where ResultType !~ "None" and isnotempty(ResultType)
  | where identity_claim_http_schemas_microsoft_com_identity_claims_objectidentifier_g !~ "None" and isnotempty(identity_claim_http_schemas_microsoft_com_identity_claims_objectidentifier_g)
  | where ResourceType =~ "VAULTS" and ResultType =~ "Success"
  | where OperationName in (OperationList) 
  | summarize count() by identity_claim_http_schemas_microsoft_com_identity_claims_objectidentifier_g, OperationName
  | where count_ > EventCountThreshold  
  | join (
  AzureDiagnostics
  | where TimeGenerated > ago(timeframe)
  | extend ResultType = columnifexists("ResultType", "NoResultType")
  | extend requestUri_s = columnifexists("requestUri_s", "None"), identity_claim_http_schemas_microsoft_com_identity_claims_objectidentifier_g = columnifexists("identity_claim_http_schemas_microsoft_com_identity_claims_objectidentifier_g", "None")
  | extend id_s = columnifexists("id_s", "None"), CallerIPAddress = columnifexists("CallerIPAddress", "None"), clientInfo_s = columnifexists("clientInfo_s", "None")
  | where ResultType !~ "None" and isnotempty(ResultType)
  | where identity_claim_http_schemas_microsoft_com_identity_claims_objectidentifier_g !~ "None" and isnotempty(identity_claim_http_schemas_microsoft_com_identity_claims_objectidentifier_g)
  | where id_s !~ "None" and isnotempty(id_s)
  | where CallerIPAddress !~ "None" and isnotempty(CallerIPAddress)
  | where clientInfo_s !~ "None" and isnotempty(clientInfo_s)
  | where requestUri_s !~ "None" and isnotempty(requestUri_s)
  | where OperationName in~ (OperationList)   
  ) on identity_claim_http_schemas_microsoft_com_identity_claims_objectidentifier_g 
  | summarize EventCount=sum(count_), StartTimeUtc=min(TimeGenerated), EndTimeUtc=max(TimeGenerated), TimeTriggered=makelist(TimeGenerated),OperationNameList=make_set(OperationName), RequestURLList=make_set(requestUri_s), CallerIPList = make_set(CallerIPAddress),  CallerIPMax= arg_max(CallerIPAddress,*) by ResourceType, ResultType, Resource, id_s, identity_claim_http_schemas_microsoft_com_identity_claims_objectidentifier_g, clientInfo_s
  | extend timestamp = EndTimeUtc, IPCustomEntity = CallerIPMax, AccountCustomEntity = identity_claim_http_schemas_microsoft_com_identity_claims_objectidentifier_g