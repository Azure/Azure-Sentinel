// Name: Malformed user agent
//
// Id: a357535e-f722-4afe-b375-cff362b2b376
//
// Description:
// Malware authors will sometimes hardcode user agent string values when writing the network communication component of their malware.
// Malformed user agents can be an indication of such malware.
//
// Severity: Medium
//
// QueryFrequency: 24
//
// QueryPeriod: 24
//
// AlertTriggerOperator: gt
//
// AlertTriggerThreshold: 0
//
// Data Source: OfficeActivity,AzureDiagnostics
//
// Techniques: #InitialAccess, #C2, #Exfiltration
//
(union isfuzzy=true
(OfficeActivity | where UserAgent != ""),
(OfficeActivity 
| where RecordType in ("AzureActiveDirectoryAccountLogon", "AzureActiveDirectoryStsLogon")
| extend UserAgent = extractjson("$[0].Value", ExtendedProperties, typeof(string))),
(AzureDiagnostics | where ResourceType == "APPLICATIONGATEWAYS" 
| where OperationName == "ApplicationGatewayAccess" 
| extend UserAgent = replace(@'\+', @' ', userAgent_s)))
// Likely artefact of hardcoding
| where UserAgent startswith "User" or UserAgent startswith "\""
// Incorrect casing
or (UserAgent startswith "Mozilla" and not(UserAgent containscs "Mozilla"))
// Incorrect casing
or UserAgent containscs  "(Compatible;"
// Missing MSIE version
or UserAgent matches regex @"MSIE\s?;"
// Incorrect spacing around MSIE version
or UserAgent matches regex  @"MSIE(?:\d|.{1,5}?\d\s;)"
| summarize by UserAgent, OperationName, RecordType

