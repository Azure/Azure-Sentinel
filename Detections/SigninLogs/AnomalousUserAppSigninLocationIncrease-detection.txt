// Name: anomalous sign-in location by user account and authenticating application
//
// Id: 7cb8f77d-c52f-4e46-b82f-3cf2e106224a
//
// Description: This query over Azure Active Directory sign-in considers all user sign-ins for each Azure Active 
// Directory application and picks out the most anomalous change in location profile for a user within an 
// individual application. An alert is generated for recent sign-ins that have location counts that are anomalous
// over last day but also over the last 7-day and 30-day periods.
//
// DataSource: #SigninLogs
//
// Severity: Medium
//
// QueryFrequency: 24
//
// QueryPeriod: 24
//
// AlertTriggerOperator: gt
//
// AlertTriggerThreshold: 0
//
// Techniques: #InitialAccess
//
SigninLogs 
// Forces Log Analytics to recognize that the query should be run over full time range
| where TimeGenerated >= ago(30d)
| extend  locationString= strcat(tostring(LocationDetails["countryOrRegion"]), "/", tostring(LocationDetails["state"]), "/", tostring(LocationDetails["city"]), ";") 
| project TimeGenerated, AppDisplayName , UserPrincipalName, locationString 
// Create time series 
| make-series dLocationCount = dcount(locationString) on TimeGenerated in range(startofday(ago(30d)),now(), 1d) 
by UserPrincipalName, AppDisplayName 
// Compute best fit line for each entry 
| extend (RSquare,Slope,Variance,RVariance,Interception,LineFit)=series_fit_line(dLocationCount) 
// Chart the 3 most interesting lines  
// A 0-value slope corresponds to an account being completely stable over time for a given Azure Active Directory application
| where Slope >0.3
| top 50 by Slope desc
| join kind = leftsemi (
SigninLogs 
// Forces Log Analytics to recognize that the query should be run over full time range
| where TimeGenerated >= ago(7d)
| extend  locationString= strcat(tostring(LocationDetails["countryOrRegion"]), "/", tostring(LocationDetails["state"]), "/", tostring(LocationDetails["city"]), ";") 
| project TimeGenerated, AppDisplayName , UserPrincipalName, locationString 
| make-series dLocationCount = dcount(locationString) on TimeGenerated in range(startofday(ago(7d)) ,now(), 1d) 
by UserPrincipalName, AppDisplayName 
| extend (RSquare,Slope,Variance,RVariance,Interception,LineFit)=series_fit_line(dLocationCount) 
| top 50 by Slope desc
| where Slope >0.3
) on UserPrincipalName, AppDisplayName
| join kind = leftsemi (
SigninLogs 
| where TimeGenerated >= ago(1d)
| extend  locationString= strcat(tostring(LocationDetails["countryOrRegion"]), "/", tostring(LocationDetails["state"]), "/", tostring(LocationDetails["city"]), ";") 
| project TimeGenerated, AppDisplayName , UserPrincipalName, locationString 
| make-series dLocationCount = dcount(locationString) on TimeGenerated in range(startofday(ago(1d)) ,now(), 1d) 
by UserPrincipalName, AppDisplayName 
| extend (RSquare,Slope,Variance,RVariance,Interception,LineFit)=series_fit_line(dLocationCount) 
| top 50 by Slope desc
// Higher threshold requirement on last day anomaly
| where Slope >5
) on UserPrincipalName, AppDisplayName
