id: ec21493c-2684-4acd-9bc2-696dbad72426
name: (Preview) TI map Domain entity to PaloAlto
description: |
  'Identifies a match in Palo Alto data in CommonSecurityLog table from any Domain IOC from TI'
severity: Medium
requiredDataConnectors:
  - connectorId: PaloAltoNetworks
    dataTypes:
      - CommonSecurityLog
  - connectorId: ThreatIntelligence
    dataTypes:
      - ThreatIntelligenceIndicator
queryFrequency: 1h
queryPeriod: 14d
triggerOperator: gt
triggerThreshold: 0
tactics:
  - Impact
query: |
    let dt_lookBack = 1h;
    let ioc_lookBack = 14d;
    //Create a list of TLDs in our threat feed for later validation of extracted domains
    let list_tlds = ThreatIntelligenceIndicator
        | where TimeGenerated > ago(ioc_lookBack)
        | where isnotempty(DomainName)
        | extend parts = split(DomainName, '.')
        | extend tld = parts[(array_length(parts)-1)]
        | summarize count() by tostring(tld)
        | summarize makelist(tld);
        ThreatIntelligenceIndicator
        | where TimeGenerated >= ago(ioc_lookBack) and ExpirationDateTime > now()
        | where Active == true
        // Picking up only IOC's that contain the entities we want
        | where isnotempty(DomainName)
        | summarize LatestIndicatorTime = arg_max(TimeGenerated, *) by IndicatorId
        | join (
            CommonSecurityLog
            | where TimeGenerated > ago(dt_lookBack)
            | where DeviceVendor == 'Palo Alto Networks'
            | where DeviceEventClassID == 'url'
            //Uncomment the line below to only alert on allowed connections
            //| where DeviceAction != "block-url"
            //Extract domain from RequestURL, if not present extarct it from AdditionalExtentions
            | extend Url = columnifexists("RequestURL", "None")
            | extend Url = iif(Url == "", extract("([^\"]+)", 1, tolower(AdditionalExtensions)), Url)
            | extend Url = iif(Url !startswith "http://" and ApplicationProtocol != "ssl", strcat('http://', trim('"', RequestURL)), iif(Url !startswith "https://" and ApplicationProtocol == "ssl", strcat('https://', trim('"', RequestURL)), RequestURL))
            | extend Domain = trim(@"""",tostring(parse_url(Url).Host))
            | where isnotempty(Domain)
            | extend parts = split(Domain, '.')
            //Split out the TLD for the purpose of checking if we have any TI indicators with this TLD to match on
            | extend tld = parts[(array_length(parts)-1)]
            //Validate parsed domain by checking TLD against TLDs from threat feed and drop domains where there is no chance of a match
            | where tld in~ (list_tlds)
            | extend CSL_TimeGenerated = TimeGenerated
        ) on $left.DomainName==$right.Domain
        | project LatestIndicatorTime, Description, ActivityGroupNames, RequestURL, Domain, IndicatorId, ThreatType, ExpirationDateTime, ConfidenceScore, CSL_TimeGenerated, DeviceAction, DestinationIP, DestinationPort, DeviceName, SourceIP, SourcePort, ApplicationProtocol, RequestMethod
        | extend timestamp = CSL_TimeGenerated, IPCustomEntity = SourceIP, HostCustomEntity = DeviceName
