// Name: DNS high reverse DNS count
// Id: 15ae38a2-2e29-48f7-883f-863fb25a5a06
// 
// Description: clients with a high reverse DNS count could be carrying out scanning activity.
// Alert is generated if the IP performing such reverse DNS lookups was not seen doing so
// in the preceding 7-day period.
//
// DataSource: #DnsEvents
//
// Severity: Medium
//
// QueryFrequency: 24
//
// QueryPeriod: 24
//
// AlertTriggerOperator: gt
//
// AlertTriggerThreshold: 0
//
// Techniques: #Discovery
//
DnsEvents 
| where TimeGenerated > ago(1d)
| where Name contains "in-addr.arpa" 
| summarize dcount(Name) by ClientIP
| where dcount_Name >10
| project ClientIP , dcount_Name 
| join kind=leftanti (DnsEvents 
    | where TimeGenerated between(ago(8d)..ago(1d))
    | where Name contains "in-addr.arpa" 
    | summarize dcount(Name) by ClientIP, bin(TimeGenerated, 1d)
    | where dcount_Name >10
    | project ClientIP , dcount_Name 
) on ClientIP
