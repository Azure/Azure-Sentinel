Function:
  Title: Enrich events with data from the IdentityInfo UEBA table 
  Version: '0.1'
  LastUpdated: Apr 27th 2022
References:
- Title: ASIM
  Link: https://aka.ms/AboutASIM
- Title: The IdentityInfo table
  Link: https://learn.microsoft.com/azure/sentinel/ueba-reference#identityinfo-table
Description: |
  This ASIM tabular function enabled enriching your result set with user information from the [UEBA IdentityInfo table](https://learn.microsoft.com/azure/sentinel/ueba-reference#identityinfo-table). For more information on how to use this enrichment function refer to [ASIM functions](https://learn.microsoft.com/azure/sentinel/normalization-functions).
    
FunctionName: ASIM_Enrich_IdentityInfo
EquivalentBuiltInFunction: _Enrich_IdentityInfo
FunctionParams:
  - Name: T
    Type: table:(*)
  - Name: AadIdField
    Type: string
    Default: ''
  - Name: TenantIdField
    Type: string
    Default: ''
  - Name: SidField
    Type: string
    Default: ''
  - Name: UpnField
    Type: string
    Default: ''
  - Name: EmailField
    Type: string
    Default: ''

FunctionQuery: |
    let AADinfo = 
        IdentityInfo
        | where TimeGenerated > ago(14d)
        | where AccountObjectId != ""
        | project-away AccountName, AccountDomain, GivenName, Surname, Type, TenantId, OnPremisesExtensionAttributes, UserState, UserStateChangedOn, SourceSystem
        | project-away InvestigationPriority, InvestigationPriorityPercentile, ExtensionProperty, AccountCloudSID, Applications, ServicePrincipals
        | summarize arg_max(TimeGenerated, *) by UserScopeId = AccountTenantId, UserId = AccountObjectId
        | project-away TimeGenerated
        | project-rename
            Username = AccountUpn,
            UserDisplayName = AccountDisplayName,
            UserSid = AccountSID,
            UserDN = OnPremisesDistinguishedName,
            UserTags = Tags,
            UserRiskLevel = RiskLevel,
            UserRiskLevelDetails = RiskLevelDetails,
            UserRiskState = RiskState, 
            UserBlastRadius = BlastRadius,
            UserGroupMembership = GroupMembership,
            UserAssignedRoles = AssignedRoles,
            UserDepartment = Department,
            UserEmployeeId = EmployeeId,
            UserJobTitle = JobTitle,
            UserMailAddress = MailAddress,
            UserAdditionalMailAddresses = AdditionalMailAddresses,
            UserManager = Manager,
            UserStreetAddress = StreetAddress,
            UserCity = City,
            UserCountry = Country,
            UserRegion = State,
            UserPhone = Phone,
            UserAccountEnabled = IsAccountEnabled,
            UserAccountCreationTime = AccountCreationTime,
            RelatedUsers = RelatedAccounts,
            UserDeleted = DeletedDateTime,
            UserLastSeen = LastSeenDate,
            UserUACFlags = UACFlags,
            UserIsMFARegistered = IsMFARegistered,
            UserRiskScore = EntityRiskScore
        | extend 
            UsernameType = "UPN",
            UserIdType = "AadId",
            UserType = iff(UserType == "Guest", UserType, ""),
            UserUpn = Username;
    let EnrichAadId = T 
    | where AadIdField != ""
    | extend 
        UserId = column_ifexists(AadIdField,''),
        UserScopeId = column_ifexists(TenantIdField,''), 
        ASimIdentityInfoMatch="EnrichAadId"
    | lookup AADinfo on UserScopeId, UserId;
    let EnrichSid = T 
    | where AadIdField == "" and SidField != ""
    | extend UserSid = column_ifexists(SidField,''), ASimIdentityInfoMatch="EnrichSid"
    | lookup AADinfo on UserSid;
    let EnrichUpn = T 
    | where AadIdField == "" and SidField == "" and UpnField != ""
    | extend Username = column_ifexists(UpnField,''), ASimIdentityInfoMatch="EnrichUpn"
    | lookup AADinfo on Username;
    let EnrichEmail = T 
    | where AadIdField == "" and SidField == "" and UpnField == "" and EmailField != ""
    | extend UserMailAddress = column_ifexists(EmailField,''), ASimIdentityInfoMatch="EnrichEmail"
    | lookup AADinfo on UserMailAddress;
    let default = T 
    | where AadIdField == "" and SidField == "" and UpnField == "" and EmailField == ""  
    | extend ASimIdentityInfoMatch="None";
    union EnrichAadId, EnrichSid, EnrichUpn, EnrichEmail, default