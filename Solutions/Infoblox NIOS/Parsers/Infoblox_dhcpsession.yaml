id: ce7e152d-8e0e-4e6e-8d3d-d60af96fe6e2
Function:
  Title: Parser for Infoblox_dhcpsession
  Version: '1.0.0'
  LastUpdated: '2023-08-23'
Category: Microsoft Sentinel Parser
FunctionName: Infoblox_dhcpsession
FunctionAlias: Infoblox_dhcpsession
FunctionQuery: |
    let datasource = (_GetWatchlist('Sources_by_SourceType')| where SearchKey == 'InfobloxNIOS' | project Source);
    let RawData = Syslog
        | where Computer in (datasource)
        | where SyslogMessage has "r-l-e"
        | extend Parser = extract_all(@"^(\d{2}\-[a-zA-Z]{3}\-\d{4}\s[0-9\.\:]+)?\s?([a-zA-Z-_]+)(\s|\:)?(.*)", dynamic([1,2,3,4]), SyslogMessage)[0]
        | extend responseTime = todatetime(Parser[0]),
                 Log_Type = tostring(Parser[1]),
                 RawData_subString = tostring(Parser[3])
        | project-away Parser;
    RawData
        | extend dhcpsession = extract_all(@"\:?([0-9.]+)\,([a-zA-Z]+)\,([a-zA-Z0-9-]+)\,([a-z0-9:]+)\,([0-9]+)\,([0-9]+)\,([0-9]+)?\,([\w$\s]+)\,([0-9.]+)\,([0-9]+)\,([0-9-.]+)", dynamic([1,2,3,4,5,6,7,8,9,10,11]), RawData_subString)[0]
        | extend DvcIpAddr = tostring(dhcpsession[0]),  
            Signature = tostring(dhcpsession[1]),
            SrcHostname = tostring(dhcpsession[2]),  
            DvcMacAddr = tostring(dhcpsession[3]),
            StartTime = tostring(dhcpsession[4]),  
            EndTime = tostring(dhcpsession[5]),  
            Network = tostring(dhcpsession[8]),
            NetworkRange = tostring(dhcpsession[10])
        | project-away SyslogMessage, dhcpsession,RawData_subString;