id: e3fbab01-ddf8-4ab2-b663-3c33e92f55b3
Function:
  Title: Parser for Infoblox_dhcprequest
  Version: '1.0.0'
  LastUpdated: '2023-08-23'
Category: Microsoft Sentinel Parser
FunctionName: Infoblox_dhcprequest
FunctionAlias: Infoblox_dhcprequest
FunctionQuery: |
    let datasource = (_GetWatchlist('Sources_by_SourceType')| where SearchKey == 'InfobloxNIOS' | project Source);
    let RawData = Syslog 
        | where Computer in (datasource)
        | where SyslogMessage has "DHCPREQUEST"
        | extend Parser = extract_all(@"^(\d{2}\-[a-zA-Z]{3}\-\d{4}\s[0-9\.\:]+)?\s?([a-zA-Z-_]+)(\s|\:)?(.*)", dynamic([1,2,3,4]), SyslogMessage)[0]
        | extend responseTime = todatetime(Parser[0]),
                 Log_Type = tostring(Parser[1]),
                 RawData_subString = tostring(Parser[3])
        | project-away Parser;
    RawData
        | extend dhcprequest = extract_all(@"for\s((\S+)(\s\(\S+\))?)\sfrom\s((\S+)(\s\(\S+\))?)\svia\s(\S+)(\sTransID\s([a-z0-9]+)\s)?(uid\s([a-z0-9:]+)|uid$|uid)?(\s\(([a-zA-Z]+)\)$)?", dynamic([1,2,3,4,5,6,7,8,9,10,11,12]), RawData_subString)[0]
        | extend SrcIpAddr = tostring(dhcprequest[1]), 
            SrcMacAddr = tostring(dhcprequest[4]),
            SrcHostname = extract(@"\((\S+)\)",1,tostring(dhcprequest[5])),
            Relay = tostring(dhcprequest[6]), 
            DhcpSessionId = tostring(dhcprequest[8]),
            SrcUserIdType = tostring(dhcprequest[10]),
            State = tostring(dhcprequest[11])        
        | project-away SyslogMessage, dhcprequest,RawData_subString;