id: 664e7bbd-fba2-41a6-be63-85376f516f4c
Function:
  Title: Parser for Infoblox_dhcpinform
  Version: '1.1.0'
  LastUpdated: '2024-07-30'
Category: Microsoft Sentinel Parser
FunctionName: Infoblox_dhcpinform
FunctionAlias: Infoblox_dhcpinform
FunctionQuery: |
    let datasource = (_GetWatchlist('Sources_by_SourceType')| where SearchKey == 'InfobloxNIOS' | project Source);
    let RawData = Syslog
        | where CollectorHostName in (datasource) or Computer in (datasource)
        | where SyslogMessage has "DHCPINFORM"
        | extend Parser = extract_all(@"^(\d{2}\-[a-zA-Z]{3}\-\d{4}\s[0-9\.\:]+)?\s?([a-zA-Z-_]+)(\s|\:)?(.*)", dynamic([1,2,3,4]), SyslogMessage)[0]
        | extend responseTime = todatetime(Parser[0]),
                Log_Type = tostring(Parser[1]),
                RawData_subString = tostring(Parser[3])
        | project-away Parser;
    RawData
        | extend dhcpinform = extract_all(@"\w+\s(\S+)\svia\s(\S+)\sTransID\s([a-z0-9]+)", dynamic([1,2,3]), RawData_subString)[0]
        | extend SrcIpAddr = tostring(dhcpinform[0]), 
            Relay = tostring(dhcpinform[1]), 
            DhcpSessionId = tostring(dhcpinform[2])        
        | project-away SyslogMessage, dhcpinform,RawData_subString;