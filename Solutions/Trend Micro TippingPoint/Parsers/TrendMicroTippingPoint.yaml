id: 8859cdc3-75ee-4491-875d-5ef1181f4e8c
Function:
  Title: Parser for TrendMicroTippingPoint
  Version: '1.0.0'
  LastUpdated: '2023-08-23'
Category: Microsoft Sentinel Parser
FunctionName: TrendMicroTippingPoint
FunctionAlias: TrendMicroTippingPoint
FunctionQuery: |
    // REFERENCES: 
    // Using functions in Azure monitor log queries: https://docs.microsoft.com/azure/azure-monitor/log-query/functions
    // TippingPoint SMS User Guide: https://docs.trendmicro.com/all/tip/sms/v5.4.0/en-us/sms_5.4.0_ug.pdf
    // 
    CommonSecurityLog
    | where DeviceProduct == "UnityOne"
    | extend DeviceCustomNumber1 = coalesce(column_ifexists("FieldDeviceCustomNumber1", long(null)),DeviceCustomNumber1),
             DeviceCustomNumber2 = coalesce(column_ifexists("FieldDeviceCustomNumber2", long(null)),DeviceCustomNumber2),
             DeviceCustomNumber3 = coalesce(column_ifexists("FieldDeviceCustomNumber3", long(null)),DeviceCustomNumber3)
    | extend DeviceVendor = iff(DeviceVendor == "TippingPoint", "Trend Micro", DeviceVendor)
    | extend DeviceProduct = iff(DeviceProduct == "UnityOne", "TippingPoint", DeviceProduct)
    | parse AdditionalExtensions with "cat=" cat
    | extend cat = coalesce(column_ifexists("DeviceEventCategory",""),cat)
    | project-rename TippingPointVLAN = DeviceCustomNumber1, TippingPointTaxonomy = DeviceCustomNumber2, TippingPointPacketTrace = DeviceCustomNumber3, TippingPointProfileName = DeviceCustomString1, TippingPointPolicyUUID = DeviceCustomString2, TippingPointSignatureUUID = DeviceCustomString3, TippingPointZoneNames = DeviceCustomString4, TippingPointSMSName = DeviceCustomString5, TippingPointFilterMessageParms = DeviceCustomString6
    | project-away DeviceCustomIPv6Address1Label, DeviceCustomIPv6Address2Label, DeviceCustomIPv6Address3Label, DeviceCustomNumber1Label, DeviceCustomNumber2Label, DeviceCustomNumber3Label, DeviceCustomString1Label, DeviceCustomString2Label, DeviceCustomString3Label, DeviceCustomString4Label, DeviceCustomString5Label, DeviceCustomString6Label