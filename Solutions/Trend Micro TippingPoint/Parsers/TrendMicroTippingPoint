// Title:           Trend Micro TippingPoint Data Parser
// Author:          Trend Micro
// Version:         1.0
// Last Updated:    11/05/2020
// Comment:         Initial Release
//  
// DESCRIPTION:
// This parser takes the TippingPoint SMS ArcSight CEF Format v4.2 logs from a Syslog data stream and parses the data into a normalized schema
//

// REFERENCES: 
// Using functions in Azure monitor log queries: https://docs.microsoft.com/azure/azure-monitor/log-query/functions
// TippingPoint SMS User Guide: https://docs.trendmicro.com/all/tip/sms/v5.4.0/en-us/sms_5.4.0_ug.pdf
// 
CommonSecurityLog
| where DeviceProduct == "UnityOne"
| extend DeviceCustomNumber1 = coalesce(column_ifexists("FieldDeviceCustomNumber1", long(null)),DeviceCustomNumber1),
         DeviceCustomNumber2 = coalesce(column_ifexists("FieldDeviceCustomNumber2", long(null)),DeviceCustomNumber2),
         DeviceCustomNumber3 = coalesce(column_ifexists("FieldDeviceCustomNumber3", long(null)),DeviceCustomNumber3)
| extend DeviceVendor = iff(DeviceVendor == "TippingPoint", "Trend Micro", DeviceVendor)
| extend DeviceProduct = iff(DeviceProduct == "UnityOne", "TippingPoint", DeviceProduct)
| parse AdditionalExtensions with "cat=" cat
| extend cat = coalesce(column_ifexists("DeviceEventCategory",""),cat)
| project-rename TippingPointVLAN = DeviceCustomNumber1, TippingPointTaxonomy = DeviceCustomNumber2, TippingPointPacketTrace = DeviceCustomNumber3, TippingPointProfileName = DeviceCustomString1, TippingPointPolicyUUID = DeviceCustomString2, TippingPointSignatureUUID = DeviceCustomString3, TippingPointZoneNames = DeviceCustomString4, TippingPointSMSName = DeviceCustomString5, TippingPointFilterMessageParms = DeviceCustomString6
| project-away DeviceCustomIPv6Address1Label, DeviceCustomIPv6Address2Label, DeviceCustomIPv6Address3Label, DeviceCustomNumber1Label, DeviceCustomNumber2Label, DeviceCustomNumber3Label, DeviceCustomString1Label, DeviceCustomString2Label, DeviceCustomString3Label, DeviceCustomString4Label, DeviceCustomString5Label, DeviceCustomString6Label