// Title:           Infoblox Cloud Data Connector Parser
// Author:          Infoblox
// Version:         2.0.9
// Last Updated:    4/10/2023
// Comment:         
//  
// DESCRIPTION:
// This parser takes raw Infoblox Cloud Data Connector (CDC) logs from a Syslog (CEF) stream and parses the logs into a normalized schema.
//
// USAGE:
// 1. Open Log Analytics/Microsoft Sentinel Logs blade. Copy the query below and paste into the Logs query window. 
// 2. Click the Save button above the query. A pane will appear on the right, select "as Function" from the drop down. Enter the Function Name as InfobloxCDC.
// 3. Kusto Functions can typically take up to 15 minutes to activate. You can then use Function Alias for other queries.
//
// REFERENCES: 
// Using functions in Azure monitor log queries: https://docs.microsoft.com/azure/azure-monitor/log-query/functions
// 
CommonSecurityLog
| where DeviceVendor == "Infoblox" and DeviceProduct == "Data Connector"
| extend AEcopy = AdditionalExtensions
//Remove DHCP Option codes at end of DHCP logs to prevent invalid chars in fieldnames, causing errors. If you require these advanced fields, remove the following line.
| extend AEcopy = trim_end("InfobloxDHCPOptions=;(.*?)",AEcopy)
| extend AEcopy = extract_all(@"(?P<key>[^=;]+)=(?P<value>[^=;]+)", dynamic(["key","value"]), AEcopy)
| mv-apply AEcopy on (
    summarize AdditionalExtensionsParsedNested = make_bag(pack(tostring(AEcopy[0]), AEcopy[1]))
)
| extend AdditionalExtensionsParsed = AdditionalExtensionsParsedNested
| evaluate bag_unpack(AdditionalExtensionsParsed)
| extend ThreatLevel_Score = toint(column_ifexists("InfobloxThreatLevel", ""))
| extend ThreatLevel = case(ThreatLevel_Score>=80, "High",
                       ThreatLevel_Score>=30 and ThreatLevel_Score<80, "Medium",
                       ThreatLevel_Score<30 and ThreatLevel_Score>=1, "Low",
                       ThreatLevel_Score == 0,"Info",
                       "N/A" )
| extend ThreatClass = extract("(.*?)_", 1, tostring(column_ifexists("InfobloxThreatProperty", "")))
| extend ThreatProperty = extract("([^_]*$)", 1, tostring(column_ifexists("InfobloxThreatProperty", "")))
| extend InfobloxB1FeedName = column_ifexists("InfobloxB1FeedName", "")
| extend InfobloxRPZ = column_ifexists("InfobloxRPZ", "")
| extend InfobloxB1PolicyAction = column_ifexists("InfobloxB1PolicyAction", "")
| extend InfobloxB1PolicyName = column_ifexists("InfobloxB1PolicyName", "")
| extend InfobloxDomainCat = column_ifexists("InfobloxDomainCat", "")
| extend InfobloxB1ConnectionType = column_ifexists("InfobloxB1ConnectionType", "")
| extend InfobloxB1SrcOSVersion = column_ifexists("InfobloxB1SrcOSVersion", "")
| extend InfobloxB1Network = column_ifexists("InfobloxB1Network", "")
| extend DeviceName = column_ifexists("DeviceName", "")
| extend SourceMACAddress = column_ifexists("SourceMACAddress", "")
| extend InfobloxLeaseOp = column_ifexists("InfobloxLeaseOp", "")
| extend InfobloxLifetime = column_ifexists("InfobloxLifetime", "")
| extend InfobloxLeaseUUID = column_ifexists("InfobloxLeaseUUID", "")
| extend InfobloxDNSRCode = column_ifexists("InfobloxDNSRCode", "")
| extend InfobloxDNSQClass = column_ifexists("InfobloxDNSQClass", "")
| extend InfobloxDNSQType = column_ifexists("InfobloxDNSQType", "")
