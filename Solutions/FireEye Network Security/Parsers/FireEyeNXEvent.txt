// Usage Instructions: 
// Paste below query in log analytics, click on Save button and select as Function from drop down by specifying function name as FireEyeNXEvent and specifying Legacy Category.
// This function maps FireEye NX events to normalization schema (https://docs.microsoft.com/azure/sentinel/normalization-schema).
// Function usually takes 10-15 minutes to activate. You can then use function alias from any other queries (e.g. FireEyeNXEvent | take 10).
// Reference : Using functions in Azure monitor log queries : https://docs.microsoft.com/azure/azure-monitor/log-query/functions
CommonSecurityLog
| where DeviceVendor =~ 'FireEye'
|extend DeviceCustomNumber1 = coalesce(column_ifexists("FieldDeviceCustomNumber1", long(null)),DeviceCustomNumber1),
		DeviceCustomNumber2 = coalesce(column_ifexists("FieldDeviceCustomNumber2", long(null)),DeviceCustomNumber2),
		DeviceCustomNumber3 = coalesce(column_ifexists("FieldDeviceCustomNumber3", long(null)),DeviceCustomNumber3),
        ExternalID = coalesce(column_ifexists("ExtID", ""),tostring(ExternalID))
| extend packed = pack(DeviceCustomNumber1Label, DeviceCustomNumber1
                     , DeviceCustomNumber2Label, DeviceCustomNumber2
                     , DeviceCustomNumber3Label, DeviceCustomNumber3
                     , DeviceCustomString1Label, DeviceCustomString1
                     , DeviceCustomString2Label, DeviceCustomString2
                     , DeviceCustomString3Label, DeviceCustomString3
                     , DeviceCustomString4Label, DeviceCustomString4
                     , DeviceCustomString5Label, DeviceCustomString5
                     , DeviceCustomString6Label, DeviceCustomString6
                     , DeviceCustomDate1Label, DeviceCustomDate1
                     , DeviceCustomDate2Label, DeviceCustomDate2)
| evaluate bag_unpack(packed)
| project-rename EventVendor=DeviceVendor
                , EventProduct=DeviceProduct
                , EventProductVersion=DeviceVersion
                , EventId=DeviceEventClassID
                , EventSeverity=LogSeverity
                , DstPortNumber=DestinationPort
                , DstIpAddr=DestinationIP
                , DvcIpAddr=DeviceAddress
                , DvcHostname=DeviceName
                , NetworkApplicationProtocol=Protocol
                , SrcPortNumber=SourcePort
                , SrcIpAddr=SourceIP
                , DstMacAddr=DestinationMACAddress
                , Id=ExternalID
                , SrcDvcHostname=SourceHostName
                , SrcMacAddr=SourceMACAddress
| extend EventType=Activity
| extend ThreatName = column_ifexists("anomaly", "")
         , OsInfo = column_ifexists("osinfo", "")
         , Malware = column_ifexists("osinfo", "")
         , Vlan = column_ifexists("osinfo", "")
         , IncidentUrl = column_ifexists("osinfo", "")
| extend EventTimeIngested=todatetime(ReceiptTime)
| project-away Activity
            , ReceiptTime
            , DeviceCustomNumber1
            , DeviceCustomNumber1Label
            , DeviceCustomNumber2
            , DeviceCustomNumber2Label
            , DeviceCustomNumber3
            , DeviceCustomNumber3Label
            , DeviceCustomString1
            , DeviceCustomString1Label
            , DeviceCustomString2
            , DeviceCustomString2Label
            , DeviceCustomString3
            , DeviceCustomString3Label
            , DeviceCustomString4
            , DeviceCustomString4Label
            , DeviceCustomString5
            , DeviceCustomString5Label
            , DeviceCustomString6
            , DeviceCustomString6Label