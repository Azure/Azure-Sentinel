// Usage Instructions: 
// Paste below query in log analytics, click on Save button and select as Function from drop down by specifying function name as IvantiUEMEvent and specifying Legacy Category.
// This function maps Ivanti events to Microsoft Sentinel Information Model (ASIM) (https://docs.microsoft.com/azure/sentinel/normalization).
// Function usually takes 10-15 minutes to activate. You can then use function alias from any other queries (e.g. IvantiUEMEvent | take 10).
// Reference : Using functions in Azure monitor log queries : https://docs.microsoft.com/azure/azure-monitor/log-query/functions
Syslog
| where SyslogMessage has_all ('Alert', 'Message')
| extend EventVendor = 'Ivanti'
| extend EventProduct = 'Ivanti Unified Endpoint Management'
| extend EventCount = 1
| extend EventSchemaVersion = 0.2
| extend ed = split(SyslogMessage, ' ')
| extend SrcHostname = extract(@'\A([A-Za-z0-9_-]+)\.\w+', 1, tostring(ed[0]))
| extend SrcDomain = extract(@'\A[A-Za-z0-9_-]+\.(.*)', 1, tostring(ed[0]))
| extend EventType = trim(' ', extract(@'Alert:\s(.*?)Message:', 1, SyslogMessage))
| extend EventMessage = extract(@'Message:\s(.*)', 1, SyslogMessage)
| project-away SyslogMessage
             , ed
