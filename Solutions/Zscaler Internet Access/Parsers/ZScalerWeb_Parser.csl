// Zscaler Web Logs (NSS)
// Last Updated Date: October 28, 2020
//
// This parser parses Syslog CEF events for Zscaler Web logs.
// https://help.zscaler.com/zia/zscaler-microsoft-azure-sentinel-deployment-guide
//
// Parser Notes:
// 1. This parser assumes logs are collected into the CommonSecurityLog table.
//
// Usage Instruction : 
// Paste below query in log analytics, click on Save button and select as Function from drop down by specifying function name and alias.
// Functions usually takes 10-15 minutes to activate. You can then use function alias from any other queries (e.g. ZscalerWeb | take 10).
//
//

CommonSecurityLog 
| where DeviceVendor == "Zscaler" and DeviceProduct == "NSSWeblog"
| extend reason = coalesce(
                            extract(@"reason=(.*?)(;|$)", 1, AdditionalExtensions),                            
                            column_ifexists("Reason", "")
                        ),
    user = extract(@"suser=(.*?);", 1, AdditionalExtensions),
    sourceIP = extract(@"sourceTranslatedAddress=(.*?)(;|$)", 1, AdditionalExtensions),
    outcome = coalesce(
                        extract(@"outcome=(.*?)(;|$)", 1, AdditionalExtensions),                            
                        column_ifexists("EventOutcome", "")
                    ),
    cat = coalesce(
                        extract(@"cat=(.*?)(;|$)", 1, AdditionalExtensions),                            
                        column_ifexists("DeviceEventCategory", "")
                    ),
    rulelabel = extract(@"rulelabel=(.*?)(;|$)", 1, AdditionalExtensions),
    ruletype = extract(@"ruletype=(.*?)(;|$)", 1, AdditionalExtensions),
    urlclass = extract(@"urlclass=(.*?)(;|$)", 1, AdditionalExtensions),
    devicemodel = extract(@"devicemodel=(.*?)(;|$)", 1, AdditionalExtensions)
