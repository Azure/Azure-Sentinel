id: 679b2b50-2611-4358-9686-5948587cbb47
Function:
  Title: Parser for MimecastAudit
  Version: '1.0.1'
  LastUpdated: '2025-10-23'
Category: Microsoft Sentinel Parser
FunctionName: MimecastAudit
FunctionAlias: MimecastAudit
FunctionQuery: |
    let dummy_table = datatable(TimeGenerated: datetime) [];
    let MimecastAudit_view = view() {
    union isfuzzy=true dummy_table,
    Audit_CL
    | extend ["Id"] = column_ifexists('id', ''),
        ["Audit Type"] = column_ifexists('auditType', ''),
        ["User"] = column_ifexists('user', ''),
        ["Event Time"] = column_ifexists('eventTime', ''),
        ["Event Info"] = column_ifexists('eventInfo', ''),
        ["Category"] = column_ifexists('category',''),
        ["Time Generated"] = column_ifexists('TimeGenerated','')
    | extend ['Event Time'] = iff( isempty( ['Event Time']) ,now() , todatetime( ["Event Time"] ))   
    | parse-kv ["Event Info"] as (IP: string, Application:string )  with (pair_delimiter=',', kv_delimiter=':') 
    | summarize arg_max(TimeGenerated, *) by  ["Category"] , ["Audit Type"] , ["User"] , ["Event Info"] , ["Event Time"] , ["Id"]
    | project ["Time Generated"]  ,["Source IP"] = IP ,Application , ["Category"] , ["Audit Type"] , ["User"] , ["Event Info"] , ["Event Time"] , ["Id"]
    };
    MimecastAudit_view