id: 2a51081a-e31d-4a29-9fd5-cf399b0d3cc1
Contentkind: Function
Description: DataverseSharePointSites
Function:
  Title: DataverseSharePointSites
  Version: 3.2.0
  LastUpdated: '2024-11-18'
Category: MSBizAppsFunctions
FunctionName: DataverseSharePointSites
FunctionAlias: DataverseSharePointSites
FunctionParams:
  - Name: MSBizAppsConfigurationWatchlistAlias
    Type: string
    Default: 'MSBizApps-Configuration'
FunctionQuery: |
  let DataverseSharepointSites_definition = datatable(InstanceUrl: string, SharePointUrl: string)['_', '_'];
  let DataverseSharepointSites_data = (
      _GetWatchlist(MSBizAppsConfigurationWatchlistAlias)
      | where SearchKey == "SharePoint"
      | extend Data = todynamic(column_ifexists('Data', dynamic({"InstanceUrl": "_", "SharePointUrl": "_"})))
      | project
          InstanceUrl = tostring(Data.InstanceUrl),
          SharePointUrl = tostring(Data.SharePointUrl)
      );
  DataverseSharepointSites_data
  | union isfuzzy = true (DataverseSharepointSites_definition)
  | where InstanceUrl != '_'
  | extend InstanceUrl = tolower(iff(InstanceUrl endswith '/', InstanceUrl, strcat(InstanceUrl, '/')))
  | extend SharePointUrl = tolower(iff(SharePointUrl endswith '/', SharePointUrl, strcat(SharePointUrl, '/')))
  | project InstanceUrl, SharePointUrl
