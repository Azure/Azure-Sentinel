// Usage Instruction : 
// Paste below query in log analytics, click on Save button and select as Function from drop down by specifying function name and alias as GCPCloudDNS.
// Function usually takes 10-15 minutes to activate. You can then use function alias from any other queries (e.g. GCPCloudDNS | take 10).
// Reference : Using functions in Azure monitor log queries : https://docs.microsoft.com/azure/azure-monitor/log-query/functions
let DNSQuery_GcpDns=()
{
    GCP_DNS_CL
    | project-rename
        Query=payload_queryName_s, 
        QueryTypeName=payload_queryType_s,
        ResponseName=payload_rdata_s, 
        EventResultDetails=payload_responseCode_s,
        NetworkProtocol=payload_protocol_s, 
        SrcIpAddr=payload_sourceIP_s,
        EventOriginalUid=insert_id_s,
        EventSeverity=severity_s   
    | extend
        EventCount=int(1),
        EventProduct='Cloud DNS',
        EventVendor='GCP',
        EventSchemaVersion="0.1.0",
        Dvc="GCPDNS",
        EventType = iif (resource_type_s == "dns_query", "lookup", resource_type_s),
        EventResult=iff(EventResultDetails =~ 'NOERROR', 'Success', 'Failure'),
        EventSubType='response',
        EventEndTime=todatetime(timestamp_t)       
    // -- Aliases
    | extend 
        ResponseCodeName=EventResultDetails, 
        Domain=Query,
        IpAddr=SrcIpAddr,
        EventStartTime = EventEndTime
};
DNSQuery_GcpDns