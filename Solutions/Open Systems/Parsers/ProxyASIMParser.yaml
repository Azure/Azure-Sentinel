id: 39973f4e-bf6a-43e9-ae0e-069f15b74849
Function:
  Title: ASIM Parser for OpenSystems Secure Web Gateway logs
  Version: '1.0.0'
  LastUpdated: May 12th 2025
Category: OpenSystemsParser
FunctionName: ASimWebSessionOpenSystemsSecureWebGateway
FunctionAlias: ASimWebSessionOpenSystemsSecureWebGateway
FunctionQuery: |
  let parser = () {
    OpenSystemsProxyLogs_CL
        | extend tmp = iff(url matches regex @"^https?\:\/\/|ftp\:\/\/", url, strcat("http://", url))
        | extend tmp2 = parse_url(tmp)
        | extend host = tostring(tmp2.Host)
        | extend port = tostring(tmp2.Port)
        | project-away tmp, tmp2
        | extend EventCount = toint(1)
        | extend EventStartTime = timestamp
        | extend EventEndTime = TimeGenerated
        | extend EventType = "HTTPsession"
        | extend EventResult = iff(toint(client_response_status) < 400, "Success", "Failure") 
        | extend EventResultDetails = tostring(client_response_status)
        | extend EventProduct = "Secure Web Gateway"
        | extend EventVendor = "Open Systems"
        | extend EventSchema = "WebSession"
        | extend EventSchemaVersion = "0.2.6"
        | extend DvcHostname = tostring(host_id)
        | extend Dvc = DvcHostname
        | extend Url = tostring(url)
        | extend HttpRequestMethod = tostring(http_method)
        | extend HttpStatusCode = EventResultDetails
        | extend HttpContentType = tostring(content_type)
        | extend HttpReferrer = tostring(referer)
        | extend HttpUserAgent = tostring(user_agent)
        | extend UserAgent = HttpUserAgent
        | extend HttpHost = tostring(host)
        | extend NetworkDuration = toint(duration)
        | extend Duration = NetworkDuration
        | extend DstBytes = tolong(server_response_size)
        | extend SrcBytes = tolong(client_request_size)
        | extend DstIpAddr = tostring(server_ip)
        | extend Dst = DstIpAddr
        | extend DstPortNumber = toint(port)
        | extend SrcIpAddr = tostring(client_ip)
        | extend Src = SrcIpAddr
        | extend DvcOriginalAction = tostring(url_filter_action)
        | extend DvcAction = case(
                                url_filter_action == "allow", "Allow",
                                url_filter_action == "whitelist", "Allow",
                                url_filter_action == "deny", "Drop",
                                "Allow"
                            )
        | project
            EventCount,
            EventStartTime,
            EventEndTime,
            EventType,
            EventResult,
            EventResultDetails,
            EventProduct,
            EventVendor,
            EventSchema,
            EventSchemaVersion,
            DvcHostname,
            Dvc,
            Url,
            HttpRequestMethod,
            HttpStatusCode,
            HttpContentType,
            HttpReferrer,
            HttpUserAgent,
            UserAgent,
            HttpHost,
            NetworkDuration,
            Duration,
            DstBytes,
            SrcBytes,
            DstIpAddr,
            Dst,
            DstPortNumber,
            SrcIpAddr,
            Src,
            DvcOriginalAction,
            DvcAction
  };
  parser
