id: 49E9A088-B96E-4743-8106-676FDB48DE4A
Function:
    Title: Parser to extract DarkWeb Data Breaches alerts from Cyble Vision Alerts
    Version: '1.0.0'
    LastUpdated: '2025-12-09'
Category: Microsoft Sentinel Parser
FunctionName: Alerts_darkweb_data_breaches
FunctionAlias: Alerts_darkweb_data_breaches
FunctionQuery: |
    CybleVisionAlerts_CL
    | extend Risk_Score = pack("s", RiskScore_s, "e", RiskScore_e, "c", RiskScore_c)
    | extend AlertID = ID
    | extend Data = parse_json(Data)
    | where Service == "darkweb_data_breaches"
    // inner string JSON inside data.data
    | extend BreachRaw = tostring(Data.data)
    | extend BreachObj = parse_json(BreachRaw)
    // Extract fields from BreachObj
    | extend
        DB_Username          = tostring(BreachObj.username),
        DB_Email             = tostring(BreachObj.email),
        DB_PasswordHash      = tostring(BreachObj.password_hash),
        DB_RegistrationDate  = tostring(BreachObj.registration_date),
        DB_DisplayName       = tostring(BreachObj.display_name)
    // Extract other metadata in Data{}
    | extend
        DB_Date              = tostring(Data.['date']),
        DB_S3Key             = tostring(Data.s3_key),
        DB_Structured        = tostring(Data.structured)
    // Tags[]
    | extend
        DB_Tags = tostring(Data.tags)
    | project
        TimeGenerated,
        AlertID,
        EntityID,
        KeywordName,
        Breach_Source,
        CompanyID,
        Status,
        Severity,
        UserSeverity,
        BucketID,
        Domain,
        BucketName,
        TruePositive,
        LLMProcessed,
        LLMExplanation,
        IP,
        Service,
        Risk_Score,
        DB_Username,
        DB_Email,
        DB_PasswordHash,
        DB_RegistrationDate,
        DB_DisplayName,
        DB_Date,
        DB_S3Key,
        DB_Structured,
        DB_Tags
