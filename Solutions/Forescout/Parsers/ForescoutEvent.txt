// This parser has been developed using Forescout Syslog Plugin version: v3.6
// Usage instructions: 
// Paste below query in log analytics, click on Save button and select as Function from drop down by specifying function name as ForescoutEvent and specify Legacy Category.
// Function usually takes 10-15 minutes to activate. You can then use function alias from any other queries (e.g. ForescoutEvent | take 10).
// Reference : Using functions in Azure monitor log queries : https://docs.microsoft.com/azure/azure-monitor/log-query/functions
Syslog 
| where ProcessName == ```ACTIONidentity```
| extend EventVendor = 'Forescout'
| extend EventProduct = 'Forescout'
| extend EventName = extract(@'([^\.:]+).*', 1, SyslogMessage)
| extend SrcIpAddr = extract(@'.*?(Source|Host):\s+?(\d+.\d+.\d+.\d+).*', 2, SyslogMessage)
| extend DstIpAddr = extract(@'.*?(Destination|Target):\s+?(\d+.\d+.\d+.\d+).*', 2, SyslogMessage)
| extend SrcUserUpn = extract(@'.*?mail_from=([^,]+).*', 1, SyslogMessage)
| extend DstUserUpn = extract(@'.*?mail_to=([^,]+).*', 1, SyslogMessage)
| extend EmailSubject = extract(@'.*?mail_subject=([^,]+).*', 1, SyslogMessage)
| extend EventResultDetails = extract(@'.*?Reason:\s+?([^,]+).*', 1, SyslogMessage)
| extend EventSchemaVersion = '0.1'
| extend EventCount = 1
| project-away SyslogMessage