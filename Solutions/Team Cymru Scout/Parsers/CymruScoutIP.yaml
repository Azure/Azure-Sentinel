id: ba3b1d75-544c-43ff-9969-9ff753e3e3dc
Function:
  Title: Parser for CymruScoutIP
  Version: "1.1.0"
  LastUpdated: "2025-05-15"
Category: Microsoft Sentinel Parser
FunctionName: CymruScoutIP
FunctionAlias: CymruScoutIP
FunctionQuery: |
        let dummy_table = datatable(TimeGenerated: datetime, IndicatorType: string, Indicator: string, IP: string, Insights: string, InsightsOverallRating: string, SummaryWhoIsASName: string, SummaryWhoIsASN: string, SummaryWhoIsNetName: string, SummaryWhoIsOrgName: string, SummaryReverseHostNames: string, IdentityASN: string, IdentityASName: string, IdentityNetName: string, IdentityOrgName: string, ASInfo: string, IPCountryCode: string, IdentityReverseHostNames: string, TagsIds: string, TagsNames: string, IPPdnsDomain: string, PDNSDetails: dynamic, OpenPorts:string, OpenPortsDetails: dynamic, ProtocolID: string, ProtocolType: string, OpenPortServices: string , CertificateDetails: string , FingerprintsDetails: string, ip:string, domain:string, port:real, md5:string, signature:string) [];
        let CymruScoutIP = view(){
        let pdnsfields = union isfuzzy=true dummy_table, Cymru_Scout_IP_Data_Summary_PDNS_CL
        | summarize arg_max(TimeGenerated, *) by ip, domain
        | extend domain = tostring(domain),
                event_count = event_count
        | extend PDNSDetailRecord = bag_pack("domain", domain,"event_count", toint(event_count))
        | summarize IPPdnsDomain = strcat_array(make_list(domain), ","), PDNSDetails = make_list(PDNSDetailRecord) by ip;
        let open_portsfields = union isfuzzy=true dummy_table, Cymru_Scout_IP_Data_Summary_OpenPorts_CL
        | summarize arg_max(TimeGenerated, *) by ip, port
        | extend OpenPortsRecord = bag_pack("port", toint(port), "protocol", toint(protocol), "protocol_text", protocol_text, "event_count", toint(event_count))
        | summarize OpenPorts = strcat_array(make_list(toint(port)), ","), ProtocolID = strcat_array(make_list(toint(protocol)), ","), ProtocolType = strcat_array(make_list(protocol_text), ","), OpenPortServices = strcat_array(make_list(service), ","), OpenPortsDetails = make_list(OpenPortsRecord) by ip;
        let certificates = union isfuzzy=true dummy_table, Cymru_Scout_IP_Data_Summary_Certs_CL
        | summarize arg_max(TimeGenerated, *) by ip, port, md5
        | extend Certificates = bag_pack("issuer",issuer, "issuer_common_name",issuer_common_name, "common_name", common_name, "subject",subject, "port",toint(port), "first_seen", first_seen, "last_seen", last_seen, "self_signed",self_signed, "not_before", not_before, "not_after", not_after, "valid_days", toint(valid_days), "md5", md5, "sha1", sha1, "sha256", sha256, "css_color", css_color)
        | summarize CertificateDetails = strcat_array(make_list(Certificates), ",") by ip;
        let fingerprints = union isfuzzy=true dummy_table, Cymru_Scout_IP_Data_Summary_Fingerprints_CL
        | summarize arg_max(TimeGenerated, *) by ip, signature
        | extend fingerprint_type = tostring(summary_fingerprint_type), signature = tostring(signature), first_seen = tostring(first_seen), last_seen = tostring(last_seen), count_d = column_ifexists("count", real(null))
        | extend Fingerprints = bag_pack("type", fingerprint_type, "signature", signature, "port", toint(port), "first_seen", first_seen, "last_seen", last_seen, "count", toint(count_d))
        | summarize FingerprintsDetails = strcat_array(make_list(Fingerprints), ",") by ip;
        union isfuzzy=true dummy_table,
        (Cymru_Scout_IP_Data_Details_CL
        | summarize arg_max(TimeGenerated, *) by ip
        | join kind=leftouter (Cymru_Scout_IP_Data_Summary_Details_CL| summarize arg_max(TimeGenerated, *) by ip) on ip
        | join kind=leftouter pdnsfields on ip
        | join kind=leftouter open_portsfields on ip
        | join kind=leftouter certificates on ip
        | join kind=leftouter fingerprints on ip),
        ( Cymru_Scout_IP_Data_Foundation_CL)
        | summarize arg_max(TimeGenerated, *) by ip
        | extend IndicatorType="IP", 
                Indicator=tostring(ip), 
                IP = tostring(ip), 
                Insights = tostring(insights_insights), 
                InsightsOverallRating = tostring(insights_overall_rating), 
                SummaryWhoIsASName = tostring(whois_as_name1), 
                SummaryWhoIsASN = whois_asn1, 
                SummaryWhoIsNetName = tostring(whois_net_name1), 
                SummaryWhoIsOrgName = tostring(whois_org_name1), 
                SummaryReverseHostNames = strcat_array(todynamic(tostring(reverse_hostnames)), ","), 
                IdentityASN = identity_asn, 
                IdentityASName = tostring(identity_as_name),
                IdentityNetName = tostring(identity_net_name), 
                IdentityOrgName = tostring(identity_org_name), 
                ASInfo = tostring(as_info), 
                IPCountryCode = coalesce(tostring(country_code), tostring(geo_ip_cc)), 
                IdentityReverseHostNames = strcat_array(todynamic(tostring(identity_reverse_hostnames)), ","),
                TagsIds = strcat_array(todynamic(tostring(tags_id)), ","),
                TagsNames = strcat_array(todynamic(tostring(tags_name)), ",")
        | project TimeGenerated,
                IndicatorType, 
                Indicator, 
                IP, 
                Insights, 
                InsightsOverallRating, 
                SummaryWhoIsASName, 
                SummaryWhoIsASN, 
                SummaryWhoIsNetName, 
                SummaryWhoIsOrgName, 
                SummaryReverseHostNames, 
                IdentityASN, 
                IdentityASName,
                IdentityNetName, 
                IdentityOrgName, 
                ASInfo, 
                IPCountryCode, 
                IdentityReverseHostNames, 
                IPPdnsDomain, 
                PDNSDetails,
                OpenPorts, 
                OpenPortsDetails, 
                ProtocolID, 
                ProtocolType, 
                OpenPortServices, 
                CertificateDetails, 
                FingerprintsDetails,
                TagsIds,
                TagsNames
        };
        CymruScoutIP
