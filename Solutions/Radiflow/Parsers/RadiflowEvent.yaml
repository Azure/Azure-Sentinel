id: 7594ae30-cfd5-42c5-894d-2c6196857a4c
Function:
  Title: Parser for RadiflowEvent
  Version: 1.0.0
  LastUpdated: '2024-05-17'
Category: Microsoft Sentinel Parser
FunctionName: RadiflowEvent
FunctionAlias: RadiflowEvent
FunctionQuery: |
  CommonSecurityLog
  | where DeviceVendor =~ 'radiflow'
  | extend EventType = 0
  | extend EventVendor = 'Radiflow'
  | extend SchemaVersion = 1.0
  | parse-kv AdditionalExtensions as
    (
      id: long
      , pt: string
      , PtName: string
      , SrcName: string
      , DstName: string
      , SrcMac: string
      , DstMac: string
      , SrcVendor: string
      , DstVendor: string
      , SrcType: string
      , DstType: string
      , SrcVlan: string
    ) with (pair_delimiter=';', kv_delimiter='=')
  | extend DestinationHostName = iif(DstName matches regex @"^[A-Za-z0-9 \!\@\#\$\%\^\&\*\(\)\{\}\[\]\:\;\<\>\,\?\_\+\-\=\~\`\'\|\/]{1,15}$", DstName, "")
  | extend DestinationMACAddress = DstMac
  | extend EventTime = TimeGenerated
  | extend Protocol = PtName
  | extend SourceHostName = iif(SrcName matches regex @"^[A-Za-z0-9 \!\@\#\$\%\^\&\*\(\)\{\}\[\]\:\;\<\>\,\?\_\+\-\=\~\`\'\|\/]{1,15}$", SrcName, "")
  | extend SourceIP = coalesce(DeviceAddress, SourceIP)
  | extend SourceMACAddress = trim(@"#012[\s]+", SrcMac)
  | project-rename
      DestinationType = DstType
      , DestinationVendor = DstVendor
      , EventID = id
      , EventCategory = DeviceEventCategory
      , EventClassID = DeviceEventClassID
      , EventCollectorHostName = CollectorHostName
      , EventMessage = Activity
      , EventServer = Computer
      , EventSeverity = LogSeverity
      , Port = pt
      , SourceType = SrcType
      , SourceVendor = SrcVendor
      , SourceVLAN = SrcVlan
  | project-keep
      TimeGenerated
      , EventCategory
      , EventClassID
      , EventCollectorHostName
      , EventID
      , EventMessage
      , EventServer
      , EventSeverity
      , EventTime
      , EventType
      , SchemaVersion
      , DeviceProduct
      , DeviceVendor
      , DeviceVersion
      , Port
      , Protocol
      , SourceHostName
      , SourceIP
      , SourceMACAddress
      , SourceType
      , SourceVendor
      , SourceVLAN
      , DestinationHostName
      , DestinationIP
      , DestinationMACAddress
      , DestinationType
      , DestinationVendor