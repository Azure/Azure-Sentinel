// This function parses Tomcat access.log and catalina.log
// Expected access.log format: %h %l %u %t "%r" %s %b "%{Referer}" "%{User-agent}"
// Reference : Using functions in Azure monitor log queries : https://docs.microsoft.com/azure/azure-monitor/log-query/functions
let tomcat_accesslog_events =() {
Tomcat_CL
| where RawData matches regex @'(\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3}).*\[.*\]\s\"(GET|POST).*?\"\s([1-5][0-9]{2})\s(\d+|-)(?:\s\"(.*?)\")?(?:\s\"(.*?)\")?.*'
| extend EventProduct = 'Tomcat'
| extend EventType = 'AccessLog'
| extend EventData = split(RawData, '"')
| extend SubEventData0 = split(trim_start(@' ', (trim_end(@' ', tostring(EventData[0])))), ' ')
| extend SubEventData1 = split(EventData[1], ' ')
| extend SubEventData2 = split(trim_start(@' ', (trim_end(@' ', tostring(EventData[2])))), ' ')
| extend SrcIpAddr = tostring(SubEventData0[0])
| extend ClientIdentity = SubEventData0[1]
| extend SrcUserName = SubEventData0[2]
| extend EventStartTime = todatetime(replace(@'\/', @'-', replace(@'(\d{2}\/\w{3}\/\d{4}):(\d{2}\:\d{2}\:\d{2})', @'\1 \2', extract(@'\[(.*?)[-+]\d+\]', 1, RawData))))
| extend HttpRequestMethod = SubEventData1[0]
| extend UrlOriginal = SubEventData1[1]
| extend HttpVersion = SubEventData1[2]
| extend HttpStatusCode = SubEventData2[0]
| extend HttpResponseBodyBytes = SubEventData2[1]
| extend HttpReferrerOriginal = EventData[3]
| extend HttpUserAgentOriginal = EventData[5]
};
let tomcat_catalina_event1=() {
Tomcat_CL
//Mar 16, 2021 1:42:06 PM org
| where RawData matches regex @'\A\w+\s\d+,\s\d{4}\s\d+:\d+:\d+\s(PM|AM)\sorg.*'
| extend EventProduct = 'Tomcat'
| extend EventType = 'CatalinaLog'
| extend EventStartTime = todatetime(replace(@',', '', extract(@'\A(\w+\s\d+,\s\d{4}\s\d+:\d+:\d+\s(PM|AM))', 0, RawData)))
| extend ClassName = extract(@'(PM|AM)\s(.*?)\s', 2, RawData)
| extend DvcAction = extract(@'(PM|AM)\sorg.*?\s(\S+)', 2, RawData)
};
let tomcat_catalina_event2=() {
Tomcat_CL
| where RawData matches regex @'\A(INFO|WARN|ERROR|DEBUG):\s'
| extend EventProduct = 'Tomcat'
| extend EventType = 'CatalinaLog'
| extend EventSeverity = extract(@'(INFO|ERROR|WARN|DEBUG)', 1, RawData)
| extend EventMessage = extract(@'(INFO|ERROR|WARN|DEBUG):\s(.*)', 2, RawData)
};
union isfuzzy=true tomcat_accesslog_events, tomcat_catalina_event1, tomcat_catalina_event2
| project TimeGenerated
        , EventProduct
        , EventType
        , EventSeverity
        , EventStartTime
        , SrcIpAddr
        , ClientIdentity
        , SrcUserName
        , HttpRequestMethod
        , UrlOriginal
        , HttpVersion
        , HttpStatusCode
        , HttpResponseBodyBytes
        , HttpReferrerOriginal
        , HttpUserAgentOriginal
        , ClassName
        , DvcAction
        , EventMessage
