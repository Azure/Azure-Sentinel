id: 86f7ddaf-abab-4e40-9bef-c443a562ea07
Function:
  Title: Corelight SSL Aggregated Events
  Version: '1.1.0'
  LastUpdated: '2025-11-26'
Category: Microsoft Sentinel Parser
FunctionName: corelight_ssl_agg
FunctionAlias: corelight_ssl_agg
FunctionQuery: |
    let dummy_table = datatable(TimeGenerated: datetime, uid_s: string) [];
    let corelight_ssl_agg = view () {
        union isfuzzy=true
            Corelight_v2_ssl_agg_CL,
            dummy_table
        | summarize arg_max(TimeGenerated, *) by uid_s
        | join kind=leftouter 
            ( corelight_conn
            | project uid, local_orig, local_resp    
            ) on $left.uid_s == $right.uid
        | project-away uid
        | extend
            path=column_ifexists("_path_s", ""),
            system_name=column_ifexists("_system_name_s", ""),
            uid = column_ifexists("uid_s", ""),
            id_orig_h=column_ifexists("id_orig_h_s", ""),
            id_orig_p=column_ifexists("id_orig_p_d", real(null)),
            id_resp_h=column_ifexists("id_resp_h_s", ""),
            id_resp_p=column_ifexists("id_resp_p_d", real(null)),
            version=column_ifexists("version_s", ""),
            cipher=column_ifexists("cipher_s", ""),
            curve=column_ifexists("curve_s", ""),
            established=column_ifexists("established_b", ""),
            server_name=column_ifexists("server_name_s", ""),
            next_protocol=column_ifexists("next_protocol_s", ""),
            ssl_history=column_ifexists("ssl_history_s", ""),
            cert_chain_fps=column_ifexists("cert_chain_fps_s", ""),
            client_cert_chain_fps=column_ifexists("client_cert_chain_fps_s", ""),
            validation_status=column_ifexists("validation_status_s", ""),
            ja3=column_ifexists("ja3_s", ""),
            ja3s=column_ifexists("ja3s_s", ""),
            resumed=column_ifexists("resumed_b", ""),
            sni_matches_cert=column_ifexists("sni_matches_cert_b", "")
        | extend
            EventVendor="Corelight",
            EventProduct="CorelightSensor",
            EventType="ssl_agg",
            ts = TimeGenerated,
            src=id_orig_h,
            src_ip=id_orig_h,
            src_port=id_orig_p,
            dest=id_resp_h,
            dest_ip=id_resp_h,
            dest_port=id_resp_p,
            ssl_cipher=cipher,
            ssl_curve=curve,
            ssl_subject_common_name=server_name,
            fingerprint=cert_chain_fps,
            is_self_signed = iff(validation_status=="self signed certificate", "yes", "no"),
            action = iff(established=="true","success","failure"),
            sensor_name = coalesce(system_name, "unknown"),
            signature=validation_status,
            ssl_version = version
        | extend
            is_broadcast = iff(src in("0.0.0.0", "255.255.255.255") or dest in("255.255.255.255", "0.0.0.0"),"true","false"),
            is_src_internal_ip = iff(local_orig == true, "true", "false"),
            is_dest_internal_ip = iff(local_resp == true, "true", "false"),
            direction=case(local_orig=="true" and local_resp=="true", "internal", local_orig=="true" and local_resp=="false", "outbound", local_orig=="false" and local_resp=="false", "external", local_orig=="false" and local_resp=="true", "inbound", "unknown")
        | project
            path,
            system_name,
            uid,
            id_orig_h,
            id_orig_p,
            id_resp_h,
            id_resp_p,
            version,
            cipher,
            curve,
            established,
            server_name,
            next_protocol,
            ssl_history,
            cert_chain_fps,
            client_cert_chain_fps,
            validation_status,
            ja3,
            ja3s,
            resumed,
            sni_matches_cert,
            EventVendor,
            EventProduct,
            EventType,
            ts,
            src,
            src_ip,
            src_port,
            dest,
            dest_ip,
            dest_port,
            ssl_cipher,
            ssl_curve,
            ssl_subject_common_name,
            fingerprint,
            is_self_signed,
            action,
            sensor_name,
            signature,
            ssl_version,
            is_broadcast,
            is_src_internal_ip,
            is_dest_internal_ip,
            direction,
            TimeGenerated
    };
    corelight_ssl_agg