id: 83e70a34-d96f-419d-815b-43d1499e88ed
name: Connection to Unpopular Website Detected (ASIM DNS Solution)
description: |
  'This query lists DNS queries not found in the top 1 million queries in the past 14 days. Please note: To enhance performance, this query uses summarized data if available.'
tags:
  - Schema: ASimDns
    SchemaVersion: 0.1.6
requiredDataConnectors: []
tactics:
  - CommandAndControl
relevantTechniques:
  - T1095
query: |
  let min_t = ago(14d);
  let max_t = now();
  let dt = 1d;
  // calculate avg. eps(events per second)
  let eps = materialize (_Im_Dns
    | project TimeGenerated
    | where TimeGenerated > ago(5m)
    | count
    | extend Count = Count / 300);
  let maxSummarizedTime = toscalar (
    union isfuzzy=true 
        (
        DNS_Summarized_Logs_ip_CL 
        | where EventTime_t >= min_t
        | summarize max_TimeGenerated=max(EventTime_t)
        | extend max_TimeGenerated = datetime_add('hour', 1, max_TimeGenerated)
        ),
        (
        print(min_t)
        | project max_TimeGenerated = print_0
        )
    | summarize maxTimeGenerated = max(max_TimeGenerated) 
    );
  let summarizationexist = materialize(
    union isfuzzy=true 
        (
        DNS_Summarized_Logs_ip_CL
        | where EventTime_t > ago(1d) 
        | project v = int(2)
        ),
        (
        print int(1) 
        | project v = print_0
        )
    | summarize maxv = max(v)
    | extend sumexist = (maxv > 1)
    );
    let allData = ( union isfuzzy=true
        (
        (datatable(exists: int, sumexist: bool)[1, false]
        | where toscalar(eps) > 1000
        | join (summarizationexist) on sumexist)
        | join (
            _Im_Dns(starttime=todatetime(ago(2d)), endtime=ago(dt))
            | where TimeGenerated > maxSummarizedTime
            | summarize Count=count() by DnsQuery
            | top 1000000 by Count
            | summarize TopOneMillionDNSQuery=make_list(DnsQuery,1000000)
            | extend exists=int(1)
            )
            on exists
        | project-away exists, maxv, sum*
        ),
        (
        (datatable(exists: int, sumexist: bool)[1, false]
        | where toscalar(eps) between (501 .. 1000)
        | join (summarizationexist) on sumexist)
        | join (
            _Im_Dns(starttime=todatetime(ago(3d)), endtime=ago(dt))
            | where TimeGenerated > maxSummarizedTime
            | summarize Count=count() by DnsQuery
            | top 1000000 by Count
            | summarize TopOneMillionDNSQuery=make_list(DnsQuery,1000000)
            | extend exists=int(1)
            )
            on exists
        | project-away exists, maxv, sum*
        ),
        (
        (datatable(exists: int, sumexist: bool)[1, false]
        | where toscalar(eps) <= 500
        | join (summarizationexist) on sumexist)
        | join (
            _Im_Dns(starttime=todatetime(ago(4d)), endtime=ago(dt))
            | where TimeGenerated > maxSummarizedTime
            | summarize Count=count() by DnsQuery
            | top 1000000 by Count
            | summarize TopOneMillionDNSQuery=make_list(DnsQuery,1000000)
            | extend exists=int(1)
            )
            on exists
        | project-away exists, maxv, sum*
        ),
        (
        DNS_Summarized_Logs_ip_CL
        | where EventTime_t between (min_t .. ago(dt)) and isnotempty(DnsQuery_s)
        | project-rename
            DnsQuery=DnsQuery_s,
            Count=count__d
        | extend Count = toint(Count)
        | summarize TotalCount=toint(sum(Count)) by DnsQuery
        | top 1000000 by TotalCount
        | summarize TopOneMillionDNSQuery=make_list(DnsQuery,1000000)
        )
        );
  _Im_Dns(starttime=ago(dt),endtime=now())
  | summarize Count=count() by DnsQuery
  | where isnotempty(DnsQuery) and DnsQuery !in (allData)
  | extend DNS_0_DomainName = DnsQuery
entityMappings:
  - entityType: DNS
    fieldMappings:
      - identifier: DomainName
        columnName: DNSQuery