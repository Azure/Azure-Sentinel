id: 922c64bb-819b-4e3f-811a-0dfbff8eb667
Function:
  Title: Parser for DataminrPulseCyberAlerts
  Version: '1.0.1'
  LastUpdated: '2025-09-08'
Category: Microsoft Sentinel Parser
FunctionName: DataminrPulseCyberAlerts
FunctionAlias: DataminrPulseCyberAlerts
FunctionQuery: |
    let dummy_table = datatable(categories: dynamic, companies: dynamic, watchlistsMatchedByType: dynamic, index: string, TimeGenerated: datetime) [];
    let DataminrPulseCyberAlerts_view = view() {
    union isfuzzy=true dummy_table,
    DataminrPulse_Alerts_CL
    | mv-expand todynamic(watchlistsMatchedByType)
    | extend property = watchlistsMatchedByType["userProperties"]
    | where property["uiListType"] in ("CYBER", "DIGITAL_RISK", "THIRD_PARTY_RISK", "CYBER_PHYSICAL", "VULNERABILITY")
    | extend category = parse_json(categories)
    | mv-apply category on (summarize CategoryNames = make_list(category.name))
    | extend company = parse_json(companies)
    | mv-apply company on (summarize CompanyNames = make_list(company.name))
    | extend watchlist = parse_json(watchlistsMatchedByType)
    | mv-apply watchlist on (summarize WatchlistNames = make_list(watchlist.name))
    | summarize any(*) by index
    | extend
        EventVendor="Dataminr",
        EventProduct="Pulse",
        AlertId = column_ifexists('index', ''),
        AlertType = column_ifexists('any_alertType_name', ''),
        AvailableRelatedAlerts = column_ifexists('any_availableRelatedAlerts', real(null)),
        Caption = column_ifexists('any_headline', ''),
        Company = column_ifexists('any_companies', ''),
        CompanyNames = tostring(column_ifexists('any_CompanyNames', '')),
        Category = column_ifexists('any_categories', ''),
        CategoryNames = tostring(column_ifexists('any_CategoryNames', '')),
        Latitude = column_ifexists('any_location_latitude', real(null)),
        Longitude = column_ifexists('any_location_longitude', real(null)),
        EventLocationName = column_ifexists('any_location_name', ''),
        EventLocationPlace = column_ifexists('any_location_places', ''),
        EventLocationProbability = column_ifexists('any_location_probability', real(null)),
        EventLocationRadius = column_ifexists('any_location_radius', real(null)),
        EventSource = column_ifexists('any_headlineData_via', ''),
        EventTime = column_ifexists('any_timestamp', real(null)),
        EventVolume = column_ifexists('any_volume', real(null)),
        EmbeddedLabels = column_ifexists('any_embedded_labels', ''),
        PostLanguagae = column_ifexists('any_odsStatus_languages', ''),
        PostLink = column_ifexists('any_odsStatus_link', ''),
        PostMedia = column_ifexists('any_odsStatus_media', ''),
        PostText = column_ifexists('any_odsStatus_text', ''),
        PostTimestamp = column_ifexists('any_odsStatus_timestamp', real(null)),
        PostTranslatedText = column_ifexists('any_odsStatus_translatedText', ''),
        PublisherCategoryName = column_ifexists('any_publisherCategory_name', ''),
        RelatedTerms = column_ifexists('any_referenceTerms', ''),
        Sectors = column_ifexists('any_sectors', ''),
        SourceChannels = column_ifexists('any_odsStatus_source_channels', ''),
        SourceDisplayName = column_ifexists('any_odsStatus_source_display_name', ''),
        SourceEntityName = column_ifexists('any_odsStatus_source_entity_name', ''),
        SourceLink = column_ifexists('any_odsStatus_source_link', ''),
        SourceVerified = column_ifexists('any_odsStatus_source_verified', ''),
        SubCaptionBulletsContent = column_ifexists('any_dataMap_bullets_content', ''),
        SubCaptionBulletsMedia = column_ifexists('any_dataMap_bullets_media', ''),
        SubCaptionBulletsSource = column_ifexists('any_dataMap_bullets_source', ''),
        WatchlistsMatchedByType = column_ifexists('any_watchlistsMatchedByType', ''),
        WatchlistNames = tostring(column_ifexists('any_WatchlistNames', ''))
    | project-rename TimeGenerated = any_TimeGenerated
    | project
        TimeGenerated,
        EventVendor,
        EventProduct,
        AlertId,
        AlertType,
        AvailableRelatedAlerts,
        Caption,
        Company,
        CompanyNames,
        CategoryNames,
        Category,
        Latitude,
        Longitude,
        EventLocationName,
        EventLocationPlace,
        EventLocationProbability,
        EventLocationRadius,
        EventSource,
        EventTime,
        EventVolume,
        EmbeddedLabels,
        PostLanguagae,
        PostLink,
        PostMedia,
        PostText,
        PostTimestamp,
        PostTranslatedText,
        PublisherCategoryName,
        RelatedTerms,
        Sectors,
        SourceChannels,
        SourceDisplayName,
        SourceEntityName,
        SourceLink,
        SourceVerified,
        SubCaptionBulletsContent,
        SubCaptionBulletsMedia,
        SubCaptionBulletsSource,
        WatchlistsMatchedByType,
        WatchlistNames
    };
    DataminrPulseCyberAlerts_view