// Id: MostPrevLxHostsByIP
// DisplayName: Most Prevalent Linux Hosts With This IPAddress
// Description: Top 10 Hosts and ProcessName with this IPAddress (set time range to +-6h when running the query)
// InputEntityType: Ip
// InputFields: [[Address]]
// OutputEntityTypes: [Host]
// QueryPeriodBefore: 6h
// QueryPeriodAfter: 6h
// DataSource: #Syslog
// Tactics: #Execution, #Persistence, #Discovery, #Lateral Movement, #Collection

let GetSysLogEventsWithIP = (v_IP_Address:string){
Syslog
| where HostIP has v_IP_Address
| extend info = pack('HostIP', HostIP, 'ProcessName', ProcessName, 'SeverityLevel', SeverityLevel)
| summarize min(EventTime), max(EventTime), count(), Host_Aux_info = makeset(info) by Computer
| top 10 by count_ desc nulls last 
| project StartTimeUtc = min_EventTime, EndTimeUtc = max_EventTime, Computer, Host_Aux_info
| project-rename Host_UnstructuredName=Computer
};
// change <Address> value below
GetSysLogEventsWithIP('<Address>')