// Name: Most Prevalent Linux Hosts With This IPAddress
// Description: Top 10 Hosts and ProcessName with this IPAddress in the range of +-6h
//
// Id: 6da4fb56-d0c2-4fa9-bca1-f856ff2de1bd
//
// Entity: IPAddress
// Input: IPAddress
// Output: Hosts, ProcessName
//
// QueryPeriod: +-6h, change as needed
//
// Data Source: Syslog
//
// Tactics: #Execution, #Persistence, #Discovery, #Lateral Movement, #Collection
//
let GetSysLogEventsWithIP = (suspiciousEventTime:datetime, v_ipAddress:string){
let v_StartTime = suspiciousEventTime-6h;
let v_EndTime = suspiciousEventTime+6h;
Syslog
| where EventTime between (v_StartTime .. v_EndTime)
| where HostIP has v_ipAddress
| summarize min(EventTime), max(EventTime), count() by Computer, HostIP, ProcessName, SeverityLevel
| top 10 by count_ desc nulls last 
| project StartTimeUtc = min_EventTime, EndTimeUtc = max_EventTime, Computer, HostIP, ProcessName, SeverityLevel 
};
// change datetime value and <ipaddress> value below
GetSysLogEventsWithIP(datetime('2019-02-06T10:36:07Z'), "<ipaddress>")