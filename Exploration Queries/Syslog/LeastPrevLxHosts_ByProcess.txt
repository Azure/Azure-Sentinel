// Id: def383f2-dff3-4f5b-9416-aca8dca39812
// DisplayName: Least Prevalent Linux Hosts With This Process
// Description: Bottom 10 Hosts, IP with this process and syslog message (set time range to +-1h when running the query)
// InputEntityType: Process
// InputFields: [CommandLine]
// OutputEntityTypes: [Host]
// QueryPeriodBefore: 1h
// QueryPeriodAfter: 1h
// DataSource: #Syslog
// Tactics: #Execution, #Persistence, #Discovery, #Lateral Movement, #Collection

let GetSysLogEventsWithProcess = (v_Process_CommandLine:string){
let tempFullPath = tostring(split(v_Process_CommandLine, ' ')[0]);
let tempFullPath2 = iff(tempFullPath startswith '"', substring(tempFullPath, 1, strlen(tempFullPath)-2), tempFullPath);
let v_Process_ImageFile_FullPath = iff(tempFullPath2 startswith "\\??\\", substring(tempFullPath2, 4, strlen(tempFullPath2)-1), tempFullPath2);
Syslog
| where ProcessName has v_Process_ImageFile_FullPath
| summarize min(EventTime), max(EventTime), count() by Computer
| top 10 by count_ asc nulls last 
| project min_EventTime, max_EventTime, Computer
| project-rename Host_UnstructuredName=Computer
};
// change <CommandLine> value below
GetSysLogEventsWithProcess('<CommandLine>')