// Name: Least Prevalent Linux Hosts With This Process
// Description: Bottom 10 Hosts, IP with this process and syslog message in the range of +-1h
//
// Id: 32d10b7e-d2cb-4bb1-bc15-e95697f93791
//
// Entity: Process
// Input: ProcessName
// Output: Hosts
//
// QueryPeriod: +-1h, change as needed
//
// Data Source: Syslog
//
// Tactics: #Execution, #Persistence, #Discovery, #Lateral Movement, #Collection
//
let GetSysLogEventsWithProcess = (suspiciousEventTime:datetime, v_Process:string){
let v_StartTime = suspiciousEventTime-1h;
let v_EndTime = suspiciousEventTime+1h;
Syslog
| where EventTime between (v_StartTime .. v_EndTime)
| where ProcessName has v_Process
| summarize min(EventTime), max(EventTime), count() by Computer, HostName, HostIP, ProcessName, SyslogMessage
| top 10 by count_ asc nulls last 
| project min_EventTime, max_EventTime, Computer, HostIP, ProcessName, SyslogMessage
};
// change datetime value and <processname> value below
GetSysLogEventsWithProcess(datetime('2019-01-30T10:36:07Z'), "<processname>")