// Name: Linux - Process Rare for Host
// Description: Lookup your interesting process and see on what hosts it is rare.  
// Less than 1% of the average and less than a count of 100 on a given host or less than a 14 count on a given host over 7 days
//
// Entity: Process
// Input: ProcessName
// Output: Hosts
//
// QueryPeriod: -7d, change as needed
//
// Data Source: Syslog
//
// Techniques: #Execution, #Persistence, #Discovery, #Lateral Movement, #Collection
//
let GetHostWithRareProcess = (suspiciousEventTime:datetime, v_Process:string){
let v_StartTime = suspiciousEventTime-7d;
let v_EndTime = suspiciousEventTime-1m;
Syslog
| where EventTime between (v_StartTime .. v_EndTime)
| where ProcessName has v_Process
| summarize count() by ProcessName, Computer
| join kind= inner (
    Syslog
	| where EventTime >= ago(30d)
	| where ProcessName has v_Process
    | summarize count() by ProcessName, Computer
    | summarize avg(count_) by ProcessName
    ) on ProcessName
// attempt to control for noise, but not miss low prevalence count with low average count
| where count_ < 14 or (count_ <= avg_count_*0.01 and count_ < 100)
| join kind= inner (
    Syslog
    | where EventTime between (v_StartTime .. v_EndTime)
	| where ProcessName has v_Process
    | summarize min(EventTime), max(EventTime) by Computer, HostIP, ProcessName
) on ProcessName, Computer
| project min_EventTime, max_EventTime, EventCount = count_, AvgEventCountAllSystems = avg_count_, Computer, HostIP, ProcessName
| top 10 by EventCount asc nulls last 
| project min_EventTime, max_EventTime, Computer, HostIP, ProcessName, EventCount, AvgEventCountAllSystems 
};
// change datetime value and <processname> value below
GetHostWithRareProcess(datetime('2019-01-31T10:36:07Z'), "<processname>")