// Id: bc6c7cc9-da18-4afd-8fda-d201f13b54a4
// DisplayName: IPAddress associated with Top 10 Accounts
// Description: The top 10 count of user account logon attempts from a given IPAddress during a given time period based on SigninLogs (set time range to +-1d when running the query)
// InputEntityType: Ip
// InputFields: [Address]
// OutputEntityTypes: [Account]
// QueryPeriodBefore: 1d
// QueryPeriodAfter: 1d
// DataSource: #SigninLogs
// Tactics: #Persistence, #Discovery, #LateralMovement, #Collection

let GetMostPrevUsersbyIP = (v_IP_Address:string){
SigninLogs
| where IPAddress contains v_IP_Address
| extend RemoteHost = tolower(tostring(parsejson(DeviceDetail.['displayName'])))
| extend OS = DeviceDetail.operatingSystem, Browser = DeviceDetail.browser
| extend StatusCode = tostring(Status.errorCode), StatusDetails = tostring(Status.additionalDetails)
| extend State = tostring(LocationDetails.state), City = tostring(LocationDetails.city)
| extend info = pack("AppDisplayName", AppDisplayName, "ClientAppUsed", ClientAppUsed, "Browser", tostring(Browser), "IPAddress", IPAddress, "ResultType", ResultType, "ResultDescription", ResultDescription, "Location", Location, "State", State, "City", City, "StatusCode", StatusCode, "StatusDetails", StatusDetails)
| summarize min(TimeGenerated), max(TimeGenerated), count(), Account_Aux_info = makeset(info) by RemoteHost , UserDisplayName, tostring(OS), UserPrincipalName, AADTenantId, UserId
| top 10 by count_ desc nulls last 
| project StartTimeUtc = min_TimeGenerated, EndTimeUtc = max_TimeGenerated, RemoteHost, UserDisplayName, OS, UserPrincipalName, AADTenantId, UserId, Account_Aux_info
| project-rename Account_UnstructuredName=UserPrincipalName, Account_DisplayName=UserDisplayName, Account_AadTenantId=AADTenantId, Account_AadUserId=UserId, Account_Host_UnstructuredName=RemoteHost, Account_Host_OsVersion=OS
};
// change <Address> value below
GetMostPrevUsersbyIP('<Address>')