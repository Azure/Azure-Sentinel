// Id: fdb3e714-c036-4708-a0eb-6ae10a1912a1
// DisplayName: IPAddress associated with bottom 10 Accounts
// Description: The bottom 10 count of user account logon attempts from a given IPAddress during a given time period based on SigninLogs (set time range to +-1d when running the query)
// InputEntityType: Ip
// InputFields: [Address]
// OutputEntityTypes: [Account]
// QueryPeriodBefore: 1d
// QueryPeriodAfter: 1d
// DataSource: #SigninLogs
// Tactics: #Persistence, #Discovery, #LateralMovement, #Collection

let GetLeastPrevUsersbyIP = (v_IP_Address:string){
SigninLogs
| where IPAddress contains v_IP_Address
| extend RemoteHost = tolower(tostring(parsejson(DeviceDetail.['displayName'])))
| extend OS = DeviceDetail.operatingSystem, Browser = DeviceDetail.browser
| extend StatusCode = tostring(Status.errorCode), StatusDetails = tostring(Status.additionalDetails)
| extend State = tostring(LocationDetails.state), City = tostring(LocationDetails.city)
| summarize min(TimeGenerated), max(TimeGenerated), count() by RemoteHost , UserDisplayName, tostring(OS), UserPrincipalName, AADTenantId, UserId
| top 10 by count_ asc nulls last 
| project StartTimeUtc = min_TimeGenerated, EndTimeUtc = max_TimeGenerated, RemoteHost, UserDisplayName, OS, UserPrincipalName, AADTenantId, UserId
| project-rename Account_UnstructuredName=UserPrincipalName, Account_DisplayName=UserDisplayName, Account_AadTenantId=AADTenantId, Account_AadUserId=UserId, Account_Host_UnstructuredName=RemoteHost, Account_Host_OsVersion=OS
};
// change <Address> value below
GetLeastPrevUsersbyIP('<Address>')