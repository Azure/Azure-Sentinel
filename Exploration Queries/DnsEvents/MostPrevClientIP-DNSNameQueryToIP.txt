// Id: MostPrevClientIPDNSNameQueryToIP
// DisplayName: DNS Name Lookup Query from most prevalent ClientIP to remote IP Address
// Description: Summary of Top 10 Client IP and Domain Names for a given remote IPAddress from DnsEvent Lookup Query data (set time range to +-3h when running the query)
// InputEntityType: Ip
// InputFields: [[Address]]
// OutputEntityTypes: [Ip]
// QueryPeriodBefore: 3h
// QueryPeriodAfter: 3h
// DataSource: #DnsEvents
// Tactics: #CommandAndControl, #Exfiltration

let GetAllIPByClientIP = (v_IP_Address:string){
DnsEvents
| where SubType == 'LookupQuery'
| where IPAddresses contains v_IP_Address
| summarize min(TimeGenerated), max(TimeGenerated), makeset(Name), count() by ClientIP, IPAddresses
| project StartTimeUtc = min_TimeGenerated, EndTimeUtc = max_TimeGenerated, ClientIP, DomainNames = set_Name, IPAddresses, count_ 
| top 10 by count_ desc nulls last
| project StartTimeUtc, EndTimeUtc, ClientIP, IPAddresses, DomainNames
| project-rename IP_Address=ClientIP
};
// change <Address> value below
GetAllIPByClientIP('<Address>')