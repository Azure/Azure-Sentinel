// Name: WireData Least Prevalent Outbound Connections by Process
// Description: Bottom 10 (least prevalent) for WireData Outbound event by a given process in the range of +-1h
//
// Id: 54c19014-7d6f-4489-9db6-5ac840c26f8f
//
// Entity: Process
// Input: Process, ProcessName
// Output: Computer, Process, IP
//
// QueryPeriod: +-30m
//
// Data Source: WireData
//
// Tactics: #Collection, #Exfiltration, #CommandAndControl
//
let GetWireDataOutboundWithProcess = (suspiciousEventTime:datetime, v_Process:string){
let v_StartTime = suspiciousEventTime-1d;
let v_EndTime = suspiciousEventTime+1d;
WireData
| where TimeGenerated between (v_StartTime .. v_EndTime)
| where Direction == "Outbound" 
| where ProcessName has v_Process
| summarize min(SessionStartTime), count() by Computer, ProcessName , LocalIP, LocalPortNumber, RemoteIP, Direction, ApplicationProtocol, MaliciousIP
| top 10 by count_ asc
| project min_SessionStartTime, Computer, ProcessName , LocalIP, LocalPortNumber, RemoteIP, Direction, ApplicationProtocol, MaliciousIP, count_
};
// change datetime value and <processname> value below
GetWireDataOutboundWithProcess(datetime('2019-01-30T10:36:07Z'), "<processname>")