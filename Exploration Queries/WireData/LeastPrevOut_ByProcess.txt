// Id: 39df618a-684d-402d-b096-6f505a8e741e
// DisplayName: WireData Least Prevalent Outbound Connections by Process
// Description: Bottom 10 (least prevalent) for WireData Outbound event by a given process (set time range to +-1h when running the query)
// InputEntityType: Process
// InputFields: [CommandLine]
// OutputEntityTypes: [Ip, Host]
// QueryPeriodBefore: 1h
// QueryPeriodAfter: 1h
// DataSource: #WireData
// Tactics: #Collection, #Exfiltration, #CommandAndControl

let GetWireDataOutboundWithProcess = (v_Process_CommandLine:string){
let tempFullPath = tostring(split(v_Process_CommandLine, ' ')[0]);
let tempFullPath2 = iff(tempFullPath startswith '"', substring(tempFullPath, 1, strlen(tempFullPath)-2), tempFullPath);
let v_Process_ImageFile_FullPath = iff(tempFullPath2 startswith "\\??\\", substring(tempFullPath2, 4, strlen(tempFullPath2)-1), tempFullPath2);
WireData
| where Direction == 'Outbound'
| where ProcessName has v_Process_ImageFile_FullPath
| extend info = pack("ProcessName", ProcessName, "LocalIP", LocalIP, "LocalPortNumber", LocalPortNumber, "Direction", Direction, "ApplicationProtocol", ApplicationProtocol, "MaliciousIP", MaliciousIP)
| summarize min(SessionStartTime), count(), IP_Aux_info = makeset(info) by Computer, RemoteIP, RemoteIPCountry, RemoteIPLatitude, RemoteIPLongitude
| extend Host_Aux_info = IP_Aux_info
| top 10 by count_ asc
| project min_SessionStartTime, Computer, RemoteIP, RemoteIPCountry, RemoteIPLatitude, RemoteIPLongitude, IP_Aux_info, Host_Aux_info
| project-rename IP_Address=RemoteIP, IP_Location_CountryName=RemoteIPCountry, IP_Location_Latitude=RemoteIPLatitude, IP_Location_Longitude=RemoteIPLongitude, Host_UnstructuredName=Computer
};
// change <CommandLine> value below
GetWireDataOutboundWithProcess('<CommandLine>')