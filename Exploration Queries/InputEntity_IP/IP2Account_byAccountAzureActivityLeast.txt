// Id: AccountActivityByIPLeastActive
// DisplayName: Least Active Accounts On Azure From IP
// Description: Least active accounts on azure from IP during the range of -12h and +12h
// InputEntityType: IP
// InputFields: [[Address]]
// OutputEntityTypes: [Account]
// QueryPeriodBefore: 12h
// QueryPeriodAfter: 12h
// DataConnector: #AzureActivity; DataTypes: #AzureActivity
// Tactics: #Exfiltration, #CommandAndControl, #Collection

let AccountActivity_byIP = (v_IP_Address:string){
AzureActivity
| where Caller != '' and CallerIpAddress == v_IP_Address
| summarize Account_Aux_StartTime = min(TimeGenerated), 
			  Account_Aux_EndTime = max(TimeGenerated), 
			  Count = count() by 
			  Caller, TenantId
| top 10 by Count asc nulls last 
| extend UPN = iff(Caller contains '@', Caller, ''), Account_AadUserId = iff(Caller !contains '@', Caller,'')
| extend Account_Name = split(UPN,'@')[0] , Account_UPNSuffix = split(UPN,'@')[1]
| project Account_Name, Account_UPNSuffix, Account_AadUserId, Account_AadTenantId=TenantId, Account_Aux_StartTime , Account_Aux_EndTime 
};
AccountActivity_byIP('<Address>')