// Id: 97a1d515-abf2-4231-9a35-985f9de0bb91
// DisplayName: Most Active Accounts On Azure From IP
// Description: Most active accounts on azure from IP during the range of -12h and +12h
// InputEntityType: IP
// InputFields: [Address]
// OutputEntityTypes: [Account]
// QueryPeriodBefore: 12h
// QueryPeriodAfter: 12h
// DataSource: AzureActivity
// Tactics: #Exfiltration, #CommandAndControl, #Collection

let AccountActivity_byIP = (v_IP_Address:string){
AzureActivity
| where Caller != '' and CallerIpAddress == v_IP_Address
| summarize Account_Aux_StartTime = min(TimeGenerated), 
			  Account_Aux_EndTime = max(TimeGenerated), 
			  Count = count() by 
			  Caller, TenantId
| top 10 by Count desc nulls last 
| extend UPN = iff(Caller contains '@', Caller, ''), Account_AadUserId = iff(Caller !contains '@', Caller,'')
| extend Account_Name = split(UPN,'@')[0] , Account_UPNSuffix = split(UPN,'@')[1]
| project Account_Name, Account_UPNSuffix, Account_AadUserId, Account_AadTenantId=TenantId, Account_Aux_StartTime , Account_Aux_EndTime 
};
AccountActivity_byIP(<Address>)