// Name: Office Activity IP Address by Account
// Description: Summary of Accounts for a given ClientIP on Office Activity data.
//
// Entity: IPAddress
// Input: Compromised Account
// Output: UserAccount
//
// QueryPeriod:  +-12h
//
// Data Source: #OfficeActivity
//
// Techniques: #Persistence, #Discovery, #LateralMovement, #Collection
//
let GetAllAccountByIP = (suspiciousEventTime:datetime, v_IPAddress:string){
let v_StartTime = suspiciousEventTime-12h;
let v_EndTime = suspiciousEventTime+12h;
OfficeActivity 
| where TimeGenerated between (v_StartTime .. v_EndTime)
| where ClientIP contains v_IPAddress
| summarize min(TimeGenerated), max(TimeGenerated), count() by UserId, ClientIP, UserType, Operation, OfficeWorkload, ResultStatus
| project StartTimeUtc = min_TimeGenerated, EndTimeUtc = max_TimeGenerated, UserId, ClientIP, UserType, Operation, OfficeWorkload, ResultStatus, count_ 
| top 10 by count_ desc nulls last
};
// change datetime value and <ipaddress> value below
GetAllAccountByIP(datetime('2019-01-30T10:36:07Z'), "<ipaddress>")