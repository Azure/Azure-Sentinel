// Name: Office Activity Account logons from IP
// Description: Summary of IP addresses for logins based on Office Activity data.
//
// Entity: Account
// Input: Compromised Account
// Output: IP address
//
// QueryPeriod:  +-12h
//
// Data Source: #OfficeActivity
//
// Techniques: #Persistence, #Discovery, #LateralMovement, #Collection
//
let GetAllIPbyAccount = (suspiciousEventTime:datetime, v_Account:string){
let v_StartTime = suspiciousEventTime-12h;
let v_EndTime = suspiciousEventTime+12h;
OfficeActivity 
| where TimeGenerated between (v_StartTime .. v_EndTime)
| where UserId contains v_Account
| summarize min(TimeGenerated), max(TimeGenerated) by UserId, ClientIP, UserType, Operation, OfficeWorkload, ResultStatus
| project StartTimeUtc = min_TimeGenerated, EndTimeUtc = max_TimeGenerated, UserId, ClientIP, UserType, Operation, OfficeWorkload, ResultStatus
| top 10 by StartTimeUtc desc nulls last 
};
// change datetime value and <username> value below
GetAllIPbyAccount (datetime('2019-01-30T10:36:07Z'), "<username>")