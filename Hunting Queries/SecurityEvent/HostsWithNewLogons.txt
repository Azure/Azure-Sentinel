// Name: Hosts with new logons
// Description: Shows new accounts that have logged onto a host for the first time - this may clearly be benign activity but an account logging onto multiple hosts for the first time can also be used to look for evidence of that account being used to move laterally across a network.
//
// Id: 62e2df59-1535-4c8e-ac6c-c91faeed0179
//
// Tactics: #LateralMovement,
//
let LogonEvents=() { 
let logonSuccess=SecurityEvent 
| where EventID==4624 
| project  TimeGenerated, ComputerName=Computer, AccountName=TargetUserName, AccountDomain=TargetDomainName, IpAddress, ActionType='Logon';
let logonFail=SecurityEvent 
| where EventID==4625 
| project TimeGenerated, ComputerName=Computer, AccountName=TargetUserName, AccountDomain=TargetDomainName, IpAddress, ActionType='LogonFailure';
logonFail 
| union logonSuccess
};
LogonEvents 
| where TimeGenerated >= ago(30d) 
| where ActionType == 'Logon' 
| summarize count() by ComputerName, AccountName 
| join kind=leftouter ( 
LogonEvents 
| where TimeGenerated >= ago(1d) 
| where ActionType == 'Logon' 
| summarize count() by ComputerName, AccountName 
) on ComputerName 
| where AccountName != AccountName1 
| summarize HostCount=dcount(ComputerName) by AccountName1 
| extend Name = AccountName1 
| project Name , HostCount
| extend AccountCustomEntity = Name
