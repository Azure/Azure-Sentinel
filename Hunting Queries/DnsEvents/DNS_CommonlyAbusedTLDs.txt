// Name: DNS commonly abused TLDs
// Id: 8e9c4680-8c0b-4885-b183-3b09efd8fc2c
//
// Description: some TLDs are more commonly associated with malware for a range of reasons - including how 
// easy domains on these TLDs are to obtain. Many of these may be undesirable from an enterprise policy
// perspective. 
// The clientCount column provides an initial insight into how widespread the domain usage is across the estate.
//
// Data Source: DnsEvents
//
// Techniques: #C2, #Exfiltration
//
DnsEvents
| where TimeGenerated >= ago(1d)
| where Name contains "." 
| project tld = tostring(split(Name, ".")[-1]), ClientIP , Name
| where tld in ( "click", "club", "download",  "xxx", "xyz")
| summarize clientCount = dcount(ClientIP) by tld, Name
| order by tld asc, clientCount desc

