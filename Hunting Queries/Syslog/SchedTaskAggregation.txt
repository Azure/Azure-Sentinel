// Name: Scheduled Task Aggregation
// Description: This query aggregates information about all of the scheduled tasks (cron jobs) and presents the data in a chart
//              The aggregation is done based on unique user-commandline pairs. It returns how many times a command line has
//              been run from a particular user, how many computers that pair has run on, and what percentage that is of the
//              total number of computers in the tenant.
//
// Id: eb09da09-6f6c-4502-bf74-f7b9f1343539
//
// Data Source: #Syslog, #cron
//
// Techniques: #Persistence, #Execution
// 
// Change startdate below if you want a different timespan
let startdate = ago(7d);
// Pull messages from Syslog-cron where the process name is "CRON" or "CROND", the severity level is info, and the SyslogMessage contains "CMD".
// It also parses out the user and commandline from the message.
let RawCommands = Syslog 
| where TimeGenerated >= startdate
| where Facility == "cron" 
| where SeverityLevel == "info" 
| where ProcessName == "CRON" or ProcessName == "CROND"  
| where SyslogMessage contains "CMD " 
| project TenantId, TimeGenerated, Computer, SeverityLevel, ProcessName, SyslogMessage
| extend TrimmedSyslogMsg = trim_end(@"\)", SyslogMessage)
| parse TrimmedSyslogMsg with * "(" user  ") CMD (" cmdline 
| project TenantId, TimeGenerated, Computer, user, cmdline; 
// Count how many times a particular commandline has been seen based on unique Computer, User, and cmdline sets
let CommandCount = RawCommands
| summarize count(cmdline) by Computer, user, cmdline
| project Computer, user, cmdline, CmdlineCount = count_cmdline ; 
// Count how many computers have run a particular user and cmdline pair
let DistComputerCount = RawCommands
| summarize dcount(Computer) by TenantId, user, cmdline
| project TenantId, user, cmdline, ComputerCount = dcount_Computer ; 
// Join above counts based on user and commandline pair
let CommandSummary = CommandCount | join (DistComputerCount) on user, cmdline
| project TenantId, user, CmdlineCount, ComputerCount, cmdline ;
// Count the total number of computers reporting cron messages in the tenant
let TotalComputers = Syslog
| where Facility == "cron"
| summarize dcount(Computer) by TenantId ;
// Join the previous counts with the total computers count. Calculate the percentage of total computers value.
let FinalSummary = CommandSummary | join kind= leftouter (TotalComputers) on TenantId
| project user, TimesCmdlineSeen = CmdlineCount, CompsThatHaveRunCmdline = ComputerCount, AsPercentOfTotalComps = round(100 * (toreal(ComputerCount)/toreal(dcount_Computer)),2), cmdline
| order by user asc, TimesCmdlineSeen desc;
FinalSummary 
