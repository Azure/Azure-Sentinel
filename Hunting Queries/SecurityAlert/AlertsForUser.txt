// Name: Alerts related to account
// Description: Any Alerts that fired related to a given account during the range of +6h and -3d
//
// Entity: User
// Input: Account, UserName
// Output: Alerts
//
// QueryPeriod: +6h and -3d default, change as needed
//
// Data Source: SecurityAlert
//
// Tactics: #Persistence, #Discovery, #LateralMovement, #Collection
//
let GetAllAlertsForUser = (suspiciousEventTime:datetime, v_User:string){
//-3d and +6h as some alerts fire after accumulation of events
let v_StartTime = suspiciousEventTime-3d;
let v_EndTime = suspiciousEventTime+6h;
SecurityAlert
| where TimeGenerated between (v_StartTime .. v_EndTime)
// expand JSON properties
| extend Extprop = parsejson(ExtendedProperties)
| extend Computer = toupper(tostring(Extprop["Compromised Host"]))
| extend Account =  tolower(tostring(Extprop["User Name"]))
| where Account contains v_User
| project TimeGenerated, AlertName, Computer, Account, ExtendedProperties 
};
// change datetime value and username value below
GetAllAlertsForUser(datetime('2019-01-20T10:02:51.000'), toupper("<username>"))
