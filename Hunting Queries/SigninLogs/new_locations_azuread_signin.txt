// Name: Azure Active Directory signins from new locations.
//
// Id: 41fa6e2d-afe9-4398-9356-cec3a927e44e
//
// Description: New Azure Active Directory signin locations today versus historical Azure Active Directory signin data
// In the case of password spraying or brute force attacks one might see authentication attempts for many accounts from a new location.
//
// DataSource: #SigninLogs
//
// Tactics: #InitialAccess
//
SigninLogs
| where TimeGenerated >= ago(1d)
| summarize perIdentityAuthCount=count() by Identity,  locationString= strcat(tostring(LocationDetails["countryOrRegion"]), "/", tostring(LocationDetails["state"]), "/", tostring(LocationDetails["city"]), ";" , tostring(LocationDetails["geoCoordinates"]))
| summarize distinctAccountCount = count(), identityList=makeset(Identity)  by locationString
| extend identityList = iff(distinctAccountCount<10, identityList, "multiple (>10)")
| join kind= anti (
    SigninLogs
    | where TimeGenerated < ago(1d)
    | where TimeGenerated >= ago(14d)
    | project   locationString= strcat(tostring(LocationDetails["countryOrRegion"]), "/", tostring(LocationDetails["state"]), "/", 
        tostring(LocationDetails["city"]), ";" , tostring(LocationDetails["geoCoordinates"]))
    | summarize priorCount = count() by locationString) on locationString
| where distinctAccountCount > 1 // select threshold above which #new accounts from a new location is deemed suspicious
// Could extend this query to show previous signin locations for the users in result set.

