// Name: Inactive or new account signins
//
// Id: 847c2652-547d-4d5f-9b71-d2f8d81eac62
//
// Description: query for accounts seen signing in for the first time - these could be associated
// with stale/inactive accounts that ought to have been deleted but weren't - and have 
// subseuqently been compromised. 
// Results for user accounts created in the last 7 days are filtered out.
//
// DataSource: #SigninLogs
//
// Tactics: #InitialAccess
//
// Inactive accounts that sign in - first-time logins for accounts created in last 7 days are filtered out
SigninLogs
// successful sign-in
| where TimeGenerated >= ago(1d)
| where ResultType == 0
| summarize loginCountToday=count() by UserPrincipalName, Identity
| join kind=leftanti (
   SigninLogs
   // historical successful sign-in
   | where TimeGenerated < ago(1d)
   | where TimeGenerated >= ago(14d)
   | where ResultType == 0
   | summarize by UserPrincipalName, Identity
) on UserPrincipalName 
| join kind= leftanti (
   // filter out newly created user accounts
   AuditLogs
   | where TimeGenerated >= ago(7d)
   | where OperationName == "Add user" 
   // Normalize to lower case in order to match against equivalent UPN in Signin logs
   | extend NewUserPrincipalName = tolower(extractjson("$.userPrincipalName", tostring(TargetResources[0]), typeof(string)))
) on $left.UserPrincipalName == $right.NewUserPrincipalName 
| extend AccountCustomEntity = UserPrincipalName
