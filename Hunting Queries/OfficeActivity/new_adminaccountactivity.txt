// Name: New Admin account activity seen which was not seen historically
//
// Description: This will help you discover any new admin account activity which was seen and were not seen historically. 
// Any new accounts seen in the results can be validated and investigated for any suspicious activities.
//
// Id: 723c5f46-133f-4f1e-ada6-5c138f811d75
//
// DataSource: #OfficeActivity
//
// Tactics:  #InitialAccess
//
let historicalActivity=
OfficeActivity
| where TimeGenerated between(ago(30d)..ago(7d))
| where RecordType=="ExchangeAdmin" and UserType in ("Admin","DcAdmin")
| summarize historicalCount=count() by UserId;
let recentActivity = OfficeActivity
| where TimeGenerated > ago(1d)
| where UserType in ("Admin","DcAdmin")
| summarize recentCount=count() by UserId;
recentActivity | join kind = leftanti (
   historicalActivity
) on UserId
| project UserId,recentCount
| order by recentCount asc, UserId
| join kind = rightsemi 
(OfficeActivity 
| where TimeGenerated >= ago(1d) 
| where RecordType == "ExchangeAdmin" | where UserType in ("Admin","DcAdmin")) 
on UserId
| summarize count(), min(TimeGenerated), max(TimeGenerated) by RecordType, Operation, UserType,UserId, OriginatingServer,ResultStatus
| extend AccountCustomEntity = UserId
