// Name: sharepoint downloads from devices associated with previously unseen user agents.
//
// Description: tracking via user agent is one way to differentiate between types of connecting device. 
// In homogeneous enterprise environments the user agent associated with an attacker device may stand out as unusual. 
//
// Data source: OfficeActivity
//
// Techniques: #Exfiltration
//
let historicalActivity=
OfficeActivity
| where  RecordType == "SharePointFileOperation"
| where Operation in ("FileDownloaded", "FileUploaded")
| where TimeGenerated between(ago(30d)..ago(7d))
| summarize historicalCount=count() by UserAgent;
let recentActivity = OfficeActivity
| where  RecordType == "SharePointFileOperation"
| where Operation in ("FileDownloaded", "FileUploaded")
| where TimeGenerated > ago(1d) 
| summarize recentCount=count() by UserAgent;
recentActivity | join kind = leftanti (
   historicalActivity 
) on UserAgent
| project RecordType = sz_SharePointFileOperation, UserAgent, recentCount
| order by recentCount asc, UserAgent; 
