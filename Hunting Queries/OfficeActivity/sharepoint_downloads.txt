// Name: New user agents associated with a clientIP for sharepoint file uploads/downloads
//
// Id: e8ae1375-4640-430c-ae8e-2514d09c71eb
//
// Description: New user agents associated with a clientIP for sharepoint file uploads/downloads. 
//
// DataSource: #OfficeActivity
//
// Tactics: #Exfiltration
//
let historicalUA=
OfficeActivity
| where  RecordType == "SharePointFileOperation"
| where Operation in ("FileDownloaded", "FileUploaded")
| where TimeGenerated between(ago(30d)..ago(7d))
| summarize by ClientIP, UserAgent;
let recentUA = OfficeActivity
| where  RecordType == "SharePointFileOperation"
| where Operation in ("FileDownloaded", "FileUploaded")
| where TimeGenerated > ago(1d) 
| summarize by ClientIP, UserAgent;
recentUA | join kind=leftanti (
   historicalUA 
) on ClientIP, UserAgent
// Some OfficeActivity records do not contain ClientIP information - exclude these for fewer results:
| where not(isempty(ClientIP)) | extend IPCustomEntity = ClientIP; 
