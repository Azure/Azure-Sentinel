let ProcessCreationEvents=() {
    let processEvents=SecurityEvent
    | where EventID==4688
    | project TimeGenerated, ComputerName=Computer, AccountName=SubjectUserName, AccountDomain=SubjectDomainName, FileName=tostring(split(NewProcessName, '\\\\')[-1]), ProcessCommandLine = CommandLine, InitiatingProcessFileName=ParentProcessName;
    processEvents;
};
// Daily summary of cscript activity – extracting script name and parameters from commandline:
ProcessCreationEvents 
| where FileName =~ "cscript.exe"
| project removeSwitches = replace(@"/+[a-zA-Z0-9:]+", "", ProcessCommandLine) // remove commandline switches
| project CommandLine = trim(@"[a-zA-Z0-9\\\\:""]*cscript(.exe)?("")?(\\s)+", removeSwitches) // remove the leading cscript.exe process name 
// extract the script name: 
| project ScriptName= iff(CommandLine startswith @"""", 
    extract(@"([:\\\\a-zA-Z_\\-\\s0-9\\.()]+)(""?)", 0, CommandLine), // handle case where script name is enclosed in " characters
    extract(@"([:\\\\a-zA-Z_\\-0-9\\.()]+)(""?)", 0, CommandLine)),  // handle case where script name is not enclosed in quotes                    
    CommandLine 
| project ScriptName=trim(@"""", ScriptName) , ScriptNameLength=strlen(ScriptName), CommandLine 
// extract remainder of commandline as script parameters: 
| project ScriptName, ScriptParams = iff(ScriptNameLength < strlen(CommandLine), substring(CommandLine, ScriptNameLength +1), "")
| summarize by ScriptName, ScriptParams