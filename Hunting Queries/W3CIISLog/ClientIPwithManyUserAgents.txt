// Name: Same IP address with multiple csUserAgent
//
// Id: 4edbb420-2df7-4089-9906-c335f065803e
//
// Description: This alerts when the same client IP (cIP) is connecting with more than 1 but less than 15 different useragent string (csUserAgent) in less than 1 hour.
// We limit to 50 or less connections to avoid high traffic sites.
// This may indicate malicious activity as this is a method of probing an environment.
//
// Status code mappings for your convenience
// IIS status code mapping - https://support.microsoft.com/en-us/help/943891/the-http-status-code-in-iis-7-0-iis-7-5-and-iis-8-0
// Win32 Status code mapping - https://msdn.microsoft.com/en-us/library/cc231199.aspx
//
// DataSource: W3CIISLog
//
// Tactic: #Discovery
//
let timeFrame = ago(1h);
W3CIISLog
| where TimeGenerated >= timeFrame
| where scStatus !startswith "20" and scStatus !startswith "30" and cIP !startswith "192.168." and cIP != sIP and cIP != "::1"
| summarize StartTimeUtc = min(TimeGenerated), EndTimeUtc = max(TimeGenerated), makeset(csUserAgent), ConnectionCount = count() by Computer, sSiteName, sIP, sPort, cIP, csMethod
| extend csUserAgentPerIPCount = arraylength(set_csUserAgent)
| where  csUserAgentPerIPCount between ( 2 .. 15 ) and ConnectionCount <=50