// Name: Rare User Agent strings
// Description: This will check for Rare User Agent strings over the last 3 days.  This can indicate potential probing of your IIS servers.
//
// Id: 3de523b5-9608-43d5-801e-732c741dd82e
//
// DataSource: #W3CIISLog
//
// Techniques: #Probing
//
let timeframe = 3d;
W3CIISLog | where TimeGenerated >= ago(timeframe)
// The below line can be used to exclude local IPs if these create noise
//| where cIP !startswith "192.168." and cIP != "::1"
| where isnotempty(csUserAgent) and csUserAgent !in~ ("-", "MSRPC")
| extend csUserAgent_size = string_size(csUserAgent)
| project TimeGenerated, sSiteName, sPort, csUserAgent, csUserAgent_size, csUserName , csMethod, csUriStem, sIP, cIP, scStatus, scSubStatus, scWin32Status, RemoteIPCountry, MaliciousIP
| join (
    W3CIISLog | where TimeGenerated >= ago(timeframe) 
	// The below line can be used to exclude local IPs if these create noise
    //| where cIP !startswith "192.168." and cIP != "::1"
    | where isnotempty(csUserAgent) and csUserAgent !in~ ("-", "MSRPC") 
    | extend csUserAgent_size = string_size(csUserAgent)
    | summarize csUserAgent_count = count() by bin(csUserAgent_size, 1)
    | top 20 by csUserAgent_count asc nulls last 
) on csUserAgent_size
| project TimeGenerated, sSiteName, sPort, sIP, cIP, csUserAgent, csUserAgent_size, csUserAgent_count, csUserName , csMethod, csUriStem, scStatus, scSubStatus, scWin32Status, RemoteIPCountry, MaliciousIP
